/* Generate Lua code that fills the left-side matrix entries
   corresponding to the discretization of the generalized Poisson equation
      nabla_i D^ij nabla_j phi  = - rho
   with recovery DG in the 2D interior cells. */

kill(all)$
load("recovery")$

dirs : [x, y] $
basisNm : ["Ser", "Tensor"] $
Jxx : 4/dx[1]^2 $
Jyy : 4/dx[2]^2 $
Jxy : 4/dx[1]/dx[2] $
C : 0 $

isTop : false $
isBottom : false $
isLeft : false $
isRight : false $

for polyOrder : 1 thru 1 do (
  for bIdx : 1 thru min(length(basisNm),polyOrder) do (
    bnm  : if polyOrder=1 then "" else basisNm[bIdx],
    fname : sconcat("~/.max-out/discontGenPoisson", bnm,
                    "Stencil2D",
                    if isTop then "_T" else "",
                    if isBottom then "_B" else "",
                    if isRight then "_R" else "",
                    if isLeft then "_L" else "",
                    "_", polyOrder, "p.lua"),
    fh : openw(fname),
    printf(fh, "local Lin = require \"Lib.Linalg\"~%"),
    printf(fh, "local function stencilFn(dx,~%"),
    printf(fh, "                         DxxC, DyyC, DxyC,~%"),
    printf(fh, "                         DxxLF, DxyLF,~%"),
    printf(fh, "                         DxxRF, DxyRF,~%"),
    printf(fh, "                         DyyBF, DxyBF,~%"),
    printf(fh, "                         DyyTF, DxyTF,~%"),
    printf(fh, "                         bcDx, bcNx, bcValx,~%"),
    printf(fh, "                         bcDy, bcNy, bcValy)~%"),
    printf(fh, "  local _M = {}~%~%"),
    
    ba : getBasis(sconcat("basis-precalc/basis", basisNm[bIdx], "2x"),
      polyOrder),
    numBasis : length(ba),
    bax : getBasis(sconcat("basis-precalc/basis", basisNm[bIdx], "1x"),
      polyOrder),
    bay : subst(x=y, bax),

    /* Volume ----------------------------------------------------- */
    recfx : if isRight then
              calcRecov1CellGen(basisNm[bIdx], x, dirs, polyOrder, C,
                dg(fL),
                dg(fC),
                bc(D=bcDx, N=bcNx, val=bcValx))
            elseif isLeft then
              calcRecov1CellGen(basisNm[bIdx], x, dirs, polyOrder, C,
                bc(D=bcDx, N=bcNx, val=bcValx),
                dg(fC),
                dg(fR))
            else
              calcRecov1CellGen(basisNm[bIdx], x, dirs, polyOrder, C,
                dg(fL),
                dg(fC),
                dg(fR)),
      
    recfy : if isTop then
              calcRecov1CellGen(basisNm[bIdx], y, dirs, polyOrder, C,
                dg(fB),
                dg(fC),
                bc(D=bcDy, N=bcNy, val=bcValy))
            elseif isBottom then
              calcRecov1CellGen(basisNm[bIdx], y, dirs, polyOrder, C,
                bc(D=bcDy, N=bcNy, val=bcValy),
                dg(fC),
                dg(fT))
            else
              calcRecov1CellGen(basisNm[bIdx], y, dirs, polyOrder, C,
                dg(fB),
                dg(fC),
                dg(fT)),

    vol : calcInnerProdList(dirs, Jxx, diff(ba, x),
            doExpand(DxxC, ba)*diff(recfx, x)) +
          calcInnerProdList(dirs, Jxy, diff(ba, x),
            doExpand(DxyC, ba)*diff(recfy, y)) +
          calcInnerProdList(dirs, Jxy, diff(ba, y),
            doExpand(DxyC, ba)*diff(recfx, x)) +
          calcInnerProdList(dirs, Jyy, diff(ba, y),
            doExpand(DyyC, ba)*diff(recfy, y)),

    /* Top -------------------------------------------------------- */
    if isTop then (
      recf2 : calcRecov2CellGen(basisNm[bIdx], y, dirs, polyOrder,
        dg(fB),
        dg(fC, bc(D=bcDy, N=bcNy, val=bcValy))),
      recf6 : if isRight then
                calcRecovFaceGen(basisNm[bIdx], [y,x], dirs, 2, polyOrder, C,
                  dg(dg(fBL), dg(fL, bc(D=bcDy, N=bcNy, val=bcValy))),
                  dg(dg(fB), dg(fC, bc(D=bcDy, N=bcNy, val=bcValy))),
                  bc(D=bcDx, N=bcNx, val=bcValx))
              elseif isLeft then
                calcRecovFaceGen(basisNm[bIdx], [y,x], dirs, 2, polyOrder, C,
                  bc(D=bcDx, N=bcNx, val=bcValx),
                  dg(dg(fB), dg(fC, bc(D=bcDy, N=bcNy, val=bcValy))),
                  dg(dg(fBR), dg(fR, bc(D=bcDy, N=bcNy, val=bcValy))))
              else
                calcRecovFaceGen(basisNm[bIdx], [y,x], dirs, 2, polyOrder, C,
                  dg(dg(fBL), dg(fL, bc(D=bcDy, N=bcNy, val=bcValy))),
                  dg(dg(fB), dg(fC, bc(D=bcDy, N=bcNy, val=bcValy))),
                  dg(dg(fBR), dg(fR, bc(D=bcDy, N=bcNy, val=bcValy)))),
      faceT : doExpand(DyyTF, bax)*subst(y=2, diff(recf2, y))*Jyy +
              doExpand(DxyTF, bax)*diff(recf6, x)*Jxy
    ) else (
      recf2 : calcRecov2CellGen(basisNm[bIdx], y, dirs, polyOrder,
        dg(fC),
        dg(fT)),
      recf6 : if isRight then
                calcRecovFaceGen(basisNm[bIdx], [y,x], dirs, 0, polyOrder, C,
                  dg(dg(fL), dg(fTL)),
                  dg(dg(fC), dg(fT)),
                  bc(D=bcDx, N=bcNx, val=bcValx))
              elseif isLeft then
                calcRecovFaceGen(basisNm[bIdx], [y,x], dirs, 0, polyOrder, C,
                  bc(D=bcDx, N=bcNx, val=bcValx),
                  dg(dg(fC), dg(fT)),
                  dg(dg(fR), dg(fTR)))
              else
                calcRecovFaceGen(basisNm[bIdx], [y,x], dirs, 0, polyOrder, C,
                  dg(dg(fL), dg(fTL)),
                  dg(dg(fC), dg(fT)),
                  dg(dg(fR), dg(fTR))),
      faceT : doExpand(DyyTF, bax)*subst(y=0, diff(recf2, y))*Jyy +
              doExpand(DxyTF, bax)*diff(recf6, x)*Jxy
    ),
    surfT : calcInnerProdList([x], 1, subst(y=1, ba), faceT),

    /* Bottom ----------------------------------------------------- */
    if isBottom then (
      recf2 : calcRecov2CellGen(basisNm[bIdx], y, dirs, polyOrder,
        dg(fC, bc(D=bcDy, N=bcNy, val=bcValy)),
        dg(fT)),
      recf6 : if isRight then
                calcRecovFaceGen(basisNm[bIdx], [y,x], dirs, -2, polyOrder, C,
                  dg(dg(fL, bc(D=bcDy, N=bcNy, val=bcValy)), dg(fTL)),
                  dg(dg(fC, bc(D=bcDy, N=bcNy, val=bcValy)), dg(fT)),
                  bc(D=bcDx, N=bcNx, val=bcValx))
              elseif isLeft then
                calcRecovFaceGen(basisNm[bIdx], [y,x], dirs, -2, polyOrder, C,
                  bc(D=bcDx, N=bcNx, val=bcValx),
                  dg(dg(fC, bc(D=bcDy, N=bcNy, val=bcValy)), dg(fT)),
                  dg(dg(fR, bc(D=bcDy, N=bcNy, val=bcValy)), dg(fTR)))
              else
                calcRecovFaceGen(basisNm[bIdx], [y,x], dirs, -2, polyOrder, C,
                  dg(dg(fL, bc(D=bcDy, N=bcNy, val=bcValy)), dg(fTL)),
                  dg(dg(fC, bc(D=bcDy, N=bcNy, val=bcValy)), dg(fT)),
                  dg(dg(fR, bc(D=bcDy, N=bcNy, val=bcValy)), dg(fTR))),
      faceB : doExpand(DyyBF, bax)*subst(y=-2, diff(recf2, y))*Jyy +
              doExpand(DxyBF, bax)*diff(recf6, x)*Jxy
    ) else (
      recf2 : calcRecov2CellGen(basisNm[bIdx], y, dirs, polyOrder,
        dg(fB),
        dg(fC)),
      recf6 : if isRight then
                calcRecovFaceGen(basisNm[bIdx], [y,x], dirs, 0, polyOrder, C,
                  dg(dg(fBL), dg(fL)),
                  dg(dg(fB), dg(fC)),
                  bc(D=bcDx, N=bcNx, val=bcValx))
              elseif isLeft then
                calcRecovFaceGen(basisNm[bIdx], [y,x], dirs, 0, polyOrder, C,
                  bc(D=bcDx, N=bcNx, val=bcValx),
                  dg(dg(fB), dg(fC)),
                  dg(dg(fBR), dg(fR)))
              else
                calcRecovFaceGen(basisNm[bIdx], [y,x], dirs, 0, polyOrder, C,
                  dg(dg(fBL), dg(fL)),
                  dg(dg(fB), dg(fC)),
                  dg(dg(fBR), dg(fR))),
      faceB : doExpand(DyyBF, bax)*subst(y=0, diff(recf2, y))*Jyy +
              doExpand(DxyBF, bax)*diff(recf6, x)*Jxy
    ),
    surfB : calcInnerProdList([x], 1, subst(y=-1, ba), faceB),

    /* Left ------------------------------------------------------- */
    if isLeft then (
      recf2 : calcRecov2CellGen(basisNm[bIdx], x, dirs, polyOrder,
        dg(fC, bc(D=bcDx, N=bcNx, val=bcValx)),
        dg(fR)),
      recf6 : if isTop then
                calcRecovFaceGen(basisNm[bIdx], [x,y], dirs, -2, polyOrder, C,
                  dg(dg(fB, bc(D=bcDx, N=bcNx, val=bcValx)), dg(fBR)),
                  dg(dg(fC, bc(D=bcDx, N=bcNx, val=bcValx)), dg(fR)),
                  bc(D=bcDy, N=bcNy, val=bcValy))
              elseif isBottom then
                calcRecovFaceGen(basisNm[bIdx], [x,y], dirs, -2, polyOrder, C,
                  bc(D=bcDy, N=bcNy, val=bcValy),
                  dg(dg(fC, bc(D=bcDx, N=bcNx, val=bcValx)), dg(fR)),
                  dg(dg(fT, bc(D=bcDx, N=bcNx, val=bcValx)), dg(fTR)))
              else
                calcRecovFaceGen(basisNm[bIdx], [x,y], dirs, -2, polyOrder, C,
                  dg(dg(fB, bc(D=bcDx, N=bcNx, val=bcValx)), dg(fBR)),
                  dg(dg(fC, bc(D=bcDx, N=bcNx, val=bcValx)), dg(fR)),
                  dg(dg(fT, bc(D=bcDx, N=bcNx, val=bcValx)), dg(fTR))),
      faceL : doExpand(DxxLF, bay)*subst(x=-2, diff(recf2, x))*Jxx +
              doExpand(DxyLF, bay)*diff(recf6, y)*Jxy
    ) else (
      recf2 : calcRecov2CellGen(basisNm[bIdx], x, dirs, polyOrder,
        dg(fL),
        dg(fC)),
      recf6 : if isTop then
                calcRecovFaceGen(basisNm[bIdx], [x,y], dirs, 0, polyOrder, C,
                  dg(dg(fBL), dg(fB)),
                  dg(dg(fL), dg(fC)),
                  bc(D=bcDy, N=bcNy, val=bcValy))
              elseif isBottom then
                calcRecovFaceGen(basisNm[bIdx], [x,y], dirs, 0, polyOrder, C,
                  bc(D=bcDy, N=bcNy, val=bcValy),
                  dg(dg(fL), dg(fC)),
                  dg(dg(fTL), dg(fT)))
              else
                calcRecovFaceGen(basisNm[bIdx], [x,y], dirs, 0, polyOrder, C,
                  dg(dg(fBL), dg(fB)),
                  dg(dg(fL), dg(fC)),
                  dg(dg(fTL), dg(fT))),
      faceL : doExpand(DxxLF, bay)*subst(x=0, diff(recf2, x))*Jxx +
              doExpand(DxyLF, bay)*diff(recf6, y)*Jxy
    ),
    surfL : calcInnerProdList([y], 1, subst(x=-1, ba), faceL),

    /* Right ------------------------------------------------------ */
    if isRight then (
      recf2 : calcRecov2CellGen(basisNm[bIdx], x, dirs, polyOrder,
        dg(fL),
        dg(fC, bc(D=bcDx, N=bcNx, val=bcValx))),
      recf6 : if isTop then
                calcRecovFaceGen(basisNm[bIdx], [x,y], dirs, 2, polyOrder, C,
                  dg(dg(fBL), dg(fB, bc(D=bcDx, N=bcNx, val=bcValx))),
                  dg(dg(fL), dg(fC, bc(D=bcDx, N=bcNx, val=bcValx))),
                  bc(D=bcDy, N=bcNy, val=bcValy))
              elseif isBottom then
                calcRecovFaceGen(basisNm[bIdx], [x,y], dirs, 2, polyOrder, C,
                  bc(D=bcDy, N=bcNy, val=bcValy),
                  dg(dg(fL), dg(fC, bc(D=bcDx, N=bcNx, val=bcValx))),
                  dg(dg(fTL), dg(fT, bc(D=bcDx, N=bcNx, val=bcValx))))
              else
                calcRecovFaceGen(basisNm[bIdx], [x,y], dirs, 2, polyOrder, C,
                  dg(dg(fBL), dg(fB, bc(D=bcDx, N=bcNx, val=bcValx))),
                  dg(dg(fL), dg(fC, bc(D=bcDx, N=bcNx, val=bcValx))),
                  dg(dg(fTL), dg(fT, bc(D=bcDx, N=bcNx, val=bcValx)))),
      faceR : doExpand(DxxRF, bay)*subst(x=2, diff(recf2, x))*Jxx +
              doExpand(DxyRF, bay)*diff(recf6, y)*Jxy
    ) else (
      recf2 : calcRecov2CellGen(basisNm[bIdx], x, dirs, polyOrder,
        dg(fC),
        dg(fR)),
      recf6 : if isTop then
                calcRecovFaceGen(basisNm[bIdx], [x,y], dirs, 0, polyOrder, C,
                  dg(dg(fB), dg(fBR)),
                  dg(dg(fC), dg(fR)),
                  bc(D=bcDy, N=bcNy, val=bcValy))
              elseif isBottom then
                calcRecovFaceGen(basisNm[bIdx], [x,y], dirs, 0, polyOrder, C,
                  bc(D=bcDy, N=bcNy, val=bcValy),
                  dg(dg(fC), dg(fR)),
                  dg(dg(fT), dg(fTR)))
              else
                calcRecovFaceGen(basisNm[bIdx], [x,y], dirs, 0, polyOrder, C,
                  dg(dg(fB), dg(fBR)),
                  dg(dg(fC), dg(fR)),
                  dg(dg(fT), dg(fTR))),
      faceR : doExpand(DxxRF, bay)*subst(x=0, diff(recf2, x))*Jxx +
              doExpand(DxyRF, bay)*diff(recf6, y)*Jxy
    ),
    surfR : calcInnerProdList([y], 1, subst(x=1, ba), faceR),

    /* Assembly --------------------------------------------------- */
    /* st : expand(surfT - surfB + surfR - surfL - vol), */
    /* I'm not sure the 'expand' here is needed and it significantly
    increases the execution time for higher polynomial cases --Petr */
    st : surfT - surfB + surfR - surfL - vol,
    stCheck : transpose(makelist(0, i, 1, numBasis)), /* Cross-check */

    fList : [fBL, fL, fTL, fB, fC, fT, fBR, fR, fTR],
    for m : 1 thru 9 do (
      cMat : coefmatrix(st, makelist(fList[m][i], i, 1, numBasis)),
      stCheck : stCheck + cMat. transpose(makelist(fList[m][i], i, 1, numBasis)),
      printf(fh, "  _M[~a] = Lin.Mat(~a,~a)~%", m, numBasis, numBasis),
      for i : 1 thru numBasis do (
        for j : 1 thru numBasis do (
          printf(fh, "  _M[~a][~a][~a] = ~a~%", m, i, j, float(expand(cMat[i][j])))
        )
      )
    ),
    cMatx : coefmatrix(st, [bcValx]),
    cMaty : coefmatrix(st, [bcValy]),
    /* Santity check: should be all zeros */
    isZero : makelist(expand(st[i]-stCheck[i][1]-cMatx[i][1]*bcValx-cMaty[i][1]*bcValy),i,1,numBasis),
    printf(fh, "  _M[10] = Lin.Vec(~a)~%", numBasis),
    for i : 1 thru numBasis do (
      printf(fh, "  _M[10][~a] = ~a + ~a~%", i,
             bcValx*float(expand(cMatx[i][1])), bcValy*float(expand(cMaty[i][1])))
    ),

    printf(fh, "  return(_M)~%"),
    printf(fh, "end~%~%"),
    printf(fh, "return(stencilFn)"),
    close(fh)
  )
)$