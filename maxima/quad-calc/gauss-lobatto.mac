/* Weights and ordinates for Guass-Lobatto quadrature */

/* See: https://mathworld.wolfram.com/LobattoQuadrature.html */

calc_w_mu(N) := block([mu,w,nr,wn],
  mu : sort(
    map(rhs, realroots( diff(legendre_p(N-1,x),x), 1e-32 ) )
    ),
  nr : length(mu),
  w : makelist( 2/(N*(N-1)*(legendre_p(N-1,mu[i]))^2), i, 1, nr),
  wn : 2/(N*(N-1)),
  return (
    [endcons(wn,cons(wn,w)), endcons(1,cons(-1,mu))]
    )
  )$

fh : openw("~/max-out/gauss-lobatto.h")$

for N : 3 thru 8 do (
  wmu : calc_w_mu(N),
  mu : float(wmu[2]),
  printf(fh, "static const double gkyl_gauss_lobatto_ordinates_~a[] = ~%", N),
  printf(fh, "{ ~a }; ~%", mu)
  )$

for N : 3 thru 8 do (
  wmu : calc_w_mu(N),
  w : float(wmu[1]),
  printf(fh, "static const double gkyl_gauss_lobatto_weights_~a[] = ~%", N),
  printf(fh, "{ ~a }; ~%", w)
  )$

close(fh)$
