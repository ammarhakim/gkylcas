/* Functions to manipulate nodal data/functions in FEM solvers. */
load("utilities")$
load("nodal_operations/nodal_functions")$

deleteNodes(nodesIn,coordsIn,allTrueIn) := block([numNodes,dimCheck,remNodes,removeNode],
  /* Given a list of nodes (nodesIn), delete the nodes that have the all
     coordinates given by coordsIn (allTrueIn=true), or those that have
     at least one coordinate equal to those in nodesIn (allTrueIn=false). */
  numNodes : length(nodesIn),
  dimCheck : length(coordsIn),
  remNodes : copylist(nodesIn),
  for i : 1 thru numNodes do (
    if allTrueIn then ( removeNode : true ) else ( removeNode : false ),
    for j : 1 thru dimCheck do (
      if allTrueIn then (
        if (nodesIn[i][coordsIn[j][1]]=coordsIn[j][2]) then (
          removeNode : removeNode and true
        ) else (
          removeNode : removeNode and false
        )
      ) else (
        if (nodesIn[i][coordsIn[j][1]]=coordsIn[j][2]) then (
          removeNode : true
        )
      )
    ),
    if removeNode then ( remNodes : delete(nodesIn[i],remNodes) )
  ),
  return(remNodes)
)$

getNodeIndices(nodeSubsetIn,nodesIn) := block([nodeIdxs,idx],
  /* Return the indices of a set of nodes (nodeSubsetIn) within the nodesIn list. */
  nodeIdxs : [],
  for k : 1 thru length(nodeSubsetIn) do (
    idx      : sublist_indices(nodesIn,lambda([x],x=nodeSubsetIn[k]))[1],
    nodeIdxs : append(nodeIdxs,[idx])
  ),
  return(nodeIdxs)
)$

getStoredNodes(nodesIn,cellLoc) := block([numNodes,dim,storedNodes,removeNode],
  /* Given the nodes in a cell, return a list of the nodes which this cell
     actually stores in memory. This is not BC-aware, it basically only
     returns the lower-left nodes for interior cells and adds upper nodes
     for boundary cells. See getStoredNodesWithBC below. */
  numNodes    : length(nodesIn),
  dim         : length(nodesIn[1]),
  storedNodes : copylist(nodesIn),
  for i : 1 thru numNodes do (
    removeNode : false,
    for d : 1 thru dim do (
      if (nodesIn[i][d]=1) and (cellLoc[d]<1) then ( removeNode : true )
    ),
    if removeNode then ( storedNodes : delete(nodesIn[i],storedNodes) )
  ),
  return(storedNodes)
)$

getStoredNodesWithBC(nodesIn,cellLoc,bcIn) := block([storedNodes],
  /* Given the nodes in a cell, return a list of the nodes which this cell
     actually stores in memory taking into account its location and BC. bcIn
     is a list of the BC along each direction, =0 periodic else nonperiodic. */
  dim           : length(nodesIn[1]),
  storedNodes   : getStoredNodes(nodesIn,cellLoc), 
  storedNodesBC : copylist(storedNodes),
  if cellLoc#makelist(0,d,1,dim) then (  /* Interior cells keep all stored nodes. */
    periodicDirs : sublist_indices(bcIn, lambda([x], x=0)),
    for n : 1 thru length(storedNodes) do (
      removeNode : false,
      for d : 1 thru dim do (
        if (storedNodes[n][d]=1) and (isInList(d,periodicDirs)) then ( removeNode : true )
      ),
      if removeNode then ( storedNodesBC : delete(storedNodes[n],storedNodesBC) )
    )
  ),
  return(storedNodesBC)
)$

calcMassUnweighted(dim, basisType, pOrder) := block(
  /* Calculate unweighted nodal mass matrix. */
  [basis_n,vars,massMod],
  basis_n : getNodalBasis(basisType, dim, pOrder),
  vars    : listofvars(basis_n),
  massNod : calcMassMatrix(vars, 1, basis_n),
  return(massNod)
)$

calcParMassUnweighted(dim, basisType, pOrder) := block([n, mass],
  /* Calculate unweighted nodal parallel mass matrix.
     this is the inner product of a basis along the parallel
     direction with the multi-dimensional basis. */
  [basis_n,vars,mass,k,d,n,i,j],

  basis_n : getNodalBasis(basisType, dim, pOrder),
  vars    : listofvars(basis_n),

  parBasis_n : getNodalBasis(basisType, 1, pOrder),
/**
  parBasis_n : copylist(basis_n),
  for k : 1 thru length(basis_n) do (
    for d : 1 thru dim-1 do (
      if not freeof(vars[d], parBasis_n[k]) then (parBasis_n[k] : 0)
    )
  ),
**/
  parvar : vars[dim],
  parBasis_n : subst(x=parvar, parBasis_n),

  m : length(parBasis_n),
  n : length(basis_n),
  mass : (mass : zeromatrix(m,n), for i from 1 thru m
    do (for j from 1 thru n
      do (mass[i,j] : innerProd(vars, 1, parBasis_n[i], basis_n[j]))),
    mass),
  return(mass)
)$
