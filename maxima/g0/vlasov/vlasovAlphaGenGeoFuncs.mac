/* Compute the volume term for the streaming (v . grad(f)) term
   in the Vlasov neutral eqn with general geometry. */

load("modal-basis");
load("out-scripts");
load(stringproc)$
fpprec : 24$

dvdx : [dv0dx0, dv1dx1, dv2dx2]$
wdx  : [w0dx0, w1dx1, w2dx2]$
dv11 : [dv10, dv11, dv12]$
vc   : [vx_c, vy_c, vz_c]$

/* Helper function for switching to eta = (v - w)/(dv/2) coordinates */
vTransSubs : [vvx=vx*dv1/2+wv1, vvy=vy*dv2/2+wv2, vvz=vz*dv3/2+wv3]$

cidx(cdim)      := makelist(i,i,0,cdim-1)$
vidx(cdim,vdim) := makelist(i,i,cdim,cdim+vdim-1)$

/* Helper functions for expanding in basis functions a quantity we know should be sparse  */
/* For example, alpha, the phase space flow only depends on a few phase space coordinates */
doMakeExprLst(vals, S)  := makelist(if vals[i] # 0 then S[i-1] else 0, i, 1, length(vals))$
doExpandLst(lst, basis) := sum(lst[i]*basis[i], i, 1, length(basis))$

calcVlasovAlphaGenGeo(fh, funcNm, cdim, vdim, basisFun, polyOrder) := block(
  [varsC, bC, varsP, bP, basisP_p1, bP_p1, NP_p1, NC, varsV, cid, vid, 
   tvComp_e, gxx_e, gxy_e, gxz_e, gyy_e, gyz_e, gzz_e, 
   vTemp_c, tvi, v_xlst, v_x_e, v_ylst, v_y_e, v_zlst, v_z_e, 
   vx_c, vy_c, vz_c, vc_e, alpha_geo_c, expr],

  printf(fh, "#include <gkyl_dg_vlasov_alpha_gen_geo_kernels.h> ~%"),

  /* Load basis of dimensionality requested. */
  [varsC,bC,varsP,bP] : loadPhaseBasis(basisFun, cdim, vdim, polyOrder),
  /* alpha_geo is linear in velocity, so use only a p=1 basis in phase space for its variation */
  basisP_p1 : load(sconcat("basis-precalc/basisSer", cdim, "x", vdim, "v")),
  bP_p1 : basisP[1],
  NP_p1 : length(bP_p1),
  NC : length(bC),
  print("NP_p1", NP_p1),
  varsV : makelist(varsP[i],i,cdim+1,cdim+vdim),
  print(varsV),

  printf(fh, "GKYL_CU_DH void ~a_p~a(const double *w, const double *dxv, const double *tvComp, const double *gij, double* GKYL_RESTRICT alpha_geo) ~%{ ~%", funcNm, polyOrder),
  printf(fh, "  // w[NDIM]:    Cell-center coordinates.~%"),
  printf(fh, "  // dxv[NDIM]:  Cell spacing.~%"),
  printf(fh, "  // tvComp[~a]: Components for tangent basis vectors.~%", NC*9),
  printf(fh, "  // gij[~a]:    Contravariant components of metric tensor.~%", NC*6),
  printf(fh, "  // v_i:        Covariant components of velocity.~%"),
  printf(fh, "  // alpha_geo:  Output alpha field.~%"),

  cid : cidx(cdim),
  vid : vidx(cdim,vdim),

  printf(fh, "~%"),
  printf(fh, "  double v_x[~a] = {0.0};~%", NP_p1),
  printf(fh, "  double v_y[~a] = {0.0};~%", NP_p1),
  printf(fh, "  double v_z[~a] = {0.0};~%", NP_p1),
  printf(fh, "~%"),	

  printf(fh, "  const double *gxx = &gij[~a]; ~%", NC*0),
  printf(fh, "  const double *gxy = &gij[~a]; ~%", NC*1),
  printf(fh, "  const double *gxz = &gij[~a]; ~%", NC*2),
  printf(fh, "  const double *gyy = &gij[~a]; ~%", NC*3),
  printf(fh, "  const double *gyz = &gij[~a]; ~%", NC*4),
  printf(fh, "  const double *gzz = &gij[~a]; ~%", NC*5),
  printf(fh, "~%"),

  tvComp_e : doExpand1(tvComp, bC),
  gxx_e : doExpand1(gxx, bC),
  gxy_e : doExpand1(gxy, bC),
  gxz_e : doExpand1(gxz, bC),
  gyy_e : doExpand1(gyy, bC),
  gyz_e : doExpand1(gyz, bC),
  gzz_e : doExpand1(gzz, bC),

  /* For multiple components, change indices. */
  tvComp_e : psubst(makelist(tvComp[i]=tvComp[a0+i],i,0,NC-1),tvComp_e),

  vTemp_c :  makelist(0, i, 1, vdim), 				 
  /* Calculate v_i components: v_x = v_X*dX/dx + v_Y*dY/dx + v_Z*dZ/dx, ...  */
   for dir1 : 1 thru vdim do (
    vTemp_c[dir1] : 0,
    for dir2 : 1 thru vdim do (
    	tvi : (dir1 - 1)*cdim + (dir2-1),
    	vTemp_c[dir1] : vTemp_c[dir1]
             +calcInnerProdList(varsP, 1, bP_p1, (dxv[vid[dir2]]/2*varsV[dir2] + w[vid[dir2]])*subst(a0=NC*(tvi),tvComp_e)))
  ),
  print("loop ended"),

  writeCExprsCollect1lhsc(v_x, vTemp_c[1]),
  writeCExprsCollect1lhsc(v_y, vTemp_c[2]),
  writeCExprsCollect1lhsc(v_z, vTemp_c[3]),
  printf(fh, "~%"),

  /* Expand in phase basis with reduced list based on sparseness. */
  v_xlst : doMakeExprLst(vTemp_c[1], v_x),
  v_x_e : doExpand(v_xlst, bP_p1),

  v_ylst : doMakeExprLst(vTemp_c[2], v_y),
  v_y_e : doExpand(v_ylst, bP_p1),

  v_zlst : doMakeExprLst(vTemp_c[3], v_z),
  v_z_e : doExpand(v_zlst, bP_p1),

  /* Calculate v^i : v^x = gxx*v_x + gxy*v_y + gxz*v_z, ... */
  vx_c : calcInnerProdList(varsP, 1, bP_p1, (gxx_e*v_x_e + gxy_e*v_y_e + gxz_e*v_z_e)),
  vy_c : calcInnerProdList(varsP, 1, bP_p1, (gxy_e*v_x_e + gyy_e*v_y_e + gyz_e*v_z_e)),
  vz_c : calcInnerProdList(varsP, 1, bP_p1, (gxz_e*v_x_e + gyz_e*v_y_e + gzz_e*v_z_e)),

  /* Write out each component of alpha_geo. */
  /* alpha_cdim = v dot grad */
  for cdir : 1 thru vdim do ( 
    vc_e : doExpandLst(vc[cdir], bP_p1), 
    alpha_geo_c : calcInnerProdList(varsP, 1, bP_p1, vc_e),
    expr : gcfac(float(expand(radcan(alpha_geo_c)))),

    /* Write out the component of alpha_cdim's phase space expansion */
    for i : 1 thru NP_p1 do (
      if expr[i] # 0.0 then printf(fh, "  ~a = ~a; ~%", alpha_geo[NP_p1*(cdir-1)+i-1], expr[i])
    )
  ),

  printf(fh, "~%"),
  printf(fh, "} ~%")
)$

bName : ["ser"]$
maxPolyOrder : [1]$

for bInd : 1 thru length(bName) do (
  for c : 3 thru 3 do (
      for v : 3 thru 3 do (
      	  disp(printf(false,sconcat("Creating alpha ", bName[bInd]," ~ax~av"),c,v)),
    	  fname : sconcat("~/max-out/vlasov_alpha_gen_geo_", c, "x", v, "v_", bName[bInd], ".c"),
    	  fh : openw(fname),

	  print("attempting to print",c,"x",v,"v functions..."),
    	  /*. Calculate velocity components  */
    	  funcNm : sconcat("vlasov_alpha_gen_geo_", c, "x", v, "v_", bName[bInd]),
    	  calcVlasovAlphaGenGeo(fh, funcNm, c, v, bName[bInd], maxPolyOrder[bInd]),
    	  close(fh)
  	  )
  )	
)$
