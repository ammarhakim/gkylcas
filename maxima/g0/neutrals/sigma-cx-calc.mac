/* Generate CX cross section (vSigmaCX) kernels for charge
   exchange operator */

load("modal-basis");
load("out-scripts");
load(stringproc)$
fpprec : 24$

/* Serendipity basis. */
maxPolyOrder_Ser : 2$

declareWriteCExprsC(lhs, rhs) := block([expr],
  expr : float(expand(rhs)),
  if expr # 0.0 then (
    ep : string(expr),
    for i : 0 thru 60 do (
    	ep : ssubst(string(pow(uIon[i],2)),string(uIon[i]^2),ep),
    	ep : ssubst(string(pow(uNeut[i],2)),string(uNeut[i]^2),ep)	
    ),
    printf(fh, "  double ~a = ~a; ~%", lhs, ep)
  )
)$

writeCExprsCs(lhs, rhs) := block([expr],
  expr : float(expand(rhs)),
  for i : 1 thru length(expr) do (	
    if expr[i] # 0.0 then (
      ep : string(expr[i]),
      ep : ssubst("log(","ln(",ep),	
      printf(fh, "  ~a = ~a; ~%", lhs[i-1], ep)	
    )
  )
)$

writeCExprsC(lhs, rhs) := block([expr],
  expr : float(expand(rhs)),
  if expr # 0.0 then (
     ep : string(expr),
     printf(fh, "  ~a = ~a; ~%", lhs, ep)	
  )
)$

/* vSigmaCX for charge exchange cross section */
calcSigmaCXcellAv(fh, funcNm, cdim, vdim, basisFun, pMax) := block([],
  kill(varsC, varsP, basisC, basisP),
  modNm : sconcat("basis-precalc/basis", basisFun, cdim, "x", vdim, "v"),
  load(modNm),
  for p : 1 thru pMax do (
    bP : basisP[p],
    print(basisFun, cdim, vdim, p, length(basisP)),
    bC : basisC[p],
    NC : length(bC),

    printf(fh, " ~%"),
    printf(fh, "GKYL_CU_DH static inline ~%"),
    printf(fh, "double ~a_p~a(const double a, const double b, const double *m0, const double *prim_vars_ion, const double *prim_vars_neut, const double *vt_sq_ion_min, const double *vt_sq_neut_min, double GKYL_RESTRICT *v_sigma_cx) ~%{ ~%", funcNm, p),
    printf(fh, "  // a               constant in fitting function. ~%"),
    printf(fh, "  // b               constant in fitting function. ~%"),
    printf(fh, "  // m0[~a]:         neutral particle density. ~%", NC),
    printf(fh, "  // prim_vars_ion[~a]:   ion prim vars. ~%", NC*(vdim+1)),
    printf(fh, "  // prim_vars_neut[~a]:  neut prim vars. ~%", NC*(vdim+1)),
    printf(fh, "  // v_sigma_cx:          cell ave cross section fitting eqn. ~%"),
    printf(fh, " ~%"),

    m0_e : doExpand1(m0,bC),
    m0_av :  innerProd(varsC,1.0,1.0,m0_e)/innerProd(varsC,1.0,1.0,1.0),
    declareWriteCExprsC(m0_neut_av, m0_av),

    printf(fh, "  const double *u_ion = &prim_vars_ion[~a]; ~%", 0),
    printf(fh, "  const double *vt_sq_ion = &prim_vars_ion[~a]; ~%", NC*vdim),
    printf(fh, "  const double *u_neut = &prim_vars_neut[~a]; ~%", 0),	
    printf(fh, "  const double *vt_sq_neut = &prim_vars_neut[~a]; ~%", NC*vdim),
    printf(fh, " ~%"),

    u_ion_av_c : subst(makelist(u_ion[i]=u_ion[i+a0],i,0,NC-1),innerProd(varsC,1.0,1.0,doExpand1(u_ion,bC))/innerProd(varsC,1.0,1.0,1.0)),
    u_neut_av_c : subst(makelist(u_neut[i]=u_neut[i+a0],i,0,NC-1),innerProd(varsC,1.0,1.0,doExpand1(u_neut,bC))/innerProd(varsC,1.0,1.0,1.0)),

    u_ion_av : makelist(0,i,1,vdim),
    u_neut_av : makelist(0,i,1,vdim),
    for vi : 1 thru vdim do (
      u_ion_av[vi] : fullratsimp(subst(a0=(vi-1)*NC,u_ion_av_c)),
      u_neut_av[vi] : fullratsimp(subst(a0=(vi-1)*NC,u_neut_av_c))	
    ),

    vt_sq_ion_av_c : innerProd(varsC,1.0,1.0, doExpand1(vt_sq_ion,bC))/innerProd(varsC,1.0,1.0,1.0),
    vt_sq_neut_av_c : innerProd(varsC,1.0,1.0, doExpand1(vt_sq_neut,bC))/innerProd(varsC,1.0,1.0,1.0),

    declareWriteCExprsC(vt_sq_ion_av, vt_sq_ion_av_c),
    declareWriteCExprsC(vt_sq_neut_av, vt_sq_neut_av_c),

    /* Limit how low the temperature can get (set by the velocity grid). */
    printf(fh, "  if ((vt_sq_ion_av > 0.) && (vt_sq_ion_av < vt_sq_ion_min)) vt_sq_ion_av = vt_sq_ion_min;~%"),
    printf(fh, "  if ((vt_sq_neut_av > 0.) && (vt_sq_neut_av < vt_sq_neut_min)) vt_sq_neut_av = vt_sq_neut_min;~%"),
    printf(fh, "  ~%"),
    
    /* Define and write v_sigma_cx */
    printf(fh, "  if (m0_neut_av <= 0 || vt_sq_neut_av <= 0 || vt_sq_ion_av <= 0) { ~%"),
    printf(fh, "    v_sigma_cx[0] = 0.0;~%"),
    printf(fh, "    return 0.0; ~%"),	
    printf(fh, "  } else {~%"),
     
    /* Define v_in variable: v_in_sq = |u_ion - u_neut|^2 */
    v_in_sq_c : innerProd(varsC,1.0,1.0,sum((u_ion_av[d] - u_neut_av[d])^2,d,1,vdim))/innerProd(varsC,1.0,1.0,1.0),
    declareWriteCExprsC(v_in_sq_av, v_in_sq_c),
    printf(fh, " ~%"),
    
    printf(fh, "  double v_cx = ~a;~%",float(sqrt(4/%pi*vt_sq_ion_av + 4/%pi*vt_sq_neut_av + v_in_sq_av))),
    v_sigma_cx_c : calcInnerProdList(varsC,1.0,bC,v_cx*(a - b*ln(v_cx))),
    writeCExprsCs(v_sigma_cx, v_sigma_cx_c),
    printf(fh, " ~%"),

    v_sigma_cx_e  : doExpand1(v_sigma_cx,bC),
    v_sigma_cx_av  : innerProd(varsC,1.0,1.0,v_sigma_cx_e)/innerProd(varsC,1.0,1.0,1.0),
    cflRate : float(expand(fullratsimp(m0_av*v_sigma_cx_av/(2*p+1)))),
    printf(fh, "  return ~a; ~%",cflRate),

    printf(fh, "  }~%"),
    printf(fh, "} ~%")
))$

bName        : ["ser"]$
maxPolyOrder : [maxPolyOrder_Ser]$

for bInd : 1 thru length(bName) do (
  for c : 1 thru 3 do (
      for v : c thru 3 do (
      	  disp(printf(false,sconcat("Creating v_sigma_cx ", bName[bInd]_," ~ax~av"),c,v)),
    	  fname : sconcat("~/max-out/sigma_cx_cell_av_", bName[bInd], c, "x", v, "v", ".c"),
    	  fh : openw(fname),

    	  printf(fh, "#include <ChargeExchangeModDecl.h> ~%"),
    	  printf(fh, "#include <math.h> ~%"),

	  print("attempting to print",c,"x",v,"v functions..."),
    	  /*. Calculate CX cross section. */
    	  funcNm : sconcat("sigma_cx_", c, "x", v, "v_", bName[bInd]),
    	  calcSigmaCXcellAv(fh, funcNm, c, v, bName[bInd], maxPolyOrder[bInd]),
    	  close(fh)
  	  )
  )	
)$
