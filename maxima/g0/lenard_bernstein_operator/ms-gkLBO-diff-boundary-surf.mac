load("lenard_bernstein_operator/gkLBOFuncs-diff-boundary-surf")$
load(stringproc)$

/* This script calls the functions which calculate the
   updates from the boundary surface contribution of the
   diffusion term in the Lenard-Bernstein collision operator */

/* ...... USER INPUTS........ */

/* Generate kernels for "uni"form, "nonuni"form or "both" grid types. */
grid : "uni"$

/* Serendipity basis. */
minPolyOrder_Ser : 1$
maxPolyOrder_Ser : 2$
minCdim_Ser : 1$
minVdim_Ser : 1$    /* see begining of v loop below though. */
maxCdim_Ser : 3$
maxVdim_Ser : 2$

/* Tensor product basis. */
/* Note that Serendipity, p = 1, is equivalent to Tensor */
/* Thus no need to calculate p = 1 Tensor basis */
minPolyOrder_Tensor : 2$
maxPolyOrder_Tensor : 2$
minCdim_Tensor : 1$
minVdim_Tensor : 1$    /* see begining of v loop below though. */
maxCdim_Tensor : 1$
maxVdim_Tensor : 2$

gkVdims : [[1,2], [2], [2]]$

/* ...... END OF USER INPUTS........ */

bName        : ["ser","tensor"]$
minPolyOrder : [minPolyOrder_Ser, minPolyOrder_Tensor]$
maxPolyOrder : [maxPolyOrder_Ser, maxPolyOrder_Tensor]$
minCdim      : [minCdim_Ser, minCdim_Tensor]$
minVdim      : [minVdim_Ser, minVdim_Tensor]$
maxCdim      : [maxCdim_Ser, maxCdim_Tensor]$
maxVdim      : [maxVdim_Ser, maxVdim_Tensor]$

varsV : [vpar, mu]$

/* Is non-uniform booleans. */

if     grid="uni"    then (gridType : [false])
elseif grid="nonuni" then (gridType : [true])
elseif grid="both"   then (gridType : [false,true])$

for bInd : 1 thru length(bName) do (
  for c : minCdim[bInd] thru maxCdim[bInd] do (
    for gkV : 1 thru length(gkVdims[c]) do (
      v : gkVdims[c][gkV],

      minPolyOrderB : minPolyOrder[bInd],
      maxPolyOrderB : maxPolyOrder[bInd],

      if (c+v>4 and maxPolyOrderB > 2) then maxPolyOrderB : 2,
      for polyOrder : minPolyOrderB thru maxPolyOrderB do (
      
        for gridInd : 1 thru length(gridType) do (
          isNonuniform : gridType[gridInd],
          if isNonuniform then (gridStr : "nonuniform") else (gridStr : ""),
          disp(printf(false,sconcat("Creating diff boundary surface gyrokinetic LBO ~a",bName[bInd]," ~ax~av p~a"),gridStr,c,v,polyOrder)),
          for dir : 1 thru v do (
            fname : sconcat("~/max-out/lbo_gyrokinetic_diff_boundary_surf", varsV[dir],"_", gridStr, c, "x", v, "v_", bName[bInd], "_p", polyOrder , ".c"),
            fh : openw(fname),
            funcName : sconcat("lbo_gyrokinetic_diff_boundary_surf", varsV[dir],"_", gridStr, c, "x", v, "v_", bName[bInd], "_p", polyOrder),
            printf(fh, "#include <gkyl_lbo_gyrokinetic_kernels.h> ~%"),
            calcGkLBOBoundaryDiffUpdateInDir(dir, fh, funcName, c, v, bName[bInd], polyOrder, isNonuniform),
            close(fh)
          )
        )
      )
    )
  )
);


