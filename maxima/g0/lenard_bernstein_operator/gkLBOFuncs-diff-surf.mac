load("modal-basis")$
load("out-scripts");
load("recovery")$
load("utilities")$
load(stringproc)$
fpprec : 24$

/* This script generates the kernels for the surface term
   diffusion contribution from Lenard Bernstein operator
   in the gyrokinetic equation. */

varsVAll  : [vpar, mu]$
vIndex1(cdim,vdim) := makelist(i,i,cdim,cdim+vdim-1)$

calcGkLBODiffUpdateInDir(dir, fh, funcNm, cdim, vdim, basisFun, polyOrder, isNonuniform) := block(
  [varsC,bC,varsP,bP,vSub,NC,NP,pDim,vidx1,surfVar,bmagBasis,bType,h_e,f_xx_c,nuVtSqSum_e,diffFac_e,incr_c],
  
  /* Load basis of dimensionality requested. */
  [varsC,bC,varsP,bP,vSub] : loadGkBasis(basisFun, cdim, vdim, polyOrder),

  /* Number of basis monomials. */
  NC : length(bC),  NP : length(bP),

  pDim    : cdim+vdim,
  vidx1   : vIndex1(cdim,vdim),
  surfVar : varsVAll[dir],

  /* Load a basis that only has the variables B depends on (we assume it
     doesn't depend on y in 3x (axi- or azimuthal- symmetry). */
  bmagBasis : copylist(bC),
  for i : 1 thru NC do (
    if not freeof(y,bC[i]) then ( bmagBasis[i] : 0 )
  ),

  /* Construct the recovery polynomial */
  if polyOrder=1 then /* Force p=1 to mean hybrid. */
    bType : "gkhyb"
  else
    bType : basisFun,
  h_e : calcRecov1CellGen(bType, surfVar, varsP, polyOrder, 1,
    dg(makelist(fl[i-1],i,1,NP)), dg(makelist(fc[i-1],i,1,NP)), dg(makelist(fr[i-1],i,1,NP))),
  /*............. RECOVERY DONE ..............................*/
    
  printf(fh, "GKYL_CU_DH void ~a(const double *w, const double *dxv, const double m_, const double *bmag_inv, const double *nuSum, const double *nuPrimMomsSum, const double *fl, const double *fc, const double *fr, double* GKYL_RESTRICT out) ~%{ ~%", funcNm),
  printf(fh, "  // m_: species mass.~%"),
  printf(fh, "  // bmag_inv: 1/(magnetic field magnitude). ~%"),
  printf(fh, "  // w[~a]: cell-center coordinates. ~%", cdim+vdim),
  printf(fh, "  // dxv[~a]: cell spacing. ~%", cdim+vdim),
  printf(fh, "  // nuSum: collisionalities added (self and cross species collisionalities). ~%"),
  printf(fh, "  // nuPrimMomsSum[~a]: sum of bulk velocities and thermal speeds squared times their respective collisionalities. ~%", (1+1)*NC),
  printf(fh, "  // fl/fc/fr: distribution function in cells ~%"),
  printf(fh, "  // out: incremented distribution function in cell ~%"),
  printf(fh, "~%"),

  /* Create a pointer to nuVtSqSum. */
  printf(fh, "  const double *nuVtSqSum = &nuPrimMomsSum[~a];~%", 1*NC),
  printf(fh, "~%"),

  printf(fh, "  double rdvSq4 = 4.0/(dxv[~a]*dxv[~a]); ~%", vidx1[dir], vidx1[dir]),
  printf(fh, "  double incr[~a] = {0.0}; ~%", NP),
  printf(fh, "~%"),
    
  nuVtSqSum_e : doExpand1(nuVtSqSum,bC),
  BmagInv_e   : doExpand1(bmag_inv,bmagBasis),
    
  if dir=1 then (
    diffFac_e : nuVtSqSum_e,
    f_xx_e    : diff(h_e, surfVar, 2)
  ) elseif dir=2 then (
    diffFac_c : calcInnerProdList(varsC,1,bC,2*m_*BmagInv_e*nuVtSqSum_e),
    printf(fh, "  double diffFac[~a] = {0.}; ~%", NC),
    writeCExprs1(diffFac, diffFac_c),
    printf(fh, "~%"),

    diffFacNoZeros_c : doMakeExprLst(diffFac_c, diffFac),
    diffFac_e : doExpand(diffFacNoZeros_c, bC),
    f_xx_e    : diff((w[cdim+1]+(dxv[cdim+1]/2)*surfVar)*diff(h_e, surfVar), surfVar)
  ),

  if (polyOrder=1 or sdowncase(basisFun)="tensor") then (
    /* This simplification only works for p=1 because it is a tensor product of
       conf and velocity space (remains to be checked for tensor basis). */

    /* Write out the second derivative of the recovered distribution. */
    printf(fh, "  double f_xx[~a] = {0.0}; ~%", NP),
    f_xx_c : calcInnerProdList(varsP, 1, bP, f_xx_e),
    writeCExprs1(f_xx, f_xx_c),
    printf(fh, "~%"),

    f_xx_e : doExpand(makelistNoZeros(f_xx_c, f_xx),bP)
  ),

  incr_c : calcInnerProdList(varsP, diffFac_e, bP, f_xx_e),
  writeCExprs1(incr, incr_c),
  printf(fh, "~%"),
  
  writeCIncrExprs1(out, rdvSq4*makelistNoZeros(incr_c, incr)), 
  printf(fh, "} ~%")
);
