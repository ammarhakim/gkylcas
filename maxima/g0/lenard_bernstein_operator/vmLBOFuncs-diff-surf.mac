load("modal-basis")$
load("out-scripts");
load("recovery")$
load(stringproc)$
fpprec : 24$

/* This script generates the kernels for the surface term
   diffusion contribution from Lenard Bernstein operator. */

vvarsAll : [vx, vy, vz]$
vidx1(cdim,vdim) := makelist(i,i,cdim,cdim+vdim-1)$

calcVmLBODiffUpdateInDir(dir, fh, funcNm, cdim, vdim, basisFun, polyOrder, isNonuniform) := block(
  [varsC,bC,varsP,bP,NP,NC,pDim,vid1,vv,surfVars,bType,h_e,nuVtSqSum_e,temp_diff_c,diff_incr_c],

  /* Load basis of dimensionality requested. */
  [varsC,bC,varsP,bP] : loadPhaseBasis(basisFun, cdim, vdim, polyOrder),

  /* Number of basis monomials. */
  NP : length(bP),  NC : length(bC),
  
  pDim : cdim+vdim,
  /* Variables. */
  vid1 : vidx1(cdim,vdim),
  vv   : vvarsAll[dir],
  surfVars : delete(vv,varsP),

  /* Construct the recovery polynomial */
  if polyOrder=1 then /* Force p=1 to mean hybrid. */
    bType : "hyb"
  else
    bType : basisFun,
  h_e : calcRecov1CellGen(bType, vv, varsP, polyOrder, 1, dg(makelist(fl[i-1],i,1,NP)), dg(makelist(fc[i-1],i,1,NP)), dg(makelist(fr[i-1],i,1,NP))),
  /*............. RECOVERY DONE ..............................*/
    
  printf(fh, "GKYL_CU_DH void ~a(const double *w, const double *dxv, const double *nuSum, const double *nuUSum, const double *nuVtSqSum, const double *fl, const double *fc, const double *fr, double* GKYL_RESTRICT out) ~%{ ~%", funcNm),
  printf(fh, "  // w[~a]:         cell-center coordinates. ~%", cdim+vdim),
  printf(fh, "  // dxv[~a]:       cell spacing. ~%", cdim+vdim),
  printf(fh, "  // nuSum:         collisionalities added (self and cross species collisionalities). ~%"),
  printf(fh, "  // nuUSum[~a]:    sum of bulk velocities times their respective collisionalities. ~%", vdim*NC),
  printf(fh, "  // nuVtSqSum[~a]: sum of thermal speeds squared time their respective collisionalities. ~%", NC),
  printf(fh, "  // fl/fc/fr:      distribution function in cells ~%"),
  printf(fh, "  // out:           incremented distribution function in cell ~%"),
  printf(fh, "  double rdvSq4 = 4.0/(dxv[~a]*dxv[~a]); ~%", vid1[dir], vid1[dir]),
  printf(fh, "  double temp_diff[~a] = {0.0}; ~%", NP),
  printf(fh, "  double diff_incr[~a] = {0.0}; ~%", NP),
  printf(fh, "~%"),
    
  /* Write out the second derivative of the recovered distribution. */
  nuVtSqSum_e : doExpand1(nuVtSqSum,bC),
    
  temp_diff_c : calcInnerProdList(varsP, 1, bP, diff(h_e, vv, 2)),
  writeCExprs1(temp_diff, temp_diff_c),
  printf(fh, "~%"),
    
  diff_incr_c : calcInnerProdList(varsP, nuVtSqSum_e, bP, doExpand1(temp_diff,bP)),
  writeCExprs1(diff_incr, diff_incr_c),
  printf(fh, "~%"),
  
  writeCIncrExprs1(out, rdvSq4*makelist(diff_incr[i-1],i,1,NP)), 
  printf(fh, "} ~%")
);


