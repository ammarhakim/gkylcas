calcRecov2CellGenNonuniform(basisNm, recDir, dirs, polyOrder, dxlo, dxup, lo, up) := block(
  /* Returns recovered polynomial across two neighboring cells.
  Inputs:
    basisNm : name of used basis function ("Ser", "Tensor", or "Max");
      NOTE: This makes no difference for 1D recoveries so it should
      ideally be an optional input but I don't see a way around it
      now. --Petr Cagas
    recDir : direction of the recovery (x, y, or z)
    dirs : direction names, e.g. [x, y]
    polyOrder : polynomial order of the inputs
    dxlo :
    dxup :
    lo : can be one of the following
      * dg(A) : A is an array of expansion coefficients corresponding
        to logical space basis functions
      * dg(A, bc(D=_, N=_, val=_)) : a constrained recovery where
        expansion coefficients are used together with boundary
        conditions at the edge
    up : equivalent of `lo` at the upper side
  Outputs:
    a polynomial
  */
  [
  numDims, recDirIdx, perpDirs, recDirPolyOrder,
  vdim, cdim, jnk1, jnk2, pvars, pbasis,
  DoF, r, rExp, rSol, rSub,
  recEqList, projSubList,
  varsaCe1D, varsaCeND,
  baLo1D, baCe1D, baUp1D, baLoND, baCeND, baUpND,
  dimProjLo, dimProjUp, qLo1D, qUp1D,
  wx, wy, wz,
  baCeNDw,
  bcExp, baVars,
  shiftList
  ],

  numDims : length(dirs),
  for i : 1 thru numDims do (
    if recDir=dirs[i] then recDirIdx : i
  ),

  perpDirs : delete(recDir, dirs),

  recDirPolyOrder : polyOrder,

  /* Get 1D basis in recovery direction. */
  if basisNm="hyb" or basisNm="gkhyb" then (
    /* Do things differently for hybrid because it may involve a p=2 direction. */
    vdim : length(sublist(dirs,lambda([var],charat(string(var),1)="v" or var=vpar or var=mu))),
    cdim : numDims - vdim,
    if basisNm = "hyb" then (
      [jnk1,jnk2,pvars,pbasis] : loadPhaseBasis(basisNm, cdim, vdim, 1)
    ) elseif basisNm = "gkhyb" then (
      /* Need to do something slightly different for gkhyb because this
         function is called by calcRecov1CellGenNonuniform with just [vpar]
         or [mu], depending on what recovery it is doing, and cdim/vdim
         are not enough to clearly specify which basis should be loaded. */
      [pvars, pbasis] : loadGkHybBasisFromVars(dirs)
    ),
    baCe1D : sqrt(2)^(numDims-1)*sublist(pbasis, lambda([b], lfreeof(perpDirs,listofvars(b)))),
    /* MF 2022/07/15: I think this is only needed when perpDirs=[]. */
    baCe1D : subst(listofvars(baCe1D)[1]=recDir,baCe1D),

    if charat(string(recDir),1)="v" then
      recDirPolyOrder : polyOrder+1
  ) else (
    [varsaCe1D,baCe1D] : loadBasis(basisNm, 1, polyOrder),
    baCe1D : subst(x=recDir, baCe1D)
  ),
  
  baLo1D : etaDir(recDir, -dxlo[recDirIdx]/2, dxlo[recDirIdx], baCe1D),
  baUp1D : etaDir(recDir, dxup[recDirIdx]/2, dxup[recDirIdx], baCe1D),
 
  /* forming and solving the equation system */
  DoF : 0,
  if is(op(lo)=dg) then (
    DoF : DoF + recDirPolyOrder + 1,
    if length(args(lo)) > 1 then (
      if is(op(args(lo)[2])=bc) then (
        DoF : DoF + 1 /* only 1 DoF for BC */
      )
      else (
        DoF : DoF + length(args(args(lo)[2]))
      )
    )
  )
  elseif is(op(lo)=bc) then (
    DoF : DoF + 1 /* only 1 DoF for BC */
  )
  elseif is(op(lo)=bcs) then (
    DoF : DoF + length(args(lo))
  ),
  if is(op(up)=dg) then (
    DoF : DoF + recDirPolyOrder + 1,
    if length(args(up)) > 1 then (
      if is(op(args(up)[2])=bc) then (
        DoF : DoF + 1 /* only 1 DoF for BC */
      )
      else (
        DoF : DoF + length(args(args(up)[2]))
      )
    )
  )
  elseif is(op(up)=bc) then (
    DoF : DoF + 1 /* only 1 DoF for BC */
  )
  elseif is(op(up)=bcs) then (
    DoF : DoF + length(args(up))
  ),
  rExp : doExpand(r, makelist(recDir^i, i, 0, DoF-1)), /* recovery polynomial */

  recEqList : [], /* equation set from the weak equality formulation */
  if is(op(lo)=dg) then (
    recEqList : append(
      recEqList,
      calcInnerProdListGen([recDir], [[-dxlo[recDirIdx], 0]], 1,
        baLo1D, rExp-doExpand(qLo1D, baLo1D))
    ),
    if length(args(lo)) > 1 then (
      if is(op(args(lo)[2])=bc) then (
        recEqList : append(
          recEqList,
          [subst(args(args(lo)[2]), val - D*subst(recDir=-dxlo[recDirIdx], rExp)
            - N*subst(recDir=-dxlo[recDirIdx], diff(rExp, recDir)))]
        )
      )
      elseif is(op(args(lo)[2])=bcs) then (
        for i : 1 thru length(args(args(lo)[2])) do (
          recEqList : append(
            recEqList,
            [subst(args(args(lo)[2])[i], val)
              - subst(recDir=-dxlo[recDirIdx], diff(rExp, recDir, subst(args(args(lo)[2])[i], der)))]
          )
        )
      )
    )
  )
  elseif is(op(lo)=bc) then (
    recEqList : append(
      recEqList,
      [subst(args(lo), val - D*subst(recDir=0, rExp)
        - N*subst(recDir=0, diff(rExp, recDir)))]
    )
  )
  else (
    for i : 1 thru length(args(lo)) do (
      recEqList : append(
        recEqList,
        [subst(args(lo)[i], val)
          - subst(recDir=0, diff(rExp, recDir, subst(args(lo)[i], der)))]
      )
    )
  ),

  if is(op(up)=dg) then (
    recEqList : append(
      recEqList,
      calcInnerProdListGen([recDir], [[0, dxup[recDirIdx]]], 1,
        baUp1D, rExp-doExpand(qUp1D, baUp1D))
    ),
    if length(args(up)) > 1 then (
      if is(op(args(up)[2])=bc) then (
        recEqList : append(
          recEqList,
          [subst(args(args(up)[2]), val - D*subst(recDir=dxup[recDirIdx], rExp)
            - N*subst(recDir=dxup[recDirIdx], diff(rExp, recDir)))]
        )
      )
      elseif is(op(args(up)[2])=bcs) then (
        for i : 1 thru length(args(args(up)[2])) do (
          recEqList : append(
            recEqList,
            [subst(args(args(up)[2])[i], val)
              - subst(recDir=dxup[recDirIdx], diff(rExp, recDir, subst(args(args(up)[2])[i], der)))]
          )
        )
      )
    )
  )
  elseif is(op(up)=bc) then (
    recEqList : append(
      recEqList,
      [subst(args(up), val - D*subst(recDir=0, rExp)
        - N*subst(recDir=0, diff(rExp, recDir)))]
    )
  )
  else (
    for i : 1 thru length(args(up)) do (
      recEqList : append(
        recEqList,
        [subst(args(up)[i], val) - subst(recDir=0, diff(rExp, recDir, subst(args(up)[i], der)))]
      )
    )
  ),

  rSol : linsolve(recEqList, makelist(r[i], i, 1, DoF)),
  rSub : fullratsimp(subst(rSol, rExp)),
  
  /* backsubstitute the non-recovered directtions if needed */
  if numDims > 1 then (
    if basisNm="hyb" or basisNm="gkhyb" then (
      baCeND : copylist(pbasis)
    ) else (
      [varsaCeND,baCeND] : loadBasis(basisNm, numDims, polyOrder),
      baVars : listofvars(baCeND),
      baCeND : psubst(makelist(baVars[i]=dirs[i],i,1,numDims), baCeND)
    ),
    /* rescale basis */
    shiftList : makelist(0, i, 1, numDims),
    baLoND : eta(dirs, shiftList, dxlo, baCeND),
    baUpND : eta(dirs, shiftList, dxup, baCeND),
    /* shift basis */
    baLoND : etaDir(recDir, -dxlo[recDirIdx]/2, 2, baLoND),
    baUpND : etaDir(recDir, dxup[recDirIdx]/2, 2, baUpND),
    /* prepare for the case where expansion coefficients at a wall are
    given; i.e., a basis function set with one less dimension is
    needed */
    baCeNDw : basisFromVars(basisNm, perpDirs, polyOrder),
    
    projSubList : [],
    if is(op(lo)=dg) then (
      dimProjLo : (2/dxlo[recDirIdx])*calcInnerProdListGen([recDir], [[-dxlo[recDirIdx], 0]],
        1, baLo1D, doExpand(args(lo)[1], baLoND)),
      projSubList : append (
        projSubList,
        makelist(qLo1D[i]=dimProjLo[i], i, 1, length(baCe1D))
      ),
      if length(args(lo)) > 1 then (
        bcExp : subst(args(args(lo)[2]), dg),
        if not atom(bcExp) then (
          projSubList : append(
            projSubList,
            [val=doExpand(bcExp, baCeNDw)]
          )
        )
      )
    ),
    if is(op(up)=dg) then (
      dimProjUp : (2/dxup[recDirIdx])*calcInnerProdListGen([recDir], [[0, dxup[recDirIdx]]],
        1, baUp1D, doExpand(args(up)[1], baUpND)),
      projSubList : append (
        projSubList,
        makelist(qUp1D[i]=dimProjUp[i], i, 1, length(baCe1D))
      ),
      if length(args(up)) > 1 then (
        bcExp : subst(args(args(up)[2]), dg),
        if not atom(bcExp) then (
          projSubList : append(
            projSubList,
            [val=doExpand(bcExp, baCeNDw)]
          )
        )
      )
    )
  )
  else (
    projSubList : [],
    if is(op(lo)=dg) then (
      projSubList : append (
        projSubList,
        makelist(qLo1D[i]=args(lo)[1][i], i, 1, length(baCe1D))
      )
    ),
    if is(op(up)=dg) then (
      projSubList : append (
        projSubList,
        makelist(qUp1D[i]=args(up)[1][i], i, 1, length(baCe1D))
      )
    )
  ),
  return(expand(subst(projSubList, rSub)))
  ) $

calcRecov2CellGen(basisNm, recDir, dirs, polyOrder, lo, up) := block(
  [dx],
  dx : makelist(2, i, 1, length(dirs)),
  return(calcRecov2CellGenNonuniform(basisNm, recDir, dirs, polyOrder, dx, dx, lo, up))
  )$

calcRecov2Cell(recDir, dirs, polyOrder, lo, up) := calcRecov2CellGen("Ser", recDir, dirs, polyOrder, lo, up)$
