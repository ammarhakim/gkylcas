/* Functions called to generate the kernels for the boundary surface terms at the 
   edge of velocity space due to the  -grad(phi) . del_v(f) term using upwinding
   in the Vlasov-Poisson equation, with the option to add a contribution
   due to external fields via curl(A) where A is the vector potential.
   NOTE: kernels expect phi and A to be scaled by appropriate factors
   i.e., q/m for plasmas, and G*m for self-gravitation systems. */

load("modal-basis")$
load("out-scripts")$
load(stringproc)$
load("scifac")$
load("nodal_operations/nodal_functions")$
load("utilities")$
fpprec : 24$

calcVlasovPoissonStreamBoundaryUpdateInDirNonuniformV(dir, fh, funcNm, cdim, vdim, basisFun, polyOrder) := block(
  [varsC,bC,varsP,bP,varsV,bV,numV,cid,cv,vv,surfVars,bSurf,fSkin_e,fEdge_e,incr],

  [varsC,bC,varsP,bP] : loadPhaseBasis(basisFun, cdim, vdim, polyOrder),
  varsV : makelist(varsP[cdim+i],i,1,vdim),
  if polyOrder=1 then (
    bV : basisFromVars("hyb", varsV, polyOrder)
  ) else (
    bV : basisFromVars(basisFun, varsV, polyOrder)
  ),

  numV : length(bV),

  printf(fh, "GKYL_CU_DH double ~a~a_~ax~av_~a_p~a(const double *w, const double *dxv, const double *vcoord, int edge, const double *fEdge, const double *fSkin, double* GKYL_RESTRICT out) ~%{ ~%", funcNm, varsC[dir], cdim, vdim, basisFun, polyOrder),
  printf(fh, "  // w[NDIM]:     Cell-center coordinates.~%"),
  printf(fh, "  // dxv[NDIM]:   Cell spacing.~%"),
  printf(fh, "  // vcoord:      Discrete (DG) velocity coordinate.~%"),
  printf(fh, "  // edge:        Determines if the update is for the left edge (-1) or right edge (+1).~%"),
  printf(fh, "  // fSkin/fEdge: Input Distribution function in skin cell/last edge cell ~%"),
  printf(fh, "  // out:         Incremented distribution function in center cell.~%"),

  /* Expand the velocity in each direction using a basis that only depends on
     the variable in that direction. */
  vcoord_c : makelist([],d,1,vdim),
  vcoord_e : makelist([],d,1,vdim),
  for d : 1 thru vdim do (
    velBasis   : copylist(bV),
    ignoreVars : listify(setdifference(setify(varsV),setify([varsV[d]]))),
    for i : 1 thru numV do (
      for v : 1 thru length(ignoreVars) do (
        if not freeof(ignoreVars[v],bV[i]) then (
          velBasis[i] : 0
    ))),

    vcoord_c[d] : subst(a0=numV*(d-1), makelist(vcoord[a0+i-1],i,1,numV) ),
    vcoord_e[d] : doExpand(vcoord_c[d],velBasis)
  ),

  cid : makelist(i,i,0,cdim-1),

  /* Surface contribution is defined as integral(phi^- Ghat) over the surface. */

  /* cv = variable in direction of surface update. */
  cv : varsP[dir],
  vv : varsV[dir],

  /* Surface variables to integrate over. */
  surfVars : delete(cv,varsP),

  /* Generate surface basis. this produces the ndim-1 orthogonal basis with no vv dependence. */
  if polyOrder=1 then (  /* Force p=1 to use hybrid basis. */
    bSurf : basisFromVars("hyb",surfVars,polyOrder)
  ) else (
    bSurf : basisFromVars(basisFun,surfVars,polyOrder)
  ),

  /* Create expansions of f_skin, f_edge on volume basis. */
  fSkin_e : doExpand1(fSkin, bP),
  fEdge_e : doExpand1(fEdge, bP),

  printf(fh, "  const double rdx2 = 2/dxv[~a]; ~%", cid[dir]),
  printf(fh, "  double Ghat[~a]; ~%", length(bSurf)),
    printf(fh, "~%"),

  /* Function that computes the surface increment (without a 2/dx factor)
     with upwinding based on the grid velocity. */
  evSurfIncr(fL_e, fR_e, evAt) := block([Ghat_c,GhatNoZero_c,Ghat_e,incrOut],
    printf(fh, "  if (~a>0) { ~%~%",float(subst(vv=0,vcoord_e[dir]))),

    /* Evaluate surface projection of Ghat = v f_left. */
    Ghat_c : calcInnerProdList(surfVars, vcoord_e[dir], bSurf, subst(cv=1, fL_e)),
    writeCExprsCollect1lhsc(Ghat, radcan(Ghat_c)),
    printf(fh, "~%"),
    flush_output(fh),
    /* Zero out components of Ghat which are empty. */
    GhatNoZero_c : makelistNoZeros1(Ghat_c, Ghat),
    Ghat_e       : doExpand(GhatNoZero_c, bSurf),

    printf(fh, "  } else { ~%~%"),

    /* Evaluate surface projection of Ghat = v f_right. */
    Ghat_c : calcInnerProdList(surfVars, vcoord_e[dir], bSurf, subst(cv=-1, fR_e)),

    /* Clever factorization is possible for polyOrder = 1, but for higher
       polynomial orders, factorization is *too* clever and can lead to
       deeply nested parentheses, not ideal for compiler optimizations */
    if (polyOrder = 1) then (
      writeCExprsNoExpand1(Ghat, Ghat_c)
    ) else (
      writeCExprs1(Ghat, Ghat_c)
    ),
    printf(fh, "~%"),
    flush_output(fh),

    /* Zero out components of Ghat which are empty. */
    GhatNoZero_c : makelistNoZeros1(Ghat_c, Ghat),
    Ghat_e       : doExpand(GhatNoZero_c, bSurf),

    printf(fh, "  } ~%"),
    printf(fh, "~%"),

    /* Compute surface contribution integral(phi^- Ghat) on the left/right. */
    incrOut : calcInnerProdList(surfVars, -evAt, subst(cv=evAt, bP), Ghat_e),
    return(incrOut)
  ),

  /* NOTE: WE ARE USING L TO DENOTE THE LEFT EDGE UPDATE AND R TO DENOTE RIGHT EDGE UPDATE OF GLOBAL DOMAIN */
  /* if edge == -1, we are doing the left edge boundary and the skin cell needs to be evauluated at +1 */
  printf(fh, "  if (edge == -1) { ~%~%"),

  incr : evSurfIncr(fSkin_e, fEdge_e, 1.0),
  writeCIncrExprsCollect1lhsc(out, rdx2*incr),

  /* otherwise edge == +1, we are doing the right edge boundary and the skin cell needs to be evauluated at -1 */
  printf(fh, "~%  } else { ~%~%"),

  incr : evSurfIncr(fEdge_e, fSkin_e, -1.0),
  writeCIncrExprsCollect1lhsc(out, rdx2*incr),

  printf(fh, "~%  } ~%"),

  printf(fh, "  return 0.;~%"),  /* Return CFL frequency in volume kernel. */
  printf(fh, "~%"),

  printf(fh, "} ~%")
)$

calcVlasovPoissonBoundaryUpdateInDirNonuniformV(dir, fh, funcNm, cdim, vdim, basisFun, polyOrder) := block(
  [varsC,bC,varsP,bP,varsV,numC,numP,pDim,d,cid,vid,vv,surfVars,bSurf,fSkin_e,fEdge_e,phi_e,acc_e,
   alphaV_c,alphaNoZero,alpha_e,surfNodes,numNodes,alphaOrd_n,surfNodesConfig,numNodesConfig,
   numNodesVel,i,rcoFac,fHatSurf_e,Ghat_c,GhatNoZero,Ghat_e,incr],

  [varsC,bC,varsP,bP] : loadPhaseBasis(basisFun, cdim, vdim, polyOrder),
  varsV : makelist(varsP[cdim+i],i,1,vdim),

  numC  : length(bC),
  numP  : length(bP),
  pDim  : length(varsP),

  printf(fh, "GKYL_CU_DH double ~a~a_~ax~av_~a_p~a(const double *w, const double *dxv, const double *vcoord, const double *field, const int edge, const double *fEdge, const double *fSkin, double* GKYL_RESTRICT out) ~%{ ~%", funcNm, varsV[dir], cdim, vdim, basisFun, polyOrder),
  printf(fh, "  // w:           Cell-center coordinates.~%"),
  printf(fh, "  // dxv[NDIM]:   Cell spacing.~%"),
  printf(fh, "  // vcoord:      Discrete (DG) velocity coordinate.~%"),
  printf(fh, "  // field:       potential (scaled by appropriate factors).~%"),
  printf(fh, "  // edge:        Determines if the update is for the left edge (-1) or right edge (+1).~%"),
  printf(fh, "  // fSkin/fEdge: Input Distribution function in skin cell/last edge cell ~%"),
  printf(fh, "  // out:         Output distribution function in skin cell ~%"),

  cid : makelist(i,i,0,cdim-1),
  vid : makelist(i,i,cdim,cdim+vdim-1),

  printf(fh, "  const double rdv2 = 2/dxv[~a]; ~%", vid[dir]),

  /* access potential data */
  printf(fh, "  const double *phi = &field[~a]; ~%", 0),
  for d : 1 thru cdim do (
    printf(fh, "  const double rd~a2 = 2/dxv[~a]; ~%", varsC[d], cid[d])
  ),
  rdx2Vec : [rdx2,rdy2,rdz2],

  /* Surface contribution is defined as integral(phi^- Ghat) over the surface. */

  /* vv = variable in direction of surface update. */
  vv : varsV[dir],

  /* Surface variables to integrate over. */
  surfVars : delete(vv,varsP),

  /* Generate surface basis. this produces the ndim-1 orthogonal basis with no vv dependence. */
  /* Also generate necessary basis strings and surface nodes given by tensor product of Gauss-Legendre quadrature points */
  if polyOrder=1 then (  /* Force p=1 to use hybrid basis. */
    bSurf : basisFromVars("hyb",surfVars,polyOrder),

    surf_cdim : cdim,  surf_vdim : vdim-1,
    surfNodes : gaussOrdHyb(1+1, surf_cdim, surf_vdim),

    basisStr : sconcat("hyb_", cdim, "x", vdim, "v", "_p", polyOrder)
  ) else (
    bSurf : basisFromVars(basisFun,surfVars,polyOrder), 

    surfNodes : gaussOrd(polyOrder+1, pDim-1),

    basisStr : sconcat(basisFun, "_", cdim+vdim, "x", "_p", polyOrder)
  ),
  NSurf : length(bSurf),
  numNodes : length(surfNodes),

  /* Create expansions of f_skin, f_edge on volume basis. */
  fSkin_e : doExpand1(fSkin, bP),
  fEdge_e : doExpand1(fEdge, bP),

  /* alpha is the phase space flux at the surface. */
  /* NOTE: Due to the form of the Vlasov-Poisson system of equations, this phase space
     flux is the same evaluated at the left edge and right edge in a given direction
     (i.e., the flux does not explicitly depend on the coordinate in the surface direction)
     For example, vx flux is q/m (grad_x phi + vy*(curl(A))_z - vz*(curl(A))_y) */
  printf(fh, "  double alpha[~a] = {0.0}; ~%", NSurf),
  printf(fh, "~%"),

  phi_e : doExpand1(phi, bC),

  /* alpha_v = -fac*grad(phi) */
  /* fac = q/m for plasma, fac = G*m for self gravitating systems */
  /* In case of external fields, also add q/m*curl(A) */
  varsCall : [x, y, z],
  if (dir = 1) then (
    acc_e : -rdx2Vec[dir]*diff(phi_e,varsCall[1])
  ) elseif (dir = 2) then (
    acc_e : -rdx2Vec[dir]*diff(phi_e,varsCall[2])
  ) else (
    acc_e : -rdx2Vec[dir]*diff(phi_e,varsCall[3])
  ),
  /* Project alpha_v onto surface basis, and print resulting coefficients. */
  alphaV_c : fullratsimp(calcInnerProdList(surfVars, 1, bSurf, subst(vv=1, acc_e))),
  writeCExprs1(alpha, alphaV_c),
  printf(fh, "~%"),
  flush_output(fh),
  /* Zero out components of alpha which are empty. */
  alphaNoZero : makelistNoZeros1(alphaV_c, alpha),
  alpha_e     : doExpand(alphaNoZero, bSurf),

  /* Evaluate alpha at ordinates.
     Note: alphaSurf_e is already a surface expansion. 
     In addition, it is continuous and does not vary with respect to the surface variable.
     Thus, there is no need for alpha_l and alpha_r. We only need alpha at the ordinates. */
  alphaOrd_n : gcfac(float(evAtNodes(alpha_e,surfNodes,surfVars))),

  /* Vlasov-Poisson flux does not depend on velocity space surface on which it sits
     i.e., grad(phi) term does not depend on vx, vy, or vz
     so sort quadrature points between configuration space and velocity space */
  /* Get number of configuration space nodes */
  surfNodesConfig : gaussOrd(polyOrder+1, cdim),
  numNodesConfig  : length(surfNodesConfig),
  numNodesVel     : numNodes/numNodesConfig,

  printf(fh, "  double fUpwindQuad[~a] = {0.0};~%", numNodes),
  printf(fh, "  double fUpwind[~a] = {0.0};~%", NSurf),
  printf(fh, "  double Ghat[~a] = {0.0}; ~%", NSurf),
  printf(fh, "~%"),
  /* if edge == -1, we are doing the left edge boundary and the skin cell needs to be evauluated at +1 */
  printf(fh, "  if (edge == -1) { ~%~%"),

  /* Write out fUpwindQuad for the far left edge */
  /* Within the skin cell, we need alpha_r (alpha evaluated at +1)
     just like how the skin cell is evaluated at +1 */
  for i : 1 thru numNodesConfig do (
    printf(fh, "  if (~a > 0) { ~%", alphaOrd_n[i]),
    for j : 1 thru numNodesVel do (
      printf(fh, "    fUpwindQuad[~a] = ~a_surfx~a_eval_quad_node_~a_r(fSkin); ~%", (j-1)+(i-1)*numNodesVel, basisStr, cdim+dir, (j-1)+(i-1)*numNodesVel)
    ),
    printf(fh, "  } else { ~%"),
    for j : 1 thru numNodesVel do (
      printf(fh, "    fUpwindQuad[~a] = ~a_surfx~a_eval_quad_node_~a_l(fEdge); ~%", (j-1)+(i-1)*numNodesVel, basisStr, cdim+dir, (j-1)+(i-1)*numNodesVel)
    ),
    printf(fh, "  } ~%")
  ),

  printf(fh, "~%"),
  printf(fh, "  // Project tensor nodal quadrature basis back onto modal basis. ~%"),
  if polyOrder=1 then  /* Force p=1 to use hybrid basis. */
    printf(fh, "  hyb_~ax~av_p1_vdir_upwind_quad_to_modal(fUpwindQuad, fUpwind); ~%", cdim, vdim)
  else
    printf(fh, "  ~a_~ax_p~a_upwind_quad_to_modal(fUpwindQuad, fUpwind); ~%", basisFun, cdim+vdim, polyOrder),
  printf(fh, "~%"),

  /* Create expansions of f_hat (upwinded f) on the left and right in the surface basis. */
  fHatSurf_e : doExpand1(fUpwind, bSurf),

  /* Print C exprs for Ghat across surface boundary */
  Ghat_c : fullratsimp(innerProd(surfVars, alpha_e, bSurf, fHatSurf_e)),
  writeCExprs1(Ghat, Ghat_c),
  printf(fh, "~%"),
  flush_output(fh),

  /* Expand G_hat in surface basis. */
  Ghat_e : doExpand1(Ghat, bSurf),

  incr : calcInnerProdList(surfVars, -1, subst(vv=1.0, bP), Ghat_e),
  writeCIncrExprsCollect1lhsc(out, rdv2*incr),  

  /* otherwise edge == +1, we are doing the right edge boundary and the skin cell needs to be evauluated at -1 */
  printf(fh, "~%  } else { ~%~%"),

  /* Write out fUpwindQuad for the far right edge */
  /* Within the skin cell, we need alpha_l (alpha evaluated at -1)
     just like how the skin cell is evaluated at -1 */
  for i : 1 thru numNodesConfig do (
    printf(fh, "  if (~a > 0) { ~%", alphaOrd_n[i]),
    for j : 1 thru numNodesVel do (
      printf(fh, "    fUpwindQuad[~a] = ~a_surfx~a_eval_quad_node_~a_r(fEdge); ~%", (j-1)+(i-1)*numNodesVel, basisStr, cdim+dir, (j-1)+(i-1)*numNodesVel)
    ),
    printf(fh, "  } else { ~%"),
    for j : 1 thru numNodesVel do (
      printf(fh, "    fUpwindQuad[~a] = ~a_surfx~a_eval_quad_node_~a_l(fSkin); ~%", (j-1)+(i-1)*numNodesVel, basisStr, cdim+dir, (j-1)+(i-1)*numNodesVel)
    ),
    printf(fh, "  } ~%")
  ),

  printf(fh, "~%"),
  printf(fh, "  // Project tensor nodal quadrature basis back onto modal basis. ~%"),
  if polyOrder=1 then  /* Force p=1 to use hybrid basis. */
    printf(fh, "  hyb_~ax~av_p1_vdir_upwind_quad_to_modal(fUpwindQuad, fUpwind); ~%", cdim, vdim)
  else
    printf(fh, "  ~a_~ax_p~a_upwind_quad_to_modal(fUpwindQuad, fUpwind); ~%", basisFun, cdim+vdim, polyOrder),
  printf(fh, "~%"),

  /* Create expansions of f_hat (upwinded f) on the left and right in the surface basis. */
  fHatSurf_e : doExpand1(fUpwind, bSurf),

  /* Print C exprs for Ghat across surface boundary */
  Ghat_c : fullratsimp(innerProd(surfVars, alpha_e, bSurf, fHatSurf_e)),
  writeCExprs1(Ghat, Ghat_c),
  printf(fh, "~%"),
  flush_output(fh),

  /* Expand G_hat in surface basis. */
  Ghat_e : doExpand1(Ghat, bSurf),

  incr : calcInnerProdList(surfVars, 1, subst(vv=-1.0, bP), Ghat_e),
  writeCIncrExprsCollect1lhsc(out, rdv2*incr),
  printf(fh, "~%  } ~%"),

  printf(fh, "  return 0.;~%"),  /* Return CFL frequency in volume kernel. */
  printf(fh, "~%"),

  printf(fh, "} ~%")
);

calcVlasovPoissonExtEmBoundaryUpdateInDirNonuniformV(dir, fh, funcNm, cdim, vdim, basisFun, polyOrder) := block(
  [varsC,bC,varsP,bP,varsV,numC,numP,pDim,d,cid,vid,vv,surfVars,bSurf,fSkin_e,fEdge_e,phi_e,acc_e,
   alphaV_c,alphaNoZero,alpha_e,surfNodes,numNodes,alphaOrd_n,surfNodesConfig,numNodesConfig,
   numNodesVel,i,rcoFac,fHatSurf_e,Ghat_c,GhatNoZero,Ghat_e,incr],

  [varsC,bC,varsP,bP] : loadPhaseBasis(basisFun, cdim, vdim, polyOrder),
  varsV : makelist(varsP[cdim+i],i,1,vdim),
  if polyOrder=1 then (
    bV : basisFromVars("hyb", varsV, polyOrder)
  ) else (
    bV : basisFromVars(basisFun, varsV, polyOrder)
  ),

  numC : length(bC),
  numV : length(bV),
  numP : length(bP),
  pDim : length(varsP),

  printf(fh, "GKYL_CU_DH double ~a~a_~ax~av_~a_p~a(const double *w, const double *dxv, const double *vcoord, const double *field, const int edge, const double *fEdge, const double *fSkin, double* GKYL_RESTRICT out) ~%{ ~%", funcNm, varsV[dir], cdim, vdim, basisFun, polyOrder),
  printf(fh, "  // w:           Cell-center coordinates.~%"),
  printf(fh, "  // dxv[NDIM]:   Cell spacing.~%"),
  printf(fh, "  // vcoord:      Discrete (DG) velocity coordinate.~%"),
  printf(fh, "  // field:       potentials, including external (scaled by appropriate factors).~%"),
  printf(fh, "  // edge:        Determines if the update is for the left edge (-1) or right edge (+1).~%"),
  printf(fh, "  // fSkin/fEdge: Input Distribution function in skin cell/last edge cell ~%"),
  printf(fh, "  // out:         Output distribution function in skin cell ~%"),

  /* Expand the velocity in each direction using a basis that only depends on
     the variable in that direction. */
  vcoord_c : makelist([],d,1,vdim),
  vcoord_e : makelist(0,d,1,3),
  for d : 1 thru vdim do (
    velBasis   : copylist(bV),
    ignoreVars : listify(setdifference(setify(varsV),setify([varsV[d]]))),
    for i : 1 thru numV do (
      for v : 1 thru length(ignoreVars) do (
        if not freeof(ignoreVars[v],bV[i]) then (
          velBasis[i] : 0
    ))),

    vcoord_c[d] : subst(a0=numV*(d-1), makelist(vcoord[a0+i-1],i,1,numV) ),
    vcoord_e[d] : doExpand(vcoord_c[d],velBasis)
  ),

  cid : makelist(i,i,0,cdim-1),
  vid : makelist(i,i,cdim,cdim+vdim-1),

  printf(fh, "  const double rdv2 = 2/dxv[~a]; ~%", vid[dir]),

  /* access potential data */
  printf(fh, "  const double *phi = &field[~a]; ~%", 0),
  for d : 1 thru cdim do (
    printf(fh, "  const double rd~a2 = 2/dxv[~a]; ~%", varsC[d], cid[d])
  ),
  rdx2Vec : [rdx2,rdy2,rdz2],

  if vdim > 1 then (
    /* If vdim = 1, then no magnetic field. */
    for d : 1 thru vdim do (
      printf(fh, "  const double *A~a = &field[~a]; ~%", d-1, numC*d)
    )
  ),

  /* Surface contribution is defined as integral(phi^- Ghat) over the surface. */

  /* vv = variable in direction of surface update. */
  vv : varsV[dir],

  /* Surface variables to integrate over. */
  surfVars : delete(vv,varsP),

  /* Generate surface basis. this produces the ndim-1 orthogonal basis with no vv dependence. */
  /* Also generate necessary basis strings and surface nodes given by tensor product of Gauss-Legendre quadrature points */
  if polyOrder=1 then (  /* Force p=1 to use hybrid basis. */
    bSurf : basisFromVars("hyb",surfVars,polyOrder),

    surf_cdim : cdim,  surf_vdim : vdim-1,
    surfNodes : gaussOrdHyb(1+1, surf_cdim, surf_vdim),

    basisStr : sconcat("hyb_", cdim, "x", vdim, "v", "_p", polyOrder)
  ) else (
    bSurf : basisFromVars(basisFun,surfVars,polyOrder), 

    surfNodes : gaussOrd(polyOrder+1, pDim-1),

    basisStr : sconcat(basisFun, "_", cdim+vdim, "x", "_p", polyOrder)
  ),
  NSurf : length(bSurf),
  numNodes : length(surfNodes),

  /* Create expansions of f_skin, f_edge on volume basis. */
  fSkin_e : doExpand1(fSkin, bP),
  fEdge_e : doExpand1(fEdge, bP),

  /* alpha is the phase space flux at the surface. */
  /* NOTE: Due to the form of the Vlasov-Poisson system of equations, this phase space
     flux is the same evaluated at the left edge and right edge in a given direction
     (i.e., the flux does not explicitly depend on the coordinate in the surface direction)
     For example, vx flux is q/m (grad_x phi + vy*(curl(A))_z - vz*(curl(A))_y) */
  printf(fh, "  double alpha[~a] = {0.0}; ~%", NSurf),
  printf(fh, "~%"),

  phi_e : doExpand1(phi, bC),

  Afld : [A0, A1, A2],
  Ax_e : doExpand1(Afld[1], bC),
  Ay_e : doExpand1(Afld[2], bC),
  Az_e : doExpand1(Afld[3], bC),

  /* B = curl(A); NOTE: already contains q/m factor */
  varsCall : [x, y, z],
  Bx_e : rdx2Vec[2]*diff(Az_e, varsCall[2]) - rdx2Vec[3]*diff(Ay_e, varsCall[3]),
  By_e : rdx2Vec[3]*diff(Ax_e, varsCall[3]) - rdx2Vec[1]*diff(Az_e, varsCall[1]),
  Bz_e : rdx2Vec[1]*diff(Ay_e, varsCall[1]) - rdx2Vec[2]*diff(Ax_e, varsCall[2]),

  /* alpha_v = -fac*grad(phi) */
  /* fac = q/m for plasma, fac = G*m for self gravitating systems */
  /* In case of external fields, also add q/m*curl(A) */
  if (dir = 1) then (
    acc_e : -rdx2Vec[dir]*diff(phi_e,varsCall[1]) + vcoord_e[2]*Bz_e - vcoord_e[3]*By_e
  ) elseif (dir = 2) then (
    acc_e : -rdx2Vec[dir]*diff(phi_e,varsCall[2]) + vcoord_e[3]*Bx_e - vcoord_e[1]*Bz_e
  ) else (
    acc_e : -rdx2Vec[dir]*diff(phi_e,varsCall[3]) + vcoord_e[1]*By_e - vcoord_e[2]*Bx_e
  ),
  /* Project alpha_v onto surface basis, and print resulting coefficients. */
  alphaV_c : fullratsimp(calcInnerProdList(surfVars, 1, bSurf, subst(vv=1, acc_e))),
  writeCExprs1(alpha, alphaV_c),
  printf(fh, "~%"),
  flush_output(fh),
  /* Zero out components of alpha which are empty. */
  alphaNoZero : makelistNoZeros1(alphaV_c, alpha),
  alpha_e     : doExpand(alphaNoZero, bSurf),

  /* Evaluate alpha at ordinates.
     Note: alphaSurf_e is already a surface expansion. 
     In addition, it is continuous and does not vary with respect to the surface variable.
     Thus, there is no need for alpha_l and alpha_r. We only need alpha at the ordinates. */
  alphaOrd_n : gcfac(float(evAtNodes(alpha_e,surfNodes,surfVars))),

  printf(fh, "  double fUpwindQuad[~a] = {0.0};~%", numNodes),
  printf(fh, "  double fUpwind[~a] = {0.0};~%", NSurf),
  printf(fh, "  double Ghat[~a] = {0.0}; ~%", NSurf),
  printf(fh, "~%"),
  /* NOTE: WE ARE USING L TO DENOTE THE LEFT EDGE UPDATE AND R TO DENOTE RIGHT EDGE UPDATE OF GLOBAL DOMAIN */
  /* if edge == -1, we are doing the left edge boundary and the skin cell needs to be evauluated at +1 */
  printf(fh, "  if (edge == -1) { ~%~%"),

  /* Write out fUpwindQuad for the far left edge */
  for i : 1 thru length(alphaOrd_n) do (
    printf(fh, "  if (~a > 0) { ~%", alphaOrd_n[i]),
    printf(fh, "    fUpwindQuad[~a] = ~a_surfx~a_eval_quad_node_~a_r(fSkin); ~%", i-1, basisStr, cdim+dir, i-1),
    printf(fh, "  } else { ~%"),
    printf(fh, "    fUpwindQuad[~a] = ~a_surfx~a_eval_quad_node_~a_l(fEdge); ~%", i-1, basisStr, cdim+dir, i-1),
    printf(fh, "  } ~%")
  ),
  printf(fh, "~%"),
  printf(fh, "  // Project tensor nodal quadrature basis back onto modal basis. ~%"),
  if polyOrder=1 then  /* Force p=1 to use hybrid basis. */
    printf(fh, "  hyb_~ax~av_p1_vdir_upwind_quad_to_modal(fUpwindQuad, fUpwind); ~%", cdim, vdim)
  else
    printf(fh, "  ~a_~ax_p~a_upwind_quad_to_modal(fUpwindQuad, fUpwind); ~%", basisFun, cdim+vdim, polyOrder),
  printf(fh, "~%"),

  /* Create expansions of f_hat (upwinded f) in the surface basis. */
  fHatSurf_e : doExpand1(fUpwind, bSurf),

  /* Print C exprs for Ghat across surface boundary */
  Ghat_c : fullratsimp(innerProd(surfVars, alpha_e, bSurf, fHatSurf_e)),
  writeCExprs1(Ghat, Ghat_c),
  printf(fh, "~%"),
  flush_output(fh),

  /* Expand G_hat in surface basis. */
  Ghat_e : doExpand1(Ghat, bSurf),

  incr : calcInnerProdList(surfVars, -1, subst(vv=1.0, bP), Ghat_e),
  writeCIncrExprsCollect1lhsc(out, rdv2*incr),  

  /* otherwise edge == +1, we are doing the right edge boundary and the skin cell needs to be evauluated at -1 */
  printf(fh, "~%  } else { ~%~%"),

  /* Write out fUpwindQuad for the far right edge */
  for i : 1 thru length(alphaOrd_n) do (
    printf(fh, "  if (~a > 0) { ~%", alphaOrd_n[i]),
    printf(fh, "    fUpwindQuad[~a] = ~a_surfx~a_eval_quad_node_~a_r(fEdge); ~%", i-1, basisStr, cdim+dir, i-1),
    printf(fh, "  } else { ~%"),
    printf(fh, "    fUpwindQuad[~a] = ~a_surfx~a_eval_quad_node_~a_l(fSkin); ~%", i-1, basisStr, cdim+dir, i-1),
    printf(fh, "  } ~%")
  ),
  printf(fh, "~%"),
  printf(fh, "  // Project tensor nodal quadrature basis back onto modal basis. ~%"),
  if polyOrder=1 then  /* Force p=1 to use hybrid basis. */
    printf(fh, "  hyb_~ax~av_p1_vdir_upwind_quad_to_modal(fUpwindQuad, fUpwind); ~%", cdim, vdim)
  else
    printf(fh, "  ~a_~ax_p~a_upwind_quad_to_modal(fUpwindQuad, fUpwind); ~%", basisFun, cdim+vdim, polyOrder),
  printf(fh, "~%"),

  /* Create expansions of f_hat (upwinded f) in the surface basis. */
  fHatSurf_e : doExpand1(fUpwind, bSurf),

  /* Print C exprs for Ghat across surface boundary */
  Ghat_c : fullratsimp(innerProd(surfVars, alpha_e, bSurf, fHatSurf_e)),
  writeCExprs1(Ghat, Ghat_c),
  printf(fh, "~%"),
  flush_output(fh),

  /* Expand G_hat in surface basis. */
  Ghat_e : doExpand1(Ghat, bSurf),

  incr : calcInnerProdList(surfVars, 1, subst(vv=-1.0, bP), Ghat_e),
  writeCIncrExprsCollect1lhsc(out, rdv2*incr),
  printf(fh, "~%  } ~%"),

  printf(fh, "  return 0.;~%"),  /* Return CFL frequency in volume kernel. */
  printf(fh, "~%"),

  printf(fh, "} ~%")
);
