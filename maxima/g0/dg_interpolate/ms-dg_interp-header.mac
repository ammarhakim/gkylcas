/*
  Generate the kernels header file for the updater which interpolates
  a DG field from one grid to another (dimensionality of the grids is
  the same, only resolution varies).
*/
load("modal-basis")$

/* ...... USER INPUTS........ */

/* Serendipity basis. */
minPolyOrder_Ser : 1$
maxPolyOrder_Ser : 2$
minCdim_Ser : 1$
maxCdim_Ser : 3$

/* Tensor order basis. No need to generate p=1. */
minPolyOrder_Tensor : 2$
maxPolyOrder_Tensor : 2$
minCdim_Tensor : 1$
maxCdim_Tensor : 0$

/* ...... END OF USER INPUTS........ */

/* Vdim possibilities for each of Cdim=[1,2,3]. */
vlasovVdims : [[1,2,3],[2,3],[3]]$
gkVdims : [[1,2], [2], [2]]$

/* To generate other bases, just add corresponding column to arrays below. */
bName        : ["ser", "tensor"]$
minPolyOrder : [minPolyOrder_Ser, minPolyOrder_Tensor]$
maxPolyOrder : [maxPolyOrder_Ser, maxPolyOrder_Tensor]$
minCdim      : [minCdim_Ser, minCdim_Tensor]$
maxCdim      : [maxCdim_Ser, maxCdim_Tensor]$

printPrototypes(fh) := block(
  [bInd,c,gkV,v,vI,maxPolyOrderB,polyOrder],

  for bInd : 1 thru length(bName) do (

    /* Conf-space kernels. */
    for c : minCdim[bInd] thru maxCdim[bInd] do (
      for polyOrder : minPolyOrder[bInd] thru maxPolyOrder[bInd] do (

        [varsP,jnk] : loadBasis(bName[bInd], c, polyOrder),

        /* Generate a kernel for interpolating along each direction. */
        for dir : 1 thru c do (
          printf(fh, "GKYL_CU_DH void dg_interpolate_~ax_~a_p~a_~a(const double *wDo, const double *wTar, const double *dxDo, const double *dxTar, const double *fldDo, double *fldTar);~%", c, bName[bInd], polyOrder, varsP[dir])
        ),
        printf(fh, "~%")
      )
    ),
    printf(fh, "~%"),

    /* Vlasov kernels. */
    for c : minCdim[bInd] thru maxCdim[bInd] do (
      for vI : 1 thru length(vlasovVdims[c]) do (
        v : vlasovVdims[c][vI],

        maxPolyOrderB : maxPolyOrder[bInd],
        if (c=3) then maxPolyOrderB : 1, /* Only generate p=1 kernels for 3x2v */
        for polyOrder : minPolyOrder[bInd] thru maxPolyOrderB do (

          [jnk,jnk,varsP,jnk] : loadPhaseBasis(bName[bInd], c, v, polyOrder),

          /* Generate a kernel for interpolating along each direction. */
          for dir : 1 thru c+v do (
            printf(fh, "GKYL_CU_DH void dg_interpolate_vlasov_~ax~av_~a_p~a_~a(const double *wDo, const double *wTar, const double *dxDo, const double *dxTar, const double *fldDo, double *fldTar);~%", c, v, bName[bInd], polyOrder, varsP[dir])
          ),
          printf(fh, "~%")

        )
      )
    ),
    printf(fh, "~%"),

    /* Gyrokinetic kernels. */
    for c : minCdim[bInd] thru maxCdim[bInd] do (
      for gkV : 1 thru length(gkVdims[c]) do (
        v : gkVdims[c][gkV],

        maxPolyOrderB : maxPolyOrder[bInd],
        if (c=3) then maxPolyOrderB : 1, /* Only generate p=1 kernels for 3x2v */
        for polyOrder : minPolyOrder[bInd] thru maxPolyOrderB do (

          [jnk,jnk,varsP,jnk,jnk] : loadGkBasis(bName[bInd], c, v, polyOrder),

          /* Generate a kernel for interpolating along each direction. */
          for dir : 1 thru c+v do (
            printf(fh, "GKYL_CU_DH void dg_interpolate_gyrokinetic_~ax~av_~a_p~a_~a(const double *wDo, const double *wTar, const double *dxDo, const double *dxTar, const double *fldDo, double *fldTar);~%", c, v, bName[bInd], polyOrder, varsP[dir])
          ),
          printf(fh, "~%")

        )
      )
    ),
    printf(fh, "~%")
  )
)$

fh : openw("~/max-out/gkyl_dg_interpolate_kernels.h")$
printf(fh, "#pragma once~%")$
printf(fh, "~%")$
printf(fh, "#include <gkyl_util.h>~%")$
printf(fh, "#include <math.h>~%")$
printf(fh, "~%")$
printf(fh, "EXTERN_C_BEG~%")$
printf(fh, "~%")$
printPrototypes(fh)$
printf(fh, "~%")$
printf(fh, "EXTERN_C_END~%")$
close(fh)$
