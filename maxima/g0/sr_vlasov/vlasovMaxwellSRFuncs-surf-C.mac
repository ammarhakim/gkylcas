/* This script generates the kernels for the surface term of the E + v x B 
   advection in velocity space for the Vlasov equation (full EM) */

load("modal-basis");
load("out-scripts");
load(stringproc)$
load("scifac")$
load("nodal_operations/nodal_functions")$
fpprec : 24$

varsV : [vx, vy, vz]$
cvars : [x, y, z]$
vvars : [vx, vy, vz]$

pVsub : [x=vx,y=vy,z=vz]$

dvdx  : [dv0dx0, dv1dx1, dv2dx2]$
wdx   : [w0dx0, w1dx1, w2dx2]$
dx11 : [dx10, dx11, dx12]$
dv11 : [dv10, dv11, dv12]$

Efld  : [E0, E1, E2]$
Bfld  : [B0, B1, B2]$
p_over_gamma_fld : [p0_over_gamma, p1_over_gamma, p2_over_gamma]$

ax(vdim, bc, bv) := 
  if vdim = 1 then
    doExpand1(Efld[1],bc) 
  else if vdim = 2 then
    doExpand1(Efld[1],bc) + doExpand1(p_over_gamma_fld[2], bv)*doExpand1(Bfld[3],bc)
  else 
    doExpand1(Efld[1],bc) + doExpand1(p_over_gamma_fld[2], bv)*doExpand1(Bfld[3],bc) - doExpand1(p_over_gamma_fld[3], bv)*doExpand1(Bfld[2],bc)$

ay(vdim, bc, bv) := 
  if vdim = 2 then
    doExpand1(Efld[2],bc) - doExpand1(p_over_gamma_fld[1], bv)*doExpand1(Bfld[3],bc)
  else
    doExpand1(Efld[2],bc) + doExpand1(p_over_gamma_fld[3], bv)*doExpand1(Bfld[1],bc) - doExpand1(p_over_gamma_fld[1], bv)*doExpand1(Bfld[3],bc)$

az(vdim, bc, bv) := doExpand1(Efld[3],bc) + doExpand1(p_over_gamma_fld[1], bv)*doExpand1(Bfld[2],bc) - doExpand1(p_over_gamma_fld[2], bv)*doExpand1(Bfld[1],bc)$

calcAccel(vdir, vdim, bc, bv) :=
  if vdir = 1
    then ax(vdim, bc, bv)
  elseif vdir = 2
    then ay(vdim, bc, bv)
  else
    az(vdim, bc, bv)$

slcn(lst, n) := makelist(lst[i], i, 1, n)$

cidx(cdim)      := makelist(i,i,0,cdim-1)$
vidx(cdim,vdim) := makelist(i,i,cdim,cdim+vdim-1)$

calcVlasovSRStreamUpdateInDir(dir, fh, funcNm, cdim, vdim, basisFun, polyOrder) := block(
  [bP,numBasisP,cid,vid,wvl,wvr,dvl,dvr,cv,vv,surfVars,fL_e,fR_e,fhat,surfTerm,incr_r,clst,incr_s],

  kill(varsC, varsP, basisC, basisP),
  load(sconcat("basis-precalc/basis", basisFun, vdim, "x")),
  /* Generate basis in velocity space only. */
  bV : subst(pVsub,basisC[polyOrder]),
  NV : length(bV),

  kill(varsC, varsP, basisC, basisP),

  load(sconcat("basis-precalc/basis", basisFun, cdim, "x", vdim, "v")),

  bP        : basisP[polyOrder],
  numBasisP : length(bP),

  printf(fh, "GKYL_CU_DH double ~a~a_~ax~av_~a_p~a(const double *w, const double *dxv, const double *p_over_gamma, const double *fl, const double *fc, const double *fr, double* GKYL_RESTRICT out) ~%{ ~%", funcNm, cvars[dir], cdim, vdim, basisFun, polyOrder),
  printf(fh, "  // w[NDIM]:   Cell-center coordinates.~%"),
  printf(fh, "  // dxv[NDIM]: Cell spacing.~%"),
  printf(fh, "  // p_over_gamma: p/gamma (velocity).~%"),
  printf(fh, "  // fl/fc/fr:  Input Distribution function in left/center/right cells.~%"),
  printf(fh, "  // out:       Incremented distribution function in center cell.~%"),

  cid : cidx(cdim),
  vid : vidx(cdim,vdim),

  /* Surface contribution is defined as integral(phi^- Ghat) over the surface. */

  /* cv = variable in direction of surface update. */
  cv : varsP[dir],
  vv : varsV[dir],

  /* Surface variables to integrate over. */
  surfVars : delete(cv,varsP),

  /* Generate surface basis. this produces the ndim-1 orthogonal basis with no vv dependence. */
  bSurf : sqrt(2)*delete(0,fullratsimp(innerProd(varsP,1,bP,subst([cv^2=var2,var2=1/3,cv=0],bP)))*bP),

  printf(fh, "  const double *p~a_over_gamma = &p_over_gamma[~a]; ~%", dir-1, NV*(dir-1)),
  /* alpha is the phase space flux at the surface. */
  printf(fh, "  double alpha[~a] = {0.0}; ~%", length(bSurf)),

  /* Project alpha_v on the left and right onto surface basis, and print resulting coefficients. */
  alpha_c : fullratsimp(calcInnerProdList(surfVars, 1, bSurf, doExpand1(p_over_gamma_fld[dir], bV))),
  writeCExprs1(alpha, alpha_c),
  printf(fh, "~%"),
  flush_output(fh),
  /* Zero out components of alpha which are empty. */
  alphaNoZero : makelistNoZeros1(alpha_c, alpha),
  alpha_e     : doExpand(alphaNoZero, bSurf),

  /* Create expansions of f_left, f_center, and f_right on volume basis. */
  fl_e : doExpand1(fl, bP),
  fc_e : doExpand1(fc, bP),
  fr_e : doExpand1(fr, bP),
  printf(fh, "  const double dx1~a = 2/dxv[~a]; ~%", dir-1, cid[dir]),
  printf(fh, "  const double dv = dxv[~a], wv = w[~a]; ~%", vid[dir], vid[dir]),
  printf(fh, "  double Ghat_r[~a]; ~%", length(bSurf)),
  printf(fh, "  double Ghat_l[~a]; ~%", length(bSurf)),
  
  printf(fh, "  if (~a>0) { ~%~%",wv),

  /* Because v > 0, need left edge value */
  /* Evaluate surface projection of Ghat_r = v f_c. */
  Ghat_r_c : calcInnerProdList(surfVars, alpha_e, bSurf, subst(cv=1, fc_e)),
  writeCExprsCollect1lhsc(Ghat_r, radcan(Ghat_r_c)),
  printf(fh, "~%"),
  flush_output(fh),
  /* Zero out components of Ghat which are empty. */
  GhatNoZero_r : makelistNoZeros1(Ghat_r_c, Ghat_r),
  Ghat_r_e     : doExpand(GhatNoZero_r, bSurf),

  /* Evaluate surface projection of Ghat_l = v f_l. */
  Ghat_l_c : calcInnerProdList(surfVars, alpha_e, bSurf, subst(cv=1, fl_e)),
  writeCExprsCollect1lhsc(Ghat_l, radcan(Ghat_l_c)),
  printf(fh, "~%"),
  flush_output(fh),
  /* Zero out components of Ghat which are empty. */
  GhatNoZero_l : makelistNoZeros1(Ghat_l_c, Ghat_l),
  Ghat_l_e     : doExpand(GhatNoZero_l, bSurf),

  printf(fh, "  } else { ~%~%"),
  
  /* Because v < 0, need right edge value */
  /* Evaluate surface projection of Ghat_r = v f_r. */
  Ghat_r_c : calcInnerProdList(surfVars, alpha_e, bSurf, subst(cv=-1, fr_e)),

  /* Clever factorization is possible for polyOrder = 1, but for higher
     polynomial orders, factorization is *too* clever and can lead to 
     deeply nested parentheses, not ideal for compiler optimizations */
  if (polyOrder = 1) then (
    writeCExprsNoExpand1(Ghat_r, Ghat_r_c)
  ) else (
    writeCExprs1(Ghat_r, Ghat_r_c)
  ),
  printf(fh, "~%"),
  flush_output(fh),

  /* Zero out components of Ghat which are empty. */
  GhatNoZero_r : makelistNoZeros1(Ghat_r_c, Ghat_r),
  Ghat_r_e     : doExpand(GhatNoZero_r, bSurf),

  /* Evaluate surface projection of Ghat_l = v f_c. */
  Ghat_l_c : calcInnerProdList(surfVars, alpha_e, bSurf, subst(cv=-1, fc_e)),

  /* Clever factorization is possible for polyOrder = 1, but for higher
     polynomial orders, factorization is *too* clever and can lead to 
     deeply nested parentheses, not ideal for compiler optimizations */
  if (polyOrder = 1) then (
    writeCExprsNoExpand1(Ghat_l, Ghat_l_c)
  ) else (
    writeCExprs1(Ghat_l, Ghat_l_c)
  ),
  printf(fh, "~%"),
  flush_output(fh),

  /* Zero out components of Ghat which are empty. */
  GhatNoZero_l : makelistNoZeros1(Ghat_l_c, Ghat_l),
  Ghat_l_e     : doExpand(GhatNoZero_l, bSurf),
  
  printf(fh, "  } ~%"),
  /* Compute surface contribution integral(phi^- Ghat) on the right. */
  incr_l : calcInnerProdList(surfVars, 1, subst(cv=-1.0, bP), Ghat_l_e),
  incr_r : calcInnerProdList(surfVars, -1, subst(cv=1.0, bP), Ghat_r_e),
  writeCIncrExprsCollect1lhsc(out, dx11[dir]*incr_l + dx11[dir]*incr_r),    
  printf(fh, "~%"),

  printf(fh, "  return 0.;~%"), /* Return CFL frequency in volume kernel. */
  printf(fh, "~%"),

  printf(fh, "} ~%")
);

calcVlasovSRLorentzUpdateInDir(dir, fh, funcNm, cdim, vdim, basisFun, polyOrder) := block(
  [],
  kill(varsC, varsP, basisC, basisP),
  load(sconcat("basis-precalc/basis", basisFun, vdim, "x")),
  /* Generate basis in velocity space only. */
  bV : subst(pVsub,basisC[polyOrder]),
  NV : length(bV), 

  kill(varsC, varsP, basisC, basisP),
  modNm : sconcat("basis-precalc/basis", basisFun, cdim, "x", vdim, "v"),
  load(modNm),

  bP    : basisP[polyOrder],
  bC    : basisC[polyOrder],
  varsC : slcn(varsP, cdim),
  numC  : length(bC),
  numP  : length(bP),
  pDim  : length(varsP),

  printf(fh, "GKYL_CU_DH double ~a~a_~ax~av_~a_p~a(const double *w, const double *dxv, const double *p_over_gamma, const double *qmem, const double *fl, const double *fc, const double *fr, double* GKYL_RESTRICT out) ~%{ ~%", funcNm, vvars[dir], cdim, vdim, basisFun, polyOrder),
  printf(fh, "  // w:         Cell-center coordinates.~%"),
  printf(fh, "  // dxv[NDIM]: Cell spacing.~%"),
  printf(fh, "  // p_over_gamma:      p/gamma (velocity).~%"),
  printf(fh, "  // qmem:      q/m*EM fields.~%"),
  printf(fh, "  // fl/fc/fr:  Input Distribution function in left/center/right cells ~%"),
  printf(fh, "  // out:       Output distribution function in center cell ~%"),

  cid : cidx(cdim),
  vid : vidx(cdim,vdim),

  printf(fh, "  const double dv1~a = 2/dxv[~a]; ~%", dir-1, vid[dir]),
  for d : 1 thru vdim do (
    printf(fh, "  const double dv~a = dxv[~a], wv~a = w[~a]; ~%", d, vid[d], d, vid[d])
  ),

  printf(fh, "  const double *E~a = &qmem[~a]; ~%", dir-1, numC*(dir-1)),
  /* If vdim = 1, then no magnetic field. */
  if vdim > 1 then (
    if vdim = 2 then (
      for d : 1 thru 2 do (
        printf(fh, "  const double *p~a_over_gamma = &p_over_gamma[~a]; ~%", d-1, NV*(d-1))
      ),
      /* In 2 velocity dimensions, only Bz. */
      printf(fh, "  const double *B~a = &qmem[~a]; ~%", 2, numC*2+3*numC)
    ),
    if vdim = 3 then (
      for d : 1 thru 3 do (
        printf(fh, "  const double *p~a_over_gamma = &p_over_gamma[~a]; ~%", d-1, NV*(d-1)),
        printf(fh, "  const double *B~a = &qmem[~a]; ~%", d-1, numC*(d-1)+3*numC)
      )            
    )
  ),
  printf(fh, "~%"),

  /* vv = variable in direction of surface update. */
  vv : varsV[dir],

  /* Surface variables to integrate over. */
  surfVars : delete(vv,varsP),

  /* Generate surface basis. this produces the ndim-1 orthogonal basis with no vv dependence. */
  bSurf : sqrt(2)*delete(0,fullratsimp(innerProd(varsP,1,bP,subst([vv^2=var2,var2=1/3,vv=0],bP)))*bP),

  /* alpha is the phase space flux at the surface. */
  printf(fh, "  double alpha_l[~a] = {0.0}; ~%", length(bSurf)),
  printf(fh, "  double alpha_r[~a] = {0.0}; ~%", length(bSurf)),
  printf(fh, "~%"),

  /* alpha_v = q/m*(E + p/gamma x B) dot d/dv(f) */
  acc_e : calcAccel(dir, vdim, bC, bV),

  /* Project alpha_v on the left and right onto surface basis, and print resulting coefficients. */
  alphaV_l_c : fullratsimp(calcInnerProdList(surfVars, 1, bSurf, subst(vv=-1, acc_e))),
  writeCExprs1(alpha_l, alphaV_l_c),
  printf(fh, "~%"),
  flush_output(fh),
  /* Zero out components of alpha which are empty. */
  alphaNoZero_l : makelistNoZeros1(alphaV_l_c, alpha_l),
  alpha_l_e     : doExpand(alphaNoZero_l, bSurf),

  alphaV_r_c : fullratsimp(calcInnerProdList(surfVars, 1, bSurf, subst(vv=1, acc_e))),
  writeCExprs1(alpha_r, alphaV_r_c),
  printf(fh, "~%"),
  flush_output(fh),
  /* Zero out components of alpha which are empty. */
  alphaNoZero_r : makelistNoZeros1(alphaV_r_c, alpha_r),
  alpha_r_e     : doExpand(alphaNoZero_r, bSurf),

  /* Surface nodes given by tensor product of Gauss-Legendre quadrature points */
  surfNodes : gaussOrd(polyOrder+1, pDim-1),
  numNodes : length(surfNodes),

  /* Evaluate alpha at ordinates.
     Note: alphaSurf_e is already a surface expansion. 
     In addition, it is continuous */
  alphaOrd_l_n : gcfac(float(evAtNodes(alpha_l_e,surfNodes,surfVars))),
  alphaOrd_r_n : gcfac(float(evAtNodes(alpha_r_e,surfNodes,surfVars))),

  printf(fh, "  double fUpwindQuad_l[~a] = {0.0};~%", numNodes),
  printf(fh, "  double fUpwindQuad_r[~a] = {0.0};~%", numNodes),
  printf(fh, "  double fUpwind_l[~a] = {0.0};;~%", length(bSurf)),
  printf(fh, "  double fUpwind_r[~a] = {0.0};~%", length(bSurf)),
  printf(fh, "  double Ghat_l[~a] = {0.0}; ~%", length(bSurf)),
  printf(fh, "  double Ghat_r[~a] = {0.0}; ~%", length(bSurf)),
  printf(fh, "~%"),
  /* Check sign of each quadrature point and choose appropriate distribution function at 
     quadrature points. If alpha > 0, need f_l (at +1) and f_c (at +1). If alpha < 0, 
     need f_c (at -1) and f_r (at -1). */
  for i : 1 thru length(alphaOrd_l_n) do (
    /* This subst eliminates the need for another variable, and removes
        the common factor (for p=1) which is not needed to determine
        sign (not working for p>1). */
    if polyOrder=1 then (
      rcoFac : 1./(content(alphaOrd_l_n[1],alpha_l[0])[1]),
      printf(fh, "  if (~a > 0) { ~%", fullratsimp(alphaOrd_l_n[i]*rcoFac)),
      printf(fh, "    fUpwindQuad_l[~a] = ~a_~ax_p~a_surfx~a_eval_quad_node_~a_r(fl); ~%", i-1, basisFun, cdim+vdim, polyOrder, cdim+dir, i-1),
      printf(fh, "  } else { ~%"),
      printf(fh, "    fUpwindQuad_l[~a] = ~a_~ax_p~a_surfx~a_eval_quad_node_~a_l(fc); ~%", i-1, basisFun, cdim+vdim, polyOrder, cdim+dir, i-1),
      printf(fh, "  } ~%"),
      rcoFac : 1./(content(alphaOrd_r_n[1],alpha_r[0])[1]),
      printf(fh, "  if (~a > 0) { ~%", fullratsimp(alphaOrd_r_n[i]*rcoFac)),
      printf(fh, "    fUpwindQuad_r[~a] = ~a_~ax_p~a_surfx~a_eval_quad_node_~a_r(fc); ~%", i-1, basisFun, cdim+vdim, polyOrder, cdim+dir, i-1),
      printf(fh, "  } else { ~%"),
      printf(fh, "    fUpwindQuad_r[~a] = ~a_~ax_p~a_surfx~a_eval_quad_node_~a_l(fr); ~%", i-1, basisFun, cdim+vdim, polyOrder, cdim+dir, i-1),
      printf(fh, "  } ~%")
    ) else (
      printf(fh, "  if (~a > 0) { ~%", alphaOrd_l_n[i]),
      printf(fh, "    fUpwindQuad_l[~a] = ~a_~ax_p~a_surfx~a_eval_quad_node_~a_r(fl); ~%", i-1, basisFun, cdim+vdim, polyOrder, cdim+dir, i-1),
      printf(fh, "  } else { ~%"),
      printf(fh, "    fUpwindQuad_l[~a] = ~a_~ax_p~a_surfx~a_eval_quad_node_~a_l(fc); ~%", i-1, basisFun, cdim+vdim, polyOrder, cdim+dir, i-1),
      printf(fh, "  } ~%"),
      printf(fh, "  if (~a > 0) { ~%", alphaOrd_r_n[i]),
      printf(fh, "    fUpwindQuad_r[~a] = ~a_~ax_p~a_surfx~a_eval_quad_node_~a_r(fc); ~%", i-1, basisFun, cdim+vdim, polyOrder, cdim+dir, i-1),
      printf(fh, "  } else { ~%"),
      printf(fh, "    fUpwindQuad_r[~a] = ~a_~ax_p~a_surfx~a_eval_quad_node_~a_l(fr); ~%", i-1, basisFun, cdim+vdim, polyOrder, cdim+dir, i-1),
      printf(fh, "  } ~%")
    )
  ),

  printf(fh, "~%"),
  printf(fh, "  // Project tensor nodal quadrature basis back onto modal basis. ~%"),
  printf(fh, "  ~a_~ax_p~a_upwind_quad_to_modal(fUpwindQuad_l, fUpwind_l); ~%", basisFun, cdim+vdim, polyOrder),
  printf(fh, "  ~a_~ax_p~a_upwind_quad_to_modal(fUpwindQuad_r, fUpwind_r); ~%", basisFun, cdim+vdim, polyOrder),
  printf(fh, "~%"),

  /* Create expansions of f_hat (upwinded f) on the left and right in the surface basis. */
  fHatSurf_l_e : doExpand1(fUpwind_l, bSurf),
  fHatSurf_r_e : doExpand1(fUpwind_r, bSurf),

  /* Print C exprs for Ghat across surface boundary, (LEFT BOUNDARY) */
  Ghat_l_c : fullratsimp(innerProd(surfVars, alpha_l_e, bSurf, fHatSurf_l_e)),

  /* Clever factorization is possible for polyOrder = 1, but for higher
     polynomial orders, factorization is *too* clever and can lead to 
     deeply nested parentheses, not ideal for compiler optimizations */
  if (polyOrder = 1) then (
    writeCExprsNoExpand1(Ghat_l, Ghat_l_c)
  ) else (
    writeCExprs1(Ghat_l, Ghat_l_c)
  ),
  printf(fh, "~%"),
  flush_output(fh),

  /* Zero out components of Ghat which are empty. */
  GhatNoZero_l : makelistNoZeros1(Ghat_l_c, Ghat_l),
  Ghat_l_e     : doExpand(GhatNoZero_l, bSurf),

  /* Print C exprs for Ghat across surface boundary, (RIGHT BOUNDARY) */
  Ghat_r_c : fullratsimp(innerProd(surfVars, alpha_r_e, bSurf, fHatSurf_r_e)),

  /* Clever factorization is possible for polyOrder = 1, but for higher
     polynomial orders, factorization is *too* clever and can lead to 
     deeply nested parentheses, not ideal for compiler optimizations */
  if (polyOrder = 1) then (
    writeCExprsNoExpand1(Ghat_r, Ghat_r_c)
  ) else (
    writeCExprs1(Ghat_r, Ghat_r_c)
  ),
  printf(fh, "~%"),
  flush_output(fh),

  /* Zero out components of Ghat which are empty. */
  GhatNoZero_r : makelistNoZeros1(Ghat_r_c, Ghat_r),
  Ghat_r_e     : doExpand(GhatNoZero_r, bSurf),

  /* Compute surface contribution integral(phi^- Ghat) on the left and right. */
  incr_l : calcInnerProdList(surfVars, 1, subst(vv=-1.0, bP), Ghat_l_e),
  incr_r : calcInnerProdList(surfVars, -1, subst(vv=1.0, bP), Ghat_r_e),
  writeCIncrExprsCollect1lhsc(out, dv11[dir]*incr_l + dv11[dir]*incr_r),  
  printf(fh, "~%"),

  printf(fh, "  return 0.;~%"), /* Return CFL frequency in volume kernel. */
  printf(fh, "~%"),

  printf(fh, "} ~%")
);
