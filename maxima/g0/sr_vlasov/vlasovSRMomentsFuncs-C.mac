/* Functions (called by moment-calc.mac) that compute the
   moments of the distribution function in Vlasov model. */

load("modal-basis");
load("out-scripts");
load(stringproc)$
fpprec : 24$

volExpr(cdim, vdim) := prod(dxv[cdim+i-1], i, 1, vdim)$
volExprTot(totDim) := prod(dxv[i-1], i, 1, totDim)$
pVsub : [x=vx,y=vy,z=vz]$
vidx(cdim,vdim) := makelist(i,i,cdim,cdim+vdim-1)$
dv11 : [dv10, dv11, dv12]$

vTrans  : [vx*dv1/2.0+wx1, vy*dv2/2.0+wx2, vz*dv3/2.0+wx3]$
wTrans  : [wx1, wx2, wx3]$
dvTrans : [vx*dv1/2.0, vy*dv2/2.0, vz*dv3/2.0]$

/* Helper functions for expanding in basis functions a quantity we know should be sparse  */
doMakeExprLst(vals, S)  := makelist(if vals[i] # 0 then S[i-1] else 0, i, 1, length(vals))$
doExpandLst(lst, basis) := sum(lst[i]*basis[i], i, 1, length(basis))$

/* Number density */
/* M0 = < f > */
calcSRM0(fh, funcNm, cdim, vdim, basisFun, polyOrder) := block(
  [varsC, bC, varsP, bP, fl, M],
  /* Load the specific desired basis, including the loading of the hybrid basis if p=1 */
  [varsC,bC,varsP,bP] : loadPhaseBasis(basisFun, cdim, vdim, polyOrder),
  
  printf(fh, "GKYL_CU_DH void ~a_M0_~ax~av_~a_p~a(const double *w, const double *dxv, const int *idx, const double *f, double* GKYL_RESTRICT out) ~%{ ~%", funcNm, cdim, vdim, basisFun, polyOrder),
  printf(fh, "  const double volFact = ~a/~a; ~%", volExpr(cdim, vdim), 2^vdim),
  fl : doExpand1(f, bP),
  M : calcInnerProdList(varsP, 1, bC, fl),
  writeCIncrExprs1(out, volFact*M),
  
  printf(fh, "} ~%")
)$


/* Momentum */
calcSRM1i(fh, funcNm, cdim, vdim, basisFun, polyOrder) := block(
  [varsC, bC, varsP, bP, varsV, bV, NC, NP, NV, fl, M, clst],
  /* Load the specific desired basis, including the loading of the hybrid basis if p=1 */
  [varsC,bC,varsP,bP] : loadPhaseBasis(basisFun, cdim, vdim, polyOrder),
  /* Number of basis monomials. */
  NP : length(bP),
  NC : length(bC),
  pDim : length(varsP),

  /* Identify polyOrder in velocity space as p=2 for p=1 since we force p=1 to
     mean hybrid basis. */
  if polyOrder=1 then ( 
    [varsV, bV] : loadBasis(basisFun, vdim, 2)
  ) 
  else (
    [varsV, bV] : loadBasis(basisFun, vdim, polyOrder)
  ), 
  bV : subst(pVsub, copylist(bV)),  
  varsV : subst(pVsub, copylist(varsV)), 
  NV : length(bV),
  
  printf(fh, "GKYL_CU_DH void ~a_M1i_~ax~av_~a_p~a(const double *w, const double *dxv, const int *idx, const double *gamma, const double *f, double* GKYL_RESTRICT out) ~%{ ~%", funcNm, cdim, vdim, basisFun, polyOrder),

  printf(fh, "  const double volFact = ~a/~a; ~%", volExpr(cdim, vdim), 2^vdim),

  vid : vidx(cdim,vdim),
  for i : 1 thru vdim do (
    printf(fh, "  const double dv1~a = 2.0/dxv[~a]; ~%", i-1, vid[i])
  ),

  /* Compute p/gamma from input gamma, p/gamma = d/dp gamma */
  gamma_e : doExpand1(gamma, bV), 
  printf(fh, "  double p0_over_gamma[~a] = {0.0}; ~%", NV), 
  px_over_gamma_c : calcInnerProdList(varsV, 1, bV, dv11[1]*diff(gamma_e, vx)), 
  writeCExprs1(p0_over_gamma, px_over_gamma_c), 
  /* zero out components of px/gamma which are empty due to derivative */
  px_over_gamma_NoZero : doMakeExprLst(px_over_gamma_c, p0_over_gamma),
  px_over_gamma_NoZero_e : doExpandLst(px_over_gamma_NoZero, bV), 
  p_over_gamma_fld : [px_over_gamma_NoZero_e],
  printf(fh, "~%"),
  if (vdim > 1) then (
    printf(fh, "  double p1_over_gamma[~a] = {0.0}; ~%", NV), 
    py_over_gamma_c : calcInnerProdList(varsV, 1, bV, dv11[2]*diff(gamma_e, vy)), 
    writeCExprs1(p1_over_gamma, py_over_gamma_c), 
    /* zero out components of py/gamma which are empty due to derivative */
    py_over_gamma_NoZero : doMakeExprLst(py_over_gamma_c, p1_over_gamma),
    py_over_gamma_NoZero_e : doExpandLst(py_over_gamma_NoZero, bV), 
    p_over_gamma_fld : [px_over_gamma_NoZero_e, py_over_gamma_NoZero_e],
    printf(fh, "~%")
  ),
  if (vdim = 3) then (
    printf(fh, "  double p2_over_gamma[~a] = {0.0}; ~%", NV), 
    pz_over_gamma_c : calcInnerProdList(varsV, 1, bV, dv11[3]*diff(gamma_e, vz)), 
    writeCExprs1(p2_over_gamma, pz_over_gamma_c), 
    /* zero out components of px/gamma which are empty due to derivative */
    pz_over_gamma_NoZero : doMakeExprLst(pz_over_gamma_c, p2_over_gamma),
    pz_over_gamma_NoZero_e : doExpandLst(pz_over_gamma_NoZero, bV), 
    p_over_gamma_fld : [px_over_gamma_NoZero_e, py_over_gamma_NoZero_e, pz_over_gamma_NoZero_e],
    printf(fh, "~%")
  ),
  
  fl : doExpand1(f, bP),
  
  M : (M : [],
    for dir : 1 thru vdim do (
      M : append(M, calcInnerProdList(varsP, p_over_gamma_fld[dir], bC, fl))
    ),
  M),
  
  clst : [volFact],
  writeCIncrExprsCollect1(out, volFact*M, clst),
  
  printf(fh, "} ~%")
)$

/* M2 = gamma*mc^2 energy moment */
calcSRM2(fh, funcNm, cdim, vdim, basisFun, polyOrder) := block(
  [varsC, bC, varsP, bP, varsV, bV, NC, NP, NV, fl, M2, clst],
  /* Load the specific desired basis, including the loading of the hybrid basis if p=1 */
  [varsC,bC,varsP,bP] : loadPhaseBasis(basisFun, cdim, vdim, polyOrder),
  /* Number of basis monomials. */
  NP : length(bP),
  NC : length(bC),
  pDim : length(varsP),

  /* Identify polyOrder in velocity space as p=2 for p=1 since we force p=1 to
     mean hybrid basis. */
  if polyOrder=1 then ( 
    [varsV, bV] : loadBasis(basisFun, vdim, 2)
  ) 
  else (
    [varsV, bV] : loadBasis(basisFun, vdim, polyOrder)
  ), 
  bV : subst(pVsub, copylist(bV)),  
  varsV : subst(pVsub, copylist(varsV)), 
  NV : length(bV),
  
  printf(fh, "GKYL_CU_DH void ~a_M2_~ax~av_~a_p~a(const double *w, const double *dxv, const int *idx, const double *gamma, const double *f, double* GKYL_RESTRICT out) ~%{ ~%", funcNm, cdim, vdim, basisFun, polyOrder),
  printf(fh, "  const double volFact = ~a/~a; ~%", volExpr(cdim, vdim), 2^vdim),

  fl : doExpand1(f, bP),
  
  M2 : calcInnerProdList(varsP, doExpand1(gamma, bV), bC, fl),
  
  clst : [volFact],
  writeCIncrExprsCollect1(out, volFact*M2, clst),
  
  printf(fh, "} ~%")
)$

/* M3i energy-flux moment (just p in relativity) */
calcSRM3i(fh, funcNm, cdim, vdim, basisFun, polyOrder) := block(
  [varsC, bC, varsP, bP, fl, M3, M3i],
  /* Load the specific desired basis, including the loading of the hybrid basis if p=1 */
  [varsC,bC,varsP,bP] : loadPhaseBasis(basisFun, cdim, vdim, polyOrder),
  
  printf(fh, "GKYL_CU_DH void ~a_M3i_~ax~av_~a_p~a(const double *w, const double *dxv, const int *idx, const double *f, double* GKYL_RESTRICT out) ~%{ ~%", funcNm, cdim, vdim, basisFun, polyOrder),
  printf(fh, "  const double volFact = ~a/~a; ~%", volExpr(cdim, vdim), 2^vdim),
  vid : vidx(cdim,vdim),
  for i : 1 thru vdim do (
    printf(fh, "  const double wx~a = w[~a], dv~a = dxv[~a]; ~%", i, cdim+i-1, i, cdim+i-1)
  ),

  fl : doExpand1(f, bP),

  M3 : (M3 : [],
    for dir : 1 thru vdim do (
      M3 : append(M3, calcInnerProdList(varsP, vTrans[dir], bC, fl))
    ),
  M3),

  clst : [volFact],
  M3i : [],
  M3i : append(M3i, volFact*M3),
  writeCIncrExprsCollect1(out, M3i, clst),
  
  printf(fh, "} ~%")
)$

/* 4-Momentum (GammaV*n, GammaV*n*V) */
calcSRNi(fh, funcNm, cdim, vdim, basisFun, polyOrder) := block(
  [varsC, bC, varsP, bP, varsV, bV, NC, NP, NV, fl, M0, M1i, clst, Ni],
  /* Load the specific desired basis, including the loading of the hybrid basis if p=1 */
  [varsC,bC,varsP,bP] : loadPhaseBasis(basisFun, cdim, vdim, polyOrder),
  /* Number of basis monomials. */
  NP : length(bP),
  NC : length(bC),
  pDim : length(varsP),

  /* Identify polyOrder in velocity space as p=2 for p=1 since we force p=1 to
     mean hybrid basis. */
  if polyOrder=1 then ( 
    [varsV, bV] : loadBasis(basisFun, vdim, 2)
  ) 
  else (
    [varsV, bV] : loadBasis(basisFun, vdim, polyOrder)
  ), 
  bV : subst(pVsub, copylist(bV)),  
  varsV : subst(pVsub, copylist(varsV)), 
  NV : length(bV),
  
  printf(fh, "GKYL_CU_DH void ~a_Ni_~ax~av_~a_p~a(const double *w, const double *dxv, const int *idx, const double *gamma, const double *f, double* GKYL_RESTRICT out) ~%{ ~%", funcNm, cdim, vdim, basisFun, polyOrder),

  printf(fh, "  const double volFact = ~a/~a; ~%", volExpr(cdim, vdim), 2^vdim),

  vid : vidx(cdim,vdim),
  for i : 1 thru vdim do (
    printf(fh, "  const double dv1~a = 2.0/dxv[~a]; ~%", i-1, vid[i])
  ),

  /* Compute p/gamma from input gamma, p/gamma = d/dp gamma */
  gamma_e : doExpand1(gamma, bV), 
  printf(fh, "  double p0_over_gamma[~a] = {0.0}; ~%", NV), 
  px_over_gamma_c : calcInnerProdList(varsV, 1, bV, dv11[1]*diff(gamma_e, vx)), 
  writeCExprs1(p0_over_gamma, px_over_gamma_c), 
  /* zero out components of px/gamma which are empty due to derivative */
  px_over_gamma_NoZero : doMakeExprLst(px_over_gamma_c, p0_over_gamma),
  px_over_gamma_NoZero_e : doExpandLst(px_over_gamma_NoZero, bV), 
  p_over_gamma_fld : [px_over_gamma_NoZero_e],
  printf(fh, "~%"),
  if (vdim > 1) then (
    printf(fh, "  double p1_over_gamma[~a] = {0.0}; ~%", NV), 
    py_over_gamma_c : calcInnerProdList(varsV, 1, bV, dv11[2]*diff(gamma_e, vy)), 
    writeCExprs1(p1_over_gamma, py_over_gamma_c), 
    /* zero out components of py/gamma which are empty due to derivative */
    py_over_gamma_NoZero : doMakeExprLst(py_over_gamma_c, p1_over_gamma),
    py_over_gamma_NoZero_e : doExpandLst(py_over_gamma_NoZero, bV), 
    p_over_gamma_fld : [px_over_gamma_NoZero_e, py_over_gamma_NoZero_e],
    printf(fh, "~%")
  ),
  if (vdim = 3) then (
    printf(fh, "  double p2_over_gamma[~a] = {0.0}; ~%", NV), 
    pz_over_gamma_c : calcInnerProdList(varsV, 1, bV, dv11[3]*diff(gamma_e, vz)), 
    writeCExprs1(p2_over_gamma, pz_over_gamma_c), 
    /* zero out components of px/gamma which are empty due to derivative */
    pz_over_gamma_NoZero : doMakeExprLst(pz_over_gamma_c, p2_over_gamma),
    pz_over_gamma_NoZero_e : doExpandLst(pz_over_gamma_NoZero, bV), 
    p_over_gamma_fld : [px_over_gamma_NoZero_e, py_over_gamma_NoZero_e, pz_over_gamma_NoZero_e],
    printf(fh, "~%")
  ),
  
  fl : doExpand1(f, bP),

  M0 : calcInnerProdList(varsP, 1, bC, fl),
  
  M1 : (M1 : [],
    for dir : 1 thru vdim do (
      M1 : append(M1, calcInnerProdList(varsP, p_over_gamma_fld[dir], bC, fl))
    ),
  M1),
  
  clst : [volFact],
  Ni : [],
  Ni : append(Ni, volFact*M0),
  Ni : append(Ni, volFact*M1),
  writeCIncrExprsCollect1(out, Ni, clst),
  
  printf(fh, "} ~%")
)$

/* Stress-energy tensor (Energy, Energy flux (vdim components), Stress tensor (vdim*(vdim+1))/2 components)) */
calcSRTij(fh, funcNm, cdim, vdim, basisFun, polyOrder) := block(
  [varsC, bC, varsP, bP, varsV, bV, NC, NP, NV, fl, Energy, M3, M2, clst, Tij],
  /* Load the specific desired basis, including the loading of the hybrid basis if p=1 */
  [varsC,bC,varsP,bP] : loadPhaseBasis(basisFun, cdim, vdim, polyOrder),
  /* Number of basis monomials. */
  NP : length(bP),
  NC : length(bC),
  pDim : length(varsP),

  /* Identify polyOrder in velocity space as p=2 for p=1 since we force p=1 to
     mean hybrid basis. */
  if polyOrder=1 then ( 
    [varsV, bV] : loadBasis(basisFun, vdim, 2)
  ) 
  else (
    [varsV, bV] : loadBasis(basisFun, vdim, polyOrder)
  ), 
  bV : subst(pVsub, copylist(bV)),  
  varsV : subst(pVsub, copylist(varsV)), 
  NV : length(bV),
  
  printf(fh, "GKYL_CU_DH void ~a_Tij_~ax~av_~a_p~a(const double *w, const double *dxv, const int *idx, const double *gamma, const double *f, double* GKYL_RESTRICT out) ~%{ ~%", funcNm, cdim, vdim, basisFun, polyOrder),

  printf(fh, "  const double volFact = ~a/~a; ~%", volExpr(cdim, vdim), 2^vdim),

  vid : vidx(cdim,vdim),
  for i : 1 thru vdim do (
    printf(fh, "  const double wx~a = w[~a], dv~a = dxv[~a]; ~%", i, cdim+i-1, i, cdim+i-1),
    printf(fh, "  const double dv1~a = 2.0/dxv[~a]; ~%", i-1, vid[i])
  ),

  /* Compute p/gamma from input gamma, p/gamma = d/dp gamma */
  gamma_e : doExpand1(gamma, bV), 
  printf(fh, "  double p0_over_gamma[~a] = {0.0}; ~%", NV), 
  px_over_gamma_c : calcInnerProdList(varsV, 1, bV, dv11[1]*diff(gamma_e, vx)), 
  writeCExprs1(p0_over_gamma, px_over_gamma_c), 
  /* zero out components of px/gamma which are empty due to derivative */
  px_over_gamma_NoZero : doMakeExprLst(px_over_gamma_c, p0_over_gamma),
  px_over_gamma_NoZero_e : doExpandLst(px_over_gamma_NoZero, bV), 
  p_over_gamma_fld : [px_over_gamma_NoZero_e],
  printf(fh, "~%"),
  if (vdim > 1) then (
    printf(fh, "  double p1_over_gamma[~a] = {0.0}; ~%", NV), 
    py_over_gamma_c : calcInnerProdList(varsV, 1, bV, dv11[2]*diff(gamma_e, vy)), 
    writeCExprs1(p1_over_gamma, py_over_gamma_c), 
    /* zero out components of py/gamma which are empty due to derivative */
    py_over_gamma_NoZero : doMakeExprLst(py_over_gamma_c, p1_over_gamma),
    py_over_gamma_NoZero_e : doExpandLst(py_over_gamma_NoZero, bV), 
    p_over_gamma_fld : [px_over_gamma_NoZero_e, py_over_gamma_NoZero_e],
    printf(fh, "~%")
  ),
  if (vdim = 3) then (
    printf(fh, "  double p2_over_gamma[~a] = {0.0}; ~%", NV), 
    pz_over_gamma_c : calcInnerProdList(varsV, 1, bV, dv11[3]*diff(gamma_e, vz)), 
    writeCExprs1(p2_over_gamma, pz_over_gamma_c), 
    /* zero out components of px/gamma which are empty due to derivative */
    pz_over_gamma_NoZero : doMakeExprLst(pz_over_gamma_c, p2_over_gamma),
    pz_over_gamma_NoZero_e : doExpandLst(pz_over_gamma_NoZero, bV), 
    p_over_gamma_fld : [px_over_gamma_NoZero_e, py_over_gamma_NoZero_e, pz_over_gamma_NoZero_e],
    printf(fh, "~%")
  ),

  fl : doExpand1(f, bP),

  Energy : calcInnerProdList(varsP, doExpand1(gamma, bV), bC, fl),
  
  M3 : (M3 : [],
    for dir : 1 thru vdim do (
      M3 : append(M3, calcInnerProdList(varsP, vTrans[dir], bC, fl))
    ),
  M3),

  M2 : (M2 : [],
    for dir : 1 thru vdim do (
      for dir2 : dir thru vdim do (
        M2 : append(M2, calcInnerProdList(varsP, p_over_gamma_fld[dir]*vTrans[dir2], bC, fl))
      )
    ),
  M2),
  
  clst : [volFact],
  Tij : [],
  Tij : append(Tij, volFact*Energy),
  Tij : append(Tij, volFact*M3),
  Tij : append(Tij, volFact*M2),
  writeCIncrExprsCollect1(out, Tij, clst),
  
  printf(fh, "} ~%")
)$

calcSRIntMDist(fh, funcNm, cdim, vdim, basisFun, polyOrder) := block(
  [varsC, bC, varsP, bP, varsV, bV, NC, NP, NV, fl, M0, Energy, M3, clst, int],
  /* Load the specific desired basis, including the loading of the hybrid basis if p=1 */
  [varsC,bC,varsP,bP] : loadPhaseBasis(basisFun, cdim, vdim, polyOrder),
  /* Number of basis monomials. */
  NP : length(bP),
  NC : length(bC),
  pDim : length(varsP),

  /* Identify polyOrder in velocity space as p=2 for p=1 since we force p=1 to
     mean hybrid basis. */
  if polyOrder=1 then ( 
    [varsV, bV] : loadBasis(basisFun, vdim, 2)
  ) 
  else (
    [varsV, bV] : loadBasis(basisFun, vdim, polyOrder)
  ), 
  bV : subst(pVsub, copylist(bV)),  
  varsV : subst(pVsub, copylist(varsV)), 
  NV : length(bV),
    
  printf(fh, "GKYL_CU_DH void ~a_int_mom_~ax~av_~a_p~a(const double *w, const double *dxv, const int *idx, const double *gamma, const double *f, double* GKYL_RESTRICT out) ~%{ ~%", funcNm, cdim, vdim, basisFun, polyOrder),
  printf(fh, "  const double volFact = ~a*~a; ~%", volExprTot(cdim+vdim), float(1/(2^(cdim+vdim))) ),
  for i : 1 thru vdim do (
    printf(fh, "  const double wx~a = w[~a], dv~a = dxv[~a]; ~%", i, cdim+i-1, i, cdim+i-1)
  ),

  fl : doExpand1(f, bP),

  M : [],

  M0 : fullratsimp(innerProd(varsP, 1, 1, fl)),
  M  : endcons(M0,M),

  Energy : fullratsimp(innerProd(varsP, 1, doExpand1(gamma, bV), fl)),
  M  : endcons(Energy, M),

  /* Energy flux */
  for dir : 1 thru vdim do (
    M3 : fullratsimp(innerProd(varsP, 1, vTrans[dir], fl)),
    M  : endcons(M3,M)
  ),

  M : map(letsimp, M),
  clst : [volFact],
  writeCIncrExprsCollect1(out, volFact*M, clst),

  printf(fh, "} ~%")
)$

calcSRMoments(fh, funcNm, cdim, vdim, basisFun, polyOrder) := block([],
  printf(fh, "#include <gkyl_mom_vlasov_sr_kernels.h> ~%"),
  calcSRM0(fh, funcNm, cdim, vdim, basisFun, polyOrder),
  calcSRM1i(fh, funcNm, cdim, vdim, basisFun, polyOrder),
  calcSRM2(fh, funcNm, cdim, vdim, basisFun, polyOrder),
  calcSRM3i(fh, funcNm, cdim, vdim, basisFun, polyOrder),
  calcSRNi(fh, funcNm, cdim, vdim, basisFun, polyOrder),
  calcSRTij(fh, funcNm, cdim, vdim, basisFun, polyOrder),
  calcSRIntMDist(fh, funcNm, cdim, vdim, basisFun, polyOrder)
)$
