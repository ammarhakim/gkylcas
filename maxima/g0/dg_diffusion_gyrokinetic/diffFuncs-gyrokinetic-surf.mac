load("modal-basis")$
load("recovery")$
load("out-scripts")$
fpprec : 24$

genDGdiffGyrokineticKernelSurf(fh, funcNm, cdim, vdim, basisType, polyOrder, dir, diffOrder) := block(
  [constCoeff,dim,vars,basis,varsC,basisC,vSub,numBasis,ql_c,qc_c,qr_c,coeffType,coeff_e,coeff_c,qRec,kernel,i],

  printf(fh, "#include <gkyl_dg_diffusion_gyrokinetic_kernels.h>~%~%"),

  diffCoeffType : [true, false],
  /* Let's not generate inhomogeneous hyperdiff for now. */
  if (diffOrder>2) then (diffCoeffType : [true]),

  for constCoeff in diffCoeffType do (
      
    dim : cdim+vdim,
  
    bType : basisType,
    [varsC, basisC, vars, basis, vSub] : loadGkBasis(basisType, cdim, vdim, polyOrder),
    if polyOrder=1 then bType : "gkhyb",  /* Force p=1 to mean hybrid. */
    numBasis : length(basis),
  
    printf(fh, "GKYL_CU_DH double ~a_~acoeff(const double *w, const double *dx, const double *coeff, cont double *jacobgeo_inv, const double *ql, const double *qc, const double *qr, double* GKYL_RESTRICT out) ~%", funcNm, coeffType),
    printf(fh, "{~%"),
    printf(fh, "  // w[NDIM]: Cell-center coordinate.~%"),
    printf(fh, "  // dxv[NDIM]: Cell length.~%"),
    printf(fh, "  // coeff: Diffusion coefficient.~%"),
    printf(fh, "  // jacobgeo_inv: one divided by the configuration space Jacobian.~%"),
    printf(fh, "  // ql: Input field in the left cell.~%"),
    printf(fh, "  // qc: Input field in the center cell.~%"),
    printf(fh, "  // qr: Input field in the right cell.~%"),
    printf(fh, "  // out: Incremented output.~%~%"),

    printf(fh, "  const double rdx2Sq = pow(2./dx[~a],~a.);~%", dir-1, diffOrder),
    printf(fh, "~%"),

    ql_c : makelist(ql[i-1], i, 1, numBasis),
    qc_c : makelist(qc[i-1], i, 1, numBasis),
    qr_c : makelist(qr[i-1], i, 1, numBasis),
    ql_e : doExpand(ql_c, basis),
    qc_e : doExpand(qc_c, basis),
    qr_e : doExpand(qr_c, basis),

    jacobgeo_inv_e : doExpand1(jacobgeo_inv, basisC),
  
    /* Divide jacobGeo*f by jacobGeo. */
    fl_c : calcInnerProdList(vars, jacobgeo_inv_e, basis, ql_e),
    fc_c : calcInnerProdList(vars, jacobgeo_inv_e, basis, qc_e),
    fr_c : calcInnerProdList(vars, jacobgeo_inv_e, basis, qr_e),

    /* Divide jacobGeo*f by jacobGeo. */
    printf(fh, "  double fl[~a];~%", numBasis),
    writeCExprsNoExpand1(fl, fl_c),
    fl_c : makelist(fl[i-1], i, 1, numBasis),
    printf(fh, "~%"),
    printf(fh, "  double fc[~a];~%", numBasis),
    writeCExprsNoExpand1(fc, fc_c),
    fc_c : makelist(fc[i-1], i, 1, numBasis),
    printf(fh, "~%"),
    printf(fh, "  double fr[~a];~%", numBasis),
    writeCExprsNoExpand1(fr, fr_c),
    fr_c : makelist(fr[i-1], i, 1, numBasis),
    printf(fh, "~%"),

    if constCoeff then (
      coeffType : "const",
      coeff_e : coeff[dir-1]
    ) else (
      coeffType : "var",
      /* Here we assume diffusion coefficient only depends on position space,
         but it coule be easily changed to vary with velocity space too. */
      if vdim=0 then basisC : basis,
      numBasisC : length(basisC),
      coeff_c : makelist(coeff[(dir-1)*numBasisC+i-1],i,1,numBasisC),
      coeff_e : doExpand(coeff_c, basisC)
    ),
  
    qRec : calcRecov1CellGen(bType, vars[dir], vars, polyOrder, diffOrder-1, dg(fl_c), dg(fc_c), dg(fr_c)),
  
    /* For higher order diffusion and spatially varying diffusion coefficient, it
       is not clear where the coefficient should appear, e.g. d_xxx( D d_x(f) )
       or d_xx( D d_xx(f)); different models appear in the literature. Since we don't
       a good physical justfication for either we just arbitrarily pick one. */
    kernel : calcInnerProdList(vars, 1, basis,
      ((-1)^(diffOrder/2+1))*diff(coeff_e*diff(qRec, vars[dir], diffOrder/2), vars[dir], diffOrder/2)),
    
    writeCIncrExprsNoExpand(fullratsimp(rdx2Sq*kernel)),
    printf(fh, "~%"),
  
    printf(fh, "  return 0.;~%~%"),
    printf(fh, "}~%~%")
  )
)$
