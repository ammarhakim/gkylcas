/* This script generates the kernels for the source terms in the
   fluid equations of different fluid models: isothermal Euler, Euler/5 moment, and 10 moment
   For all three, momentum source is: q/m*(rho*E + rho*u x B)
   For Euler/5 moment also have energy source: q/m*(rhou*E)
   For 10 moment also have pressure source: P x B + (P x B)_sym */

load("modal-basis")$
load("out-scripts")$
fpprec : 24$

calcEulerSource(fh, funcNm, cdim, basisFun, polyOrder) := block(
  [varsC, bC, NC, rho_e, Ex_e, Ey_e, Ez_e, Bx_e, By_e, Bz_e, 
   rhoux_e, rhouy_e, rhouz_e, 
   incr_rhoux, incr_rhouy, incr_rhouz, incr_energy],

  kill(varsC, bC),

  /* Load basis of dimensionality requested. */
  [varsC,bC] : loadBasis(basisFun, cdim, polyOrder),

  /* Number of basis monomials. */
  NC : length(bC),

  printf(fh, "#include <gkyl_euler_kernels.h> ~%"),
  printf(fh, "GKYL_CU_DH void ~a(const double* qmem, const double* fluid, const double* p_ij, double* GKYL_RESTRICT out) ~%{ ~%", funcNm),
  printf(fh, "  // qmem:  q/m*EM fields.~%"),
  printf(fh, "  // fluid: [rho, rho ux, rho uy, rho uz...], Fluid input state vector.~%"),
  printf(fh, "  // p_ij:  Input pressure tensor (only used by 10 moment).~%"),
  printf(fh, "  // out:   Output increment"),
  printf(fh, "~%"),

  printf(fh, "~%"),
  printf(fh, "  const double *Ex = &qmem[~a]; ~%", 0*NC),
  printf(fh, "  const double *Ey = &qmem[~a]; ~%", 1*NC),
  printf(fh, "  const double *Ez = &qmem[~a]; ~%", 2*NC),
  printf(fh, "  const double *Bx = &qmem[~a]; ~%", 3*NC),
  printf(fh, "  const double *By = &qmem[~a]; ~%", 4*NC),
  printf(fh, "  const double *Bz = &qmem[~a]; ~%", 5*NC),
  printf(fh, "~%"),
  printf(fh, "  const double *rho = &fluid[~a]; ~%", 0*NC),
  printf(fh, "  const double *rhoux = &fluid[~a]; ~%", 1*NC),
  printf(fh, "  const double *rhouy = &fluid[~a]; ~%", 2*NC),
  printf(fh, "  const double *rhouz = &fluid[~a]; ~%", 3*NC), 
  printf(fh, "~%"),

  /* No source term for density */
  printf(fh, "  double *out_rhoux = &out[~a]; ~%", 1*NC),
  printf(fh, "  double *out_rhouy = &out[~a]; ~%", 2*NC),
  printf(fh, "  double *out_rhouz = &out[~a]; ~%", 3*NC),
  printf(fh, "  double *out_energy = &out[~a]; ~%", 4*NC),
  printf(fh, "~%"),

  Ex_e : doExpand1(Ex, bC),
  Ey_e : doExpand1(Ey, bC),
  Ez_e : doExpand1(Ez, bC),  
  Bx_e : doExpand1(Bx, bC),
  By_e : doExpand1(By, bC),
  Bz_e : doExpand1(Bz, bC),  

  rho_e : doExpand1(rho, bC),
  rhoux_e : doExpand1(rhoux, bC),
  rhouy_e : doExpand1(rhouy, bC),
  rhouz_e : doExpand1(rhouz, bC),

  incr_rhoux : calcInnerProdList(varsC, 1, bC, rho_e*Ex_e + rhouy_e*Bz_e - rhouz_e*By_e),
  writeCIncrExprs1(out_rhoux, incr_rhoux),
  printf(fh, "~%"),
  flush_output(fh),

  incr_rhouy : calcInnerProdList(varsC, 1, bC, rho_e*Ey_e + rhouz_e*Bx_e - rhoux_e*Bz_e),
  writeCIncrExprs1(out_rhouy, incr_rhouy),
  printf(fh, "~%"),
  flush_output(fh),

  incr_rhouz : calcInnerProdList(varsC, 1, bC, rho_e*Ez_e + rhoux_e*By_e - rhouy_e*Bx_e),
  writeCIncrExprs1(out_rhouz, incr_rhouz),
  printf(fh, "~%"),
  flush_output(fh),

  incr_energy : calcInnerProdList(varsC, 1, bC, rhoux_e*Ex_e + rhouy_e*Ey_e + rhouz_e*Ez_e),
  writeCIncrExprs1(out_energy, incr_energy),
  printf(fh, "~%"),
  flush_output(fh),

  printf(fh, "} ~%")
)$
