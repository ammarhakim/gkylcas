/* Generate kernels for the updater that computes the cross primitive moments 
   upar_ei and vtsq_ei (or upar_ie and vtsq_ie) used in the cross collision 
   terms, given the moments of electrons and ions. 
   Weak division and multiplication are employed. */

load("modal-basis");
load("out-scripts");
load("nodal_operations/nodal_functions");
load(stringproc)$
fpprec : 24$

calcGKCrossPrimMomsBGK(fh, funcNm, cdim, vgk, basisFun, polyOrder) := block(
  [varsC, bC, NC],

  kill(varsC, bC),

  /* Load basis of dimensionality requested. */
  [varsC,bC] : loadBasis(basisFun, cdim, polyOrder),

  /* Number of basis monomials. */
  NC  : length(bC),

  print("gk ", cdim,"x", vgk, "v", polyOrder, "p"),
  
  /* Include the header files. */
  printf(fh, "#include <gkyl_gyrokinetic_cross_prim_moms_bgk_kernels.h> ~%"),
  printf(fh, "#include <gkyl_binop_mul_ser.h> ~%"),
  printf(fh, "#include <gkyl_basis_~a_~ax_p~a_inv.h> ~%", basisFun, cdim, polyOrder),
  printf(fh, " ~%"),
  
  /* Function declaration with input/output variables. */
  printf(fh, "GKYL_CU_DH void ~a(const double betaGreenep1, const double m_self, const double *prim_moms_self, const double m_other, const double *prim_moms_other, const double *nu_sr, const double *nu_rs, double *prim_moms_cross) ~%{ ~%", funcNm),
  printf(fh, "  // m_:              mass. ~%"),
  printf(fh, "  // prim_moms_:      primitive moments of the distribution function. ~%"),
  printf(fh, "  // prim_moms_cross: cross primitive moments. ~%"),
  printf(fh, " ~%"),

  printf(fh, "  const double m_s = m_self; ~%"),
  printf(fh, "  const double m_r = m_other; ~%"),
  /* Create pointers to the moments of each species. */
  printf(fh, "  const double *n_s = &prim_moms_self[~a]; ~%", 0),
  printf(fh, "  const double *upar_s = &prim_moms_self[~a]; ~%", NC),
  printf(fh, "  const double *vtsq_s = &prim_moms_self[~a]; ~%", NC*2),
  printf(fh, "  const double *n_r = &prim_moms_other[~a]; ~%", 0),
  printf(fh, "  const double *upar_r = &prim_moms_other[~a]; ~%", NC),
  printf(fh, "  const double *vtsq_r = &prim_moms_other[~a]; ~%", NC*2),
  printf(fh, " ~%"),
  /* Create pointers to the cross moments. */
  printf(fh, "  double *n_sr = &prim_moms_cross[~a]; ~%", 0),
  printf(fh, "  double *upar_sr = &prim_moms_cross[~a]; ~%", NC*1),
  printf(fh, "  double *vtsq_sr = &prim_moms_cross[~a]; ~%", NC*2),
  printf(fh, " ~%"),
  /* Create the arrays of the temps. */
  printf(fh, "  double msNsNusr[~a] = {0.0}; ~%", NC),
  printf(fh, "  double mrNrNurs[~a] = {0.0}; ~%", NC),
  printf(fh, "  double m_n_nu[~a] = {0.0}; ~%", NC),
  printf(fh, "  double m_n_nu_inv[~a] = {0.0}; ~%", NC),
  printf(fh, "  double alphaE[~a] = {0.0}; ~%", NC),     
  printf(fh, "~%"),
  printf(fh, "  double msNsNusr_inv[~a] = {0.0}; ~%", NC),
  printf(fh, "  double coeff[~a] = {0.0}; ~%", NC), 
  printf(fh, "  double dUpar[~a] = {0.0}; ~%", NC), 
  printf(fh, "  double cUpar[~a] = {0.0}; ~%", NC),
  printf(fh, "~%"),
  printf(fh, "  double dv; ~%"),
  printf(fh, "  double T1[~a] = {0.0}; ~%", NC), 
  printf(fh, "  double T2[~a] = {0.0}; ~%", NC), 
  printf(fh, "  double T3[~a] = {0.0}; ~%", NC), 
  printf(fh, "  double cVtsq[~a] = {0.0}; ~%", NC),
  printf(fh, "  bool negative_cross_temp = false; ~%"),
  printf(fh, "~%"),

  /* Caculate alphaE. */
  printf(fh, "  binop_mul_~ad_~a_p~a(n_s, nu_sr, msNsNusr); ~%", cdim, basisFun, polyOrder),
  printf(fh, "  binop_mul_~ad_~a_p~a(n_r, nu_rs, mrNrNurs); ~%", cdim, basisFun, polyOrder),
  for i : 1 thru NC do (
    printf(fh, "  msNsNusr[~a] = m_s * msNsNusr[~a]; ~%", i-1, i-1),
    printf(fh, "  mrNrNurs[~a] = m_r * mrNrNurs[~a]; ~%", i-1, i-1),
    printf(fh, "  m_n_nu[~a] = msNsNusr[~a] + mrNrNurs[~a]; ~%", i-1, i-1, i-1)
  ),
  printf(fh, "  ~a_~ax_p~a_inv(m_n_nu, m_n_nu_inv); ~%", basisFun, cdim, polyOrder),
  printf(fh, "  binop_mul_~ad_~a_p~a(msNsNusr, mrNrNurs, alphaE); ~%", cdim, basisFun, polyOrder),
  printf(fh, "  binop_mul_~ad_~a_p~a(alphaE, m_n_nu_inv, alphaE); ~%", cdim, basisFun, polyOrder),
  for i : 1 thru NC do (
    printf(fh, "  alphaE[~a] = alphaE[~a] * 2.0 * betaGreenep1 / (m_s+m_r); ~%", i-1, i-1)
  ),
  printf(fh, "~%"),

  /* Calculate cross density n_sr. */
  for i : 1 thru NC do (
    printf(fh, "  n_sr[~a] = n_s[~a]; ~%", i-1, i-1)
  ),
  printf(fh, " ~%"),

  /* Calculate cross parallel velocity upar_sr. */
  printf(fh, "  ~a_~ax_p~a_inv(msNsNusr, msNsNusr_inv); ~%", basisFun, cdim, polyOrder),
  printf(fh, "  binop_mul_~ad_~a_p~a(alphaE, msNsNusr_inv, coeff); ~%", cdim, basisFun, polyOrder),
  for i : 1 thru NC do (
    printf(fh, "  dUpar[~a] = upar_r[~a] - upar_s[~a]; ~%", i-1, i-1, i-1)
  ),
  printf(fh, "  binop_mul_~ad_~a_p~a(coeff, dUpar, cUpar); ~%", cdim, basisFun, polyOrder),
  for i : 1 thru NC do (
    printf(fh, "  upar_sr[~a] = upar_s[~a] + cUpar[~a]*(m_s+m_r)/2.0; ~%", i-1, i-1, i-1)
  ),
  printf(fh, " ~%"),
  
  /* Calculate cross thermal velocity square vtsq_sr. */
  if vgk=1 then (
    printf(fh, "  dv = 1.0; ~%")
  ) else (
    printf(fh, "  dv = 3.0; ~%")
  ),
  for i : 1 thru NC do (
    printf(fh, "  T1[~a] = dv * (m_r*vtsq_r[~a]-m_s*vtsq_s[~a]); ~%", i-1, i-1, i-1)
  ),
  printf(fh, "  binop_mul_~ad_~a_p~a(dUpar, dUpar, T2); ~%", cdim, basisFun, polyOrder),
  printf(fh, "  binop_mul_~ad_~a_p~a(coeff, T2, T3); ~%", cdim, basisFun, polyOrder),
  for i : 1 thru NC do (
    printf(fh, "  cVtsq[~a] = T1[~a] + m_r*T2[~a] - (m_s+m_r)*(m_s+m_r)/4.0*T3[~a] ; ~%", i-1, i-1, i-1, i-1)
  ),
  printf(fh, "  binop_mul_~ad_~a_p~a(coeff, cVtsq, cVtsq); ~%", cdim, basisFun, polyOrder),
  for i : 1 thru NC do (
    printf(fh, "  vtsq_sr[~a] = vtsq_s[~a] + cVtsq[~a]/dv; ~%", i-1, i-1, i-1)
  ),
  printf(fh, " ~%"),

  /* Turn collisions off when vtsq_sr is not positive-definite. */
  printf(fh, "  // If vtsq_sr is negative at a corner, turn off collisions.~%"),
  nodes : getNodes(basisFun, cdim, 1),
  vtsq_sr_e : doExpand1(vtsq_sr,bC),
  vtsq_sr_corners : gcfac(float(fullratsimp( evAtNodes(vtsq_sr_e,nodes,varsC) ))),
  for i : 1 thru length(nodes) do (
    printf(fh, "  if (~a < 0.0) negative_cross_temp = true; ~%", vtsq_sr_corners[i])
  ),
  printf(fh, "  if (negative_cross_temp) { ~%"),
  for j : 1 thru NC do (
    printf(fh, "    upar_sr[~a] = upar_s[~a]; ~%", j-1, j-1),
    printf(fh, "    vtsq_sr[~a] = vtsq_s[~a]; ~%", j-1, j-1)
  ),
  printf(fh, "  } ~%"),

  printf(fh, "} ~%")
)$
