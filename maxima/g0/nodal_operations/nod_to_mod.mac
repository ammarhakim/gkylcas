load("modal-basis");
load("out-scripts");
load(stringproc)$
load("scifac")$
load("g0/nodal_operations/nodal_functions")$
fpprec : 24$

/* ...... USER INPUTS........ */

/* Serendipity basis. */
minPolyOrder_Ser : 1$
maxPolyOrder_Ser : 2$
minDim_Ser : 1$
maxDim_Ser : 6$

/* Tensor product basis. */
/* Note that Serendipity, p = 1, is equivalent to Tensor */
/* Thus no need to calculate p = 1 Tensor basis */
minPolyOrder_Tensor : 2$
maxPolyOrder_Tensor : 2$
minDim_Tensor : 2$
maxDim_Tensor : 4$

/* ...... END OF USER INPUTS........ */

bName        : ["ser","tensor"]$
minPolyOrder : [minPolyOrder_Ser, minPolyOrder_Tensor]$
maxPolyOrder : [maxPolyOrder_Ser, maxPolyOrder_Tensor]$
minDim      : [minDim_Ser, minDim_Tensor]$
maxDim      : [maxDim_Ser, maxDim_Tensor]$

/* Compute the surface contribution in velocity space from the acceleration term */
for bInd : 1 thru length(bName) do (
  fname : sconcat("~/max-out/basis_nodal_to_modal_",bName[bInd], ".c"),
  fh : openw(fname),
  printf(fh, "#include <gkyl_basis_~a_kernels.h>~%", bName[bInd]),

  for d : minDim[bInd] thru maxDim[bInd] do (
    minPolyOrderB : minPolyOrder[bInd],
    maxPolyOrderB : maxPolyOrder[bInd],
    if (d>5 and maxPolyOrderB > 1) then maxPolyOrderB : 1,
    for polyOrder : minPolyOrderB thru maxPolyOrderB do (
      kill(vars, basis),
      printf(fh, "GKYL_CU_DH~%"),
      printf(fh, "void~%"),
      printf(fh, "nodal_to_modal_~ad_~a_p~a(const double *fnodal, double *fmodal)~%", d, bName[bInd], polyOrder),
      printf(fh, "{~%"),
      /* Get equi-distant node locations */
      if (d = 6 and polyOrder = 1) then (
        nodes : getNodes("tensor", d, polyOrder)
      )
      else (
        nodes : getNodes(bName[bInd], d, polyOrder)
      ),
      numNodes : length(nodes),

      /* Get modal expansion from nodal points. */
      nodToMod : calcNodToModWithNodes(bName[bInd], d, polyOrder, nodes),
      fmod_e : gcfac(fullratsimp(nodToMod . makelist(fnodal[i-1],i,1,numNodes))),
      fmod_e : makelist(fmod_e[i][1],i,1,numNodes),
      /* Write coefficients of surface expansion fupwind. */
      /* Clever factorization is possible for polyOrder = 1, but for higher
         polynomial orders, factorization is *too* clever and can lead to 
         deeply nested parentheses, not ideal for compiler optimizations */
      if (polyOrder = 1) then (
        writeCExprsNoExpand1(fmodal, fmod_e)
      ) else (
        writeCExprs1(fmodal, fmod_e)
      ),
      printf(fh, "}~%~%")
    )
  ),
  close(fh)
);