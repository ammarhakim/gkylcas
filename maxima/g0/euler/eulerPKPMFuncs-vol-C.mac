load("modal-basis")$
load("out-scripts")$
fpprec : 24$

statevec : [rhou0, rhou1, rhou2, energy]$
dx1  : [dx10, dx11, dx12]$
lhs : [outrhou0, outrhou1, outrhou2, outenergy]$

cvars : [x, y, z]$

calcEulerPKPMVolUpdater(fh, funcNm, cdim, basisFun, pOrder) := block([modNm,bC,NC,cid,
  rhoux_e,rhouy_e,rhouz_e,energy_e,
  ux_e, uy_e, uz_e, u_e,
  Pxx_e, Pxy_e, Pxz_e, Pyy_e, Pyz_e, Pzz_e, P_dot_u_e,
  qx_e, qy_e, qz_e, q_e,
  umid,t2,t3,t4,t5,volTerms,vol,expr,varsC,basisC],

  kill(varsC, bC),

  /* Load basis of dimensionality requested. */
  [varsC,bC] : loadBasis(basisFun, cdim, polyOrder),

  /* Number of basis monomials. */
  NC : length(bC),

  printf(fh, "#include <gkyl_euler_kernels.h> ~%"),

  /* specify a point to evaluate maximum speed at for use in computing CFL */
  cflPt : makelist(varsC[d]=0, d, 1, length(varsC)),

  printf(fh, "GKYL_CU_DH double ~a_p~a(const double *w, const double *dxv, const double *u_i, const double *p_ij, const double *vlasov_pkpm_moms, const double *statevec, double* GKYL_RESTRICT out) ~%{ ~%", funcNm, pOrder),
  printf(fh, "  // w[NDIM]: Cell-center coordinates.~%"),
  printf(fh, "  // dxv[NDIM]: Cell spacing.~%"),
  printf(fh, "  // uvar: [ux, uy, uz], Fluid flow.~%"),
  printf(fh, "  // ppar: Fluid parallel pressure (computed from kinetic equation).~%"),
  printf(fh, "  // pperp: Fluid perpendicular pressure.~%"),
  printf(fh, "  // qpar: Fluid parallel heat flux (computed from kinetic equation).~%"),
  printf(fh, "  // statevec: [rho ux, rho uy, rho uz, energy], Fluid input state vector.~%"),
  printf(fh, "  // out: Incremented output.~%"),
  printf(fh, "~%"),
  for dir : 1 thru cdim do (
    printf(fh, "  double dx1~a = 2./dxv[~a]; ~%", dir-1, dir-1)
  ),
  printf(fh, "~%"),

  printf(fh, "  const double *rhou0 = &statevec[~a]; ~%", 0*NC),
  printf(fh, "  const double *rhou1 = &statevec[~a]; ~%", 1*NC),
  printf(fh, "  const double *rhou2 = &statevec[~a]; ~%", 2*NC),
  printf(fh, "  const double *energy = &statevec[~a]; ~%", 3*NC),
  printf(fh, "~%"),

  /* access flow, pressure tensor, and heat flux data */
  printf(fh, "  const double *ux = &u_i[~a]; ~%", 0),
  printf(fh, "  const double *uy = &u_i[~a]; ~%", NC),
  printf(fh, "  const double *uz = &u_i[~a]; ~%", NC*2),
  printf(fh, "~%"),

  printf(fh, "  const double *Pxx = &p_ij[~a]; ~%", 0),
  printf(fh, "  const double *Pxy = &p_ij[~a]; ~%", NC),
  printf(fh, "  const double *Pxz = &p_ij[~a]; ~%", NC*2),
  printf(fh, "  const double *Pyy = &p_ij[~a]; ~%", NC*3),
  printf(fh, "  const double *Pyz = &p_ij[~a]; ~%", NC*4),
  printf(fh, "  const double *Pzz = &p_ij[~a]; ~%", NC*5),
  printf(fh, "~%"),

  /* q_parallel b_hat only needed in directions being updated */
  for dir : 1 thru cdim do (
    printf(fh, "  const double *q~a = &vlasov_pkpm_moms[~a]; ~%", cvars[dir], (2+dir-1)*NC)
  ),
  printf(fh, "~%"),

  printf(fh, "  double *outrhou0 = &out[~a]; ~%", 0*NC),
  printf(fh, "  double *outrhou1 = &out[~a]; ~%", 1*NC),
  printf(fh, "  double *outrhou2 = &out[~a]; ~%", 2*NC),
  printf(fh, "  double *outenergy = &out[~a]; ~%", 3*NC),
  printf(fh, "~%"),

  /* cflFreq_mid is our approximation of sum_i max(abs(alpha_i))/(dx_i/(2p+1)) */
  printf(fh, "  double cflFreq_mid = 0.0; ~%"),

  rhoux_e : doExpand1(statevec[1], bC),
  rhouy_e : doExpand1(statevec[2], bC),
  rhouz_e : doExpand1(statevec[3], bC),
  energy_e : doExpand1(statevec[4], bC),

  /* Expand flow, u_i, pressure tensor, P_ij, and parallel heat flux, q_parallel b_i, in configuration space basis */
  ux_e : doExpand1(ux, bC),
  uy_e : doExpand1(uy, bC),
  uz_e : doExpand1(uz, bC),
  u_e : [ux_e, uy_e, uz_e],

  Pxx_e : doExpand1(Pxx, bC),
  Pxy_e : doExpand1(Pxy, bC),
  Pxz_e : doExpand1(Pxz, bC),
  Pyy_e : doExpand1(Pyy, bC),
  Pyz_e : doExpand1(Pyz, bC),
  Pzz_e : doExpand1(Pzz, bC),
  P_dot_u_e : [Pxx_e*ux_e + Pxy_e*uy_e + Pxz_e*uz_e, 
               Pxy_e*ux_e + Pyy_e*uy_e + Pyz_e*uz_e, 
               Pxz_e*ux_e + Pyz_e*uy_e + Pzz_e*uz_e],

  qx_e : doExpand1(qx, bC),
  qy_e : doExpand1(qy, bC),
  qz_e : doExpand1(qz, bC),
  q_e : [qx_e, qy_e, qz_e],

  /* compute stable time step, max speed = |u| */
  for dir : 1 thru cdim do (
    /* evaluate velocity at center of cell */
    umid : gcfac(float(expand(subst(cflPt, u_e[dir])))),
    printf(fh, "  cflFreq_mid += 0.5*~a*(fabs(~a)); ~%", float(2*polyOrder+1)*dx1[dir], umid)
  ),
  printf(fh, "~%"),

  /* Momentum update, flux = rho*u*u + pperp*I + (ppar - pperp)bb, where I is the identity matrix */
  /* Note we express P =  pperp*I + (ppar - pperp)bb as P_ij */
  t2 : (t2 : 0, for dir : 1 thru cdim do (
     if (dir = 1) then (t2 : t2 + dx1[dir]*calcInnerProdList(varsC, 1, diff(bC, varsC[dir]), rhoux_e*ux_e) 
                                + dx1[dir]*calcInnerProdList(varsC, 1, diff(bC, varsC[dir]), Pxx_e)),
     if (dir = 2) then (t2 : t2 + dx1[dir]*calcInnerProdList(varsC, 1, diff(bC, varsC[dir]), rhoux_e*uy_e)
                                + dx1[dir]*calcInnerProdList(varsC, 1, diff(bC, varsC[dir]), Pxy_e)),
     if (dir = 3) then (t2 : t2 + dx1[dir]*calcInnerProdList(varsC, 1, diff(bC, varsC[dir]), rhoux_e*uz_e)
                                + dx1[dir]*calcInnerProdList(varsC, 1, diff(bC, varsC[dir]), Pxz_e))
     ),
  t2),
  t3 : (t3 : 0, for dir : 1 thru cdim do (
     if (dir = 1) then (t3 : t3 + dx1[dir]*calcInnerProdList(varsC, 1, diff(bC, varsC[dir]), rhouy_e*ux_e)
                                + dx1[dir]*calcInnerProdList(varsC, 1, diff(bC, varsC[dir]), Pxy_e)),
     if (dir = 2) then (t3 : t3 + dx1[dir]*calcInnerProdList(varsC, 1, diff(bC, varsC[dir]), rhouy_e*uy_e)
                                + dx1[dir]*calcInnerProdList(varsC, 1, diff(bC, varsC[dir]), Pyy_e)),
     if (dir = 3) then (t3 : t3 + dx1[dir]*calcInnerProdList(varsC, 1, diff(bC, varsC[dir]), rhouy_e*uz_e)
                                + dx1[dir]*calcInnerProdList(varsC, 1, diff(bC, varsC[dir]), Pyz_e))
     ),
  t3),
  t4 : (t4 : 0, for dir : 1 thru cdim do (
     if (dir = 1) then (t4 : t4 + dx1[dir]*calcInnerProdList(varsC, 1, diff(bC, varsC[dir]), rhouz_e*ux_e)
                                + dx1[dir]*calcInnerProdList(varsC, 1, diff(bC, varsC[dir]), Pxz_e)),
     if (dir = 2) then (t4 : t4 + dx1[dir]*calcInnerProdList(varsC, 1, diff(bC, varsC[dir]), rhouz_e*uy_e)
                                + dx1[dir]*calcInnerProdList(varsC, 1, diff(bC, varsC[dir]), Pyz_e)),
     if (dir = 3) then (t4 : t4 + dx1[dir]*calcInnerProdList(varsC, 1, diff(bC, varsC[dir]), rhouz_e*uz_e)
                                + dx1[dir]*calcInnerProdList(varsC, 1, diff(bC, varsC[dir]), Pzz_e))
     ),
  t4),
  /* Energy update, flux = E*u + P.u + q_parallel b_hat */
  t5 : (t5 : 0, for dir : 1 thru cdim do (
     t5 : t5 + dx1[dir]*calcInnerProdList(varsC, 1, diff(bC, varsC[dir]), energy_e*u_e[dir]+P_dot_u_e[dir]+q_e[dir])
     ),
  t5),

  volTerms : [t2,t3,t4,t5],
  for m : 1 thru 4 do (
    vol : volTerms[m],
    writeCIncrExprs1(lhs[m], vol),
    printf(fh, "~%")
  ),

  printf(fh, "  return cflFreq_mid; ~%"),
  printf(fh, "} ~%")

)$
