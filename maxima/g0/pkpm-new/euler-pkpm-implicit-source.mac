/* This script generates the kernels for computing the semi-implicit 
   source solve as described in Wang et al. JCP 2020 for the PKPM system.
   We construct a linear system of (3*num_species + 3) for each of the
   species current densities and the three components of the electric field. */

load("modal-basis")$
load("out-scripts")$
load(stringproc)$
load("scifac")$
fpprec : 24$

set_pkpm_em_source_solve(fh, funcNm, cdim, polyOrder) := block(
  [varsC, bC, bC_2p, NC, NC_2p],

  kill(varsC, bC),
  kill(varsC, bC_2p),

  /* Load basis of dimensionality requested. Loads a basis of order p and p/2*/
  [varsC,bC] : loadBasis("Tensor", cdim, polyOrder),
  [varsC,bC_2p] : loadBasis("Tensor", cdim, 2*polyOrder),

  /* Number of basis monomials. */
  NC : length(bC),
  NC_2p : length(bC_2p),

  printf(fh, "#include <gkyl_mat.h> ~%"),
  printf(fh, "#include <gkyl_euler_pkpm_kernels.h> ~%"),
  printf(fh, "GKYL_CU_DH void ~a(int count, 
  int num_species, double qbym[GKYL_MAX_SPECIES], double epsilon0, bool pkpm_field_static, double dt, 
  struct gkyl_nmat *A_n, struct gkyl_nmat *rhs_n, 
  const double *app_accel[GKYL_MAX_SPECIES], const double *ext_em, const double *app_current, 
  const double *vlasov_pkpm_moms[GKYL_MAX_SPECIES], const double* pkpm_u[GKYL_MAX_SPECIES], 
  double* GKYL_RESTRICT em) ~%{ ~%", funcNm, polyOrder),
  printf(fh, "  // count:             integer to indicate which matrix being fetched. ~%"),
  printf(fh, "  // num_species:       number of species being evolved (number of momentum equations). ~%"),
  printf(fh, "  // qbym:              charge/mass ratio for each species. ~%"),
  printf(fh, "  // epsilon0:          permittivity of free space. ~%"),
  printf(fh, "  // pkpm_field_static: boolean for whether or not the self-consistent field is static. ~%"),
  printf(fh, "  // dt:                size of the time step. ~%"),  
  printf(fh, "  // A:                 preallocated LHS matrix. ~%"),
  printf(fh, "  // rhs:               preallocated RHS vector. ~%"),
  printf(fh, "  // app_accel:         Applied accelerations (external forces).~%"),
  printf(fh, "  // ext_em:            Externally applied EM fields.~%"),
  printf(fh, "  // app_current:       Applied external currents.~%"),
  printf(fh, "  // vlasov_pkpm_moms:  [rho, p_parallel, p_perp], Moments computed from kinetic equation in pkpm model.~%"),
  printf(fh, "  // pkpm_u:            [ux, uy, uz], Input flow velocity.~%"),
  printf(fh, "  // em:                [Ex, Ey, Ez, Bx, By, Bz], EM input state vector.~%"),
  printf(fh, "~%"),

  printf(fh, "  struct gkyl_mat lhs = gkyl_nmat_get(A_n, count); ~%"),
  printf(fh, "  struct gkyl_mat rhs = gkyl_nmat_get(rhs_n, count); ~%"),
  printf(fh, "  // Clear matrix and rhs source solve. ~%"),
  printf(fh, "  gkyl_mat_clear(&lhs, 0.0); gkyl_mat_clear(&rhs, 0.0); ~%"),
  printf(fh, "~%"),

  printf(fh, "  double rho[GKYL_MAX_SPECIES][~a]; ~%", NC_2p), 
  printf(fh, "  double ux[GKYL_MAX_SPECIES][~a]; ~%", NC), 
  printf(fh, "  double uy[GKYL_MAX_SPECIES][~a]; ~%", NC), 
  printf(fh, "  double uz[GKYL_MAX_SPECIES][~a]; ~%", NC), 
  printf(fh, "~%"),
  printf(fh, "  double app_accel_x[GKYL_MAX_SPECIES][~a]; ~%", NC), 
  printf(fh, "  double app_accel_y[GKYL_MAX_SPECIES][~a]; ~%", NC), 
  printf(fh, "  double app_accel_z[GKYL_MAX_SPECIES][~a]; ~%", NC), 
  printf(fh, "~%"),

  printf(fh, "  for (int i = 0; i < num_species; ++i) { ~%"),
  printf(fh, "    const double *inp_u = pkpm_u[i]; ~%"),
  printf(fh, "    const double *inp_app_accel = app_accel[i]; ~%"),
  printf(fh, "    const double *inp_vlasov_pkpm_moms = vlasov_pkpm_moms[i]; ~%"),
  printf(fh, "~%"),

  /* Mass density is order 2*p because it comes from the kinetic equation */
  for j : 1 thru NC_2p do (
    printf(fh, "    rho[i][~a] = inp_vlasov_pkpm_moms[~a]; ~%", j-1, j-1)
  ),
  printf(fh, "~%"),
  for j : 1 thru NC do (
    printf(fh, "    ux[i][~a] = inp_u[~a]; ~%", j-1, j-1+0*NC), 
    printf(fh, "    uy[i][~a] = inp_u[~a]; ~%", j-1, j-1+1*NC), 
    printf(fh, "    uz[i][~a] = inp_u[~a]; ~%", j-1, j-1+2*NC),
    printf(fh, "~%"),
    printf(fh, "    app_accel_x[i][~a] = inp_app_accel[~a]; ~%", j-1, j-1+0*NC), 
    printf(fh, "    app_accel_y[i][~a] = inp_app_accel[~a]; ~%", j-1, j-1+1*NC), 
    printf(fh, "    app_accel_z[i][~a] = inp_app_accel[~a]; ~%", j-1, j-1+2*NC),
    printf(fh, "~%")    
  ), 
  printf(fh, "  } ~%"),
  printf(fh, "~%"),

  printf(fh, "  double *Ex = &em[~a]; ~%", 0*NC),
  printf(fh, "  double *Ey = &em[~a]; ~%", 1*NC),
  printf(fh, "  double *Ez = &em[~a]; ~%", 2*NC),
  printf(fh, "  double *Bx = &em[~a]; ~%", 3*NC),
  printf(fh, "  double *By = &em[~a]; ~%", 4*NC),
  printf(fh, "  double *Bz = &em[~a]; ~%", 5*NC),
  printf(fh, "~%"), 
  printf(fh, "  const double *ext_Ex = &ext_em[~a]; ~%", 0*NC),
  printf(fh, "  const double *ext_Ey = &ext_em[~a]; ~%", 1*NC),
  printf(fh, "  const double *ext_Ez = &ext_em[~a]; ~%", 2*NC),
  printf(fh, "  const double *ext_Bx = &ext_em[~a]; ~%", 3*NC),
  printf(fh, "  const double *ext_By = &ext_em[~a]; ~%", 4*NC),
  printf(fh, "  const double *ext_Bz = &ext_em[~a]; ~%", 5*NC),
  printf(fh, "~%"), 
  printf(fh, "  const double *app_curr_x = &app_current[~a]; ~%", 0*NC),
  printf(fh, "  const double *app_curr_y = &app_current[~a]; ~%", 1*NC),
  printf(fh, "  const double *app_curr_z = &app_current[~a]; ~%", 2*NC),
  printf(fh, "~%"), 

  printf(fh, "  double tot_Bx[~a]; ~%", NC), 
  printf(fh, "  double tot_By[~a]; ~%", NC), 
  printf(fh, "  double tot_Bz[~a]; ~%", NC), 
  for j : 1 thru NC do (
    printf(fh, "  tot_Bx[~a] = Bx[~a] + ext_Bx[~a]; ~%", j-1, j-1, j-1), 
    printf(fh, "  tot_By[~a] = By[~a] + ext_By[~a]; ~%", j-1, j-1, j-1), 
    printf(fh, "  tot_Bz[~a] = Bz[~a] + ext_Bz[~a]; ~%", j-1, j-1, j-1) 
  ), 
  printf(fh, "~%"),

  /* Set the RHS for all known quantities including the solution at the old time-step, and any 
     external electromagnetic fields, applied accelerations, or applied currents */
  printf(fh, "  // Set RHS for flow velocity equations, including solution at known time-step and external forces. ~%"),     
  printf(fh, "  for (int i = 0; i < num_species; ++i) { ~%"),
  printf(fh, "~%"),
  for j : 1 thru NC do (
    printf(fh,"    gkyl_mat_set(&rhs, ~a + i*(~a), 0, ux[i][~a] + 0.5*dt*(qbym[i]*ext_Ex[~a] + app_accel_x[i][~a])); ~%", j-1, 3*NC, j-1, j-1, j-1, j-1),
    printf(fh,"    gkyl_mat_set(&rhs, ~a + i*(~a), 0, uy[i][~a] + 0.5*dt*(qbym[i]*ext_Ey[~a] + app_accel_y[i][~a])); ~%", j-1+1*NC, 3*NC, j-1, j-1, j-1, j-1),
    printf(fh,"    gkyl_mat_set(&rhs, ~a + i*(~a), 0, uz[i][~a] + 0.5*dt*(qbym[i]*ext_Ez[~a] + app_accel_z[i][~a])); ~%", j-1+2*NC, 3*NC, j-1, j-1, j-1, j-1),
    printf(fh, "~%")
  ),  
  printf(fh, "  } ~%"),
  printf(fh, "~%"),  

  printf(fh, "  // Set RHS for Ampere's Law, including solution at known time-step and applied currents. ~%"),   
  for j : 1 thru NC do (
    printf(fh,"  gkyl_mat_set(&rhs, ~a + num_species*(~a), 0, epsilon0*Ex[~a] - 0.5*dt*app_curr_x[~a]); ~%", j-1, 3*NC, j-1, j-1),
    printf(fh,"  gkyl_mat_set(&rhs, ~a + num_species*(~a), 0, epsilon0*Ey[~a] - 0.5*dt*app_curr_y[~a]); ~%", j-1+1*NC, 3*NC, j-1, j-1),
    printf(fh,"  gkyl_mat_set(&rhs, ~a + num_species*(~a), 0, epsilon0*Ez[~a] - 0.5*dt*app_curr_z[~a]); ~%", j-1+2*NC, 3*NC, j-1, j-1),
    printf(fh, "~%")
  ),  

  /* Expand total magnetic field and mass density */
  tot_Bx_e : doExpand1(tot_Bx,bC),
  tot_By_e : doExpand1(tot_By,bC),
  tot_Bz_e : doExpand1(tot_Bz,bC),
  rhoi_e : doExpand1(rho[s],bC_2p),

  /* Generate matrices of unknown coefficients */

  /* Unknowns from cross product with Bx */
  uxB_x_e : doExpand1(uxB_x,bC),
  uxB_x_n : makelist(uxB_x[i],i,0,NC-1),
  eq_uxB_x : calcInnerProdList(varsC, uxB_x_e, bC, tot_Bx_e),
  E_uxB_x : fullratsimp(coefmatrix(eq_uxB_x, uxB_x_n)),

  /* Unknowns from cross product with By */
  uxB_y_e : doExpand1(uxB_y,bC),
  uxB_y_n : makelist(uxB_y[i],i,0,NC-1),
  eq_uxB_y : calcInnerProdList(varsC, uxB_y_e, bC, tot_By_e),
  E_uxB_y : fullratsimp(coefmatrix(eq_uxB_y, uxB_y_n)),

  /* Unknowns from cross product with Bz */
  uxB_z_e : doExpand1(uxB_z,bC),
  uxB_z_n : makelist(uxB_z[i],i,0,NC-1),
  eq_uxB_z : calcInnerProdList(varsC, uxB_z_e, bC, tot_Bz_e),
  E_uxB_z : fullratsimp(coefmatrix(eq_uxB_z, uxB_z_n)),

  /* Unknowns from electric field force */
  rhoE_e : doExpand1(rhoE,bC),
  rhoE_n : makelist(rhoE[i],i,0,NC-1),
  eq_rhoE : calcInnerProdList(varsC, rhoE_e, bC, rhoi_e),
  E_rhoE : fullratsimp(coefmatrix(eq_rhoE, rhoE_n)),

  /* Remaining unknowns (no weak multiplications) */
  A_e : doExpand1(A,bC),
  A_n : makelist(A[i],i,0,NC-1),
  eq_A : calcInnerProdList(varsC, 1, bC, A_e),
  E_A : fullratsimp(coefmatrix(eq_A, A_n)),

  printf(fh, "  // Construct LHS. ~%"), 
  printf(fh, "  // For flow velocity equation: u_s^{n+1} - 0.5*dt*(q_s/m_s*E^{n+1} + q_s/m_s*u_s^{n+1} x B^n). ~%"), 
  printf(fh, "  // For Ampere's Law: epsilon0*E^{n+1} + 0.5*dt*sum_s q_s/m_s*rho_s^n u_s^{n+1}. ~%"), 
  printf(fh, "  for (int s = 0; s < num_species; ++s) { ~%"),
  printf(fh, " ~%"),
  printf(fh, "    double E_field_fac = -0.5*dt*qbym[s]/epsilon0; ~%", NC), 
  printf(fh, "    double B_field_fac = -0.5*dt*qbym[s]; ~%", NC), 
  /* Matrix is dense so just do a simple write routine */
  for i : 1 thru NC do (
    for j : 1 thru NC do (
      /* Diagonal entries of matrix (ux^{n+1}, uy^{n+1}, uz^{n+1}) */
      printf(fh, "    gkyl_mat_set(&lhs, ~a + s*(~a), ~a + s*(~a), ~a); ~%", i-1, 3*NC, j-1, 3*NC, float(expand(E_A[i,j]))),
      printf(fh, "    gkyl_mat_set(&lhs, ~a + s*(~a), ~a + s*(~a), ~a); ~%", i-1+1*NC, 3*NC, j-1+1*NC, 3*NC, float(expand(E_A[i,j]))),
      printf(fh, "    gkyl_mat_set(&lhs, ~a + s*(~a), ~a + s*(~a), ~a); ~%", i-1+2*NC, 3*NC, j-1+2*NC, 3*NC, float(expand(E_A[i,j]))),
      printf(fh, " ~%"),

      /* Electric field force (-0.5*dt*q/m)/epsilon0 * (epsilon0*E^{n+1}) for Ex, Ey, and Ez */
      printf(fh, "    gkyl_mat_set(&lhs, ~a + s*(~a), ~a + num_species*(~a), E_field_fac*(~a)); ~%", i-1, 3*NC, j-1, 3*NC, float(expand(E_A[i,j]))),
      printf(fh, "    gkyl_mat_set(&lhs, ~a + s*(~a), ~a + num_species*(~a), E_field_fac*(~a)); ~%", i-1+1*NC, 3*NC, j-1+1*NC, 3*NC, float(expand(E_A[i,j]))),
      printf(fh, "    gkyl_mat_set(&lhs, ~a + s*(~a), ~a + num_species*(~a), E_field_fac*(~a)); ~%", i-1+2*NC, 3*NC, j-1+2*NC, 3*NC, float(expand(E_A[i,j]))),
      printf(fh, " ~%"),

      /* Magnetic force from Bx affects uy and uz (uz*Bx and -uy*Bx respectively) */
      printf(fh, "    gkyl_mat_set(&lhs, ~a + s*(~a), ~a + s*(~a), B_field_fac*(~a)); ~%", i-1+1*NC, 3*NC, j-1+2*NC, 3*NC, float(expand(E_uxB_x[i,j]))),
      printf(fh,"    gkyl_mat_set(&lhs, ~a + s*(~a), ~a + s*(~a), -B_field_fac*(~a)); ~%", i-1+2*NC, 3*NC, j-1+1*NC, 3*NC, float(expand(E_uxB_x[i,j]))),
      printf(fh, " ~%"),

      /* Lorentz force from By affects uz and ux (ux*By and -uz*By respectively) */
      printf(fh,"    gkyl_mat_set(&lhs, ~a + s*(~a), ~a + s*(~a), B_field_fac*(~a)); ~%", i-1+2*NC, 3*NC, j-1, 3*NC, float(expand(E_uxB_y[i,j]))),
      printf(fh,"    gkyl_mat_set(&lhs, ~a + s*(~a), ~a + s*(~a), -B_field_fac*(~a)); ~%", i-1, 3*NC, j-1+2*NC, 3*NC, float(expand(E_uxB_y[i,j]))),
      printf(fh, " ~%"),

      /* Lorentz force from Bz affects ux and uy (uy*Bz and -ux*Bz respectively) */
      printf(fh,"    gkyl_mat_set(&lhs, ~a + s*(~a), ~a + s*(~a), B_field_fac*(~a)); ~%", i-1, 3*NC, j-1+1*NC, 3*NC, float(expand(E_uxB_z[i,j]))),
      printf(fh,"    gkyl_mat_set(&lhs, ~a + s*(~a), ~a + s*(~a), -B_field_fac*(~a)); ~%", i-1+1*NC, 3*NC, j-1, 3*NC, float(expand(E_uxB_z[i,j]))),
      printf(fh, " ~%"),

      /* Current accumulation in Ampere's Law if the PKPM self-consistent field is dynamic */
      printf(fh, "    if (!pkpm_field_static) { ~%"), 
      printf(fh, "      gkyl_mat_set(&lhs, ~a + num_species*(~a), ~a + s*(~a), 0.5*dt*qbym[s]*(~a)); ~%", i-1, 3*NC, j-1, 3*NC, float(expand(E_rhoE[i,j]))),
      printf(fh, "      gkyl_mat_set(&lhs, ~a + num_species*(~a), ~a + s*(~a), 0.5*dt*qbym[s]*(~a)); ~%", i-1+1*NC, 3*NC, j-1+1*NC, 3*NC, float(expand(E_rhoE[i,j]))),
      printf(fh, "      gkyl_mat_set(&lhs, ~a + num_species*(~a), ~a + s*(~a), 0.5*dt*qbym[s]*(~a)); ~%", i-1+2*NC, 3*NC, j-1+2*NC, 3*NC, float(expand(E_rhoE[i,j]))),
      printf(fh, "    } ~%"), 
      printf(fh, " ~%")      
    )
  ),
  printf(fh, "  } ~%"),

  /* Final component of matrix (diagonal component of E^{n+1}) */
  for i : 1 thru NC do (
    for j : 1 thru NC do (
      printf(fh, "  gkyl_mat_set(&lhs, ~a + num_species*(~a), ~a + num_species*(~a), ~a); ~%", i-1, 3*NC, j-1, 3*NC, float(expand(E_A[i,j]))),
      printf(fh, "  gkyl_mat_set(&lhs, ~a + num_species*(~a), ~a + num_species*(~a), ~a); ~%", i-1+1*NC, 3*NC, j-1+1*NC, 3*NC, float(expand(E_A[i,j]))),
      printf(fh, "  gkyl_mat_set(&lhs, ~a + num_species*(~a), ~a + num_species*(~a), ~a); ~%", i-1+2*NC, 3*NC, j-1+2*NC, 3*NC, float(expand(E_A[i,j]))),
      printf(fh, " ~%")    
    )
  ),
  printf(fh, "} ~%")
)$  

copy_pkpm_em_source_solve(fh, funcNm, cdim, polyOrder) := block(
  [varsC, bC, bC_2p, NC, NC_2p],

  kill(varsC, bC),
  kill(varsC, bC_2p),

  /* Load basis of dimensionality requested. Loads a basis of order p and p/2*/
  [varsC,bC] : loadBasis("Tensor", cdim, polyOrder),
  [varsC,bC_2p] : loadBasis("Tensor", cdim, 2*polyOrder),

  /* Number of basis monomials. */
  NC : length(bC),
  NC_2p : length(bC_2p),

  printf(fh, "#include <gkyl_mat.h> ~%"),
  printf(fh, "#include <gkyl_euler_pkpm_kernels.h> ~%"),
  printf(fh, "#include <gkyl_binop_mul_ser.h> ~%"),
  printf(fh, "GKYL_CU_DH void ~a(int count, 
  int num_species, double qbym[GKYL_MAX_SPECIES], double epsilon0, 
  struct gkyl_nmat *x, 
  const double *vlasov_pkpm_moms[GKYL_MAX_SPECIES], const double *pkpm_u[GKYL_MAX_SPECIES], 
  double* GKYL_RESTRICT euler_pkpm[GKYL_MAX_SPECIES], double* GKYL_RESTRICT em) ~%{ ~%", funcNm, polyOrder),
  printf(fh, "  // count:       integer to indicate which matrix being fetched. ~%"),
  printf(fh, "  // x:           Input solution vector. ~%"),
  printf(fh, "  // vlasov_pkpm_moms: [rho, p_parallel, p_perp], Moments computed from kinetic equation in pkpm model at old time t^n.~%"),
  printf(fh, "  // pkpm_u:      [ux, uy, uz], Input flow velocity at old time t^n.~%"),  
  printf(fh, "  // euler_pkpm:  [rho ux, rho uy, rho uz], Fluid output state vector at time t^{n+1}.~%"),
  printf(fh, "  // em:          [Ex, Ey, Ez, Bx, By, Bz], EM output state vector at time t^{n+1}.~%"),
  printf(fh, "  //              Source solve only updates Ex, Ey, Ez. ~%"),
  printf(fh, "~%"),

  printf(fh, "  struct gkyl_mat sol = gkyl_nmat_get(x, count); ~%"),

  printf(fh, "  double ux_new[~a] = {0.0}; ~%", NC), 
  printf(fh, "  double uy_new[~a] = {0.0}; ~%", NC), 
  printf(fh, "  double uz_new[~a] = {0.0}; ~%", NC), 
  printf(fh, "  double Ex_new[~a] = {0.0}; ~%", NC), 
  printf(fh, "  double Ey_new[~a] = {0.0}; ~%", NC), 
  printf(fh, "  double Ez_new[~a] = {0.0}; ~%", NC), 
  printf(fh, "~%"),

  printf(fh, "  for (int i = 0; i < num_species; ++i) { ~%"),
  printf(fh, "    const double *rho_old = &vlasov_pkpm_moms[i][~a]; ~%", 0*NC),
  printf(fh, "    const double *ux_old = &pkpm_u[i][~a]; ~%", 0*NC),
  printf(fh, "    const double *uy_old = &pkpm_u[i][~a]; ~%", 1*NC),
  printf(fh, "    const double *uz_old = &pkpm_u[i][~a]; ~%", 2*NC),
  for j : 1 thru NC do (
    printf(fh, "    ux_new[~a] = 2.0*gkyl_mat_get(&sol, ~a + i*(~a), 0) - ux_old[~a]; ~%", j-1, j-1, 3*NC, j-1),
    printf(fh, "    uy_new[~a] = 2.0*gkyl_mat_get(&sol, ~a + i*(~a), 0) - uy_old[~a]; ~%", j-1, j-1+1*NC, 3*NC, j-1),
    printf(fh, "    uz_new[~a] = 2.0*gkyl_mat_get(&sol, ~a + i*(~a), 0) - uz_old[~a]; ~%", j-1, j-1+2*NC, 3*NC, j-1),
    printf(fh, "~%")
  ), 
  /* We solved for u_s^{n+1}, so we need to weak multiply solution by rho^n to convert back to momentum */
  printf(fh, "    double *out_rhoux = &euler_pkpm[i][~a]; ~%", 0*NC_2p),
  printf(fh, "    double *out_rhouy = &euler_pkpm[i][~a]; ~%", 1*NC_2p),
  printf(fh, "    double *out_rhouz = &euler_pkpm[i][~a]; ~%", 2*NC_2p),
  printf(fh, "~%"),

  rho_old_e : doExpand1(rho_old, bC_2p), 
  ux_new_e : doExpand1(ux_new, bC), 
  uy_new_e : doExpand1(uy_new, bC), 
  uz_new_e : doExpand1(uz_new, bC),
  
  out_rhoux_c : calcInnerProdList(varsC, 1, bC_2p, rho_old_e*ux_new_e), 
  out_rhouy_c : calcInnerProdList(varsC, 1, bC_2p, rho_old_e*uy_new_e), 
  out_rhouz_c : calcInnerProdList(varsC, 1, bC_2p, rho_old_e*uz_new_e), 

  writeCExprs1(out_rhoux, out_rhoux_c), 
  writeCExprs1(out_rhouy, out_rhouy_c), 
  writeCExprs1(out_rhouz, out_rhouz_c), 
  printf(fh, "~%"),

  printf(fh, "  } ~%"),
  printf(fh, "~%"),

  printf(fh, "  double *out_Ex = &em[~a]; ~%", 0*NC),
  printf(fh, "  double *out_Ey = &em[~a]; ~%", 1*NC),
  printf(fh, "  double *out_Ez = &em[~a]; ~%", 2*NC),
  for j : 1 thru NC do (
    printf(fh, "  Ex_new[~a] = gkyl_mat_get(&sol, ~a + num_species*(~a), 0); ~%", j-1, j-1, 3*NC),
    printf(fh, "  Ey_new[~a] = gkyl_mat_get(&sol, ~a + num_species*(~a), 0); ~%", j-1, j-1+1*NC, 3*NC),
    printf(fh, "  Ez_new[~a] = gkyl_mat_get(&sol, ~a + num_species*(~a), 0); ~%", j-1, j-1+2*NC, 3*NC),
    printf(fh, "~%"),
    /* We solved for epsilon0*E^{n+1}, so we need to divide solution by epsilon0 to convert back to electric field */
    printf(fh, "  out_Ex[~a] = 2.0*Ex_new[~a]/epsilon0 - out_Ex[~a]; ~%", j-1, j-1, j-1), 
    printf(fh, "  out_Ey[~a] = 2.0*Ey_new[~a]/epsilon0 - out_Ey[~a]; ~%", j-1, j-1, j-1), 
    printf(fh, "  out_Ez[~a] = 2.0*Ez_new[~a]/epsilon0 - out_Ez[~a]; ~%", j-1, j-1, j-1), 
    printf(fh, "~%")
  ), 
  printf(fh, "} ~%")
)$  
