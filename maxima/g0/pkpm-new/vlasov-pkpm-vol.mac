/* This script generates the kernels for the volume term contribution from 
   the Vlasov equation for the parallel-kinetic-perpendicular-moment (pkpm) model */

load("modal-basis");
load("out-scripts");
load("utilities")$
load ("scifac")$
load(stringproc)$
fpprec : 24$

dx11 : [dx0, dx1, dx2]$

cidx(cdim)      := makelist(i,i,0,cdim-1)$
vidx(cdim,vdim) := makelist(i,i,cdim,cdim+vdim-1)$

/* Helper functions for expanding in basis functions a quantity we know should be sparse 
   For example, alpha, the phase space flow only depends on a few phase space coordinates */
/* List output is a vector with some offset off */
doMakeExprLstOff(vals, S, off)  := makelist(if vals[i] # 0 then S[off+i-1] else 0, i, 1, length(vals))$
/* List output is a scalar */
doMakeExprLst(vals, S)  := makelist(if vals[i] # 0 then S[i-1] else 0, i, 1, length(vals))$

calc_pkpm_vlasov_vol(fh, funcNm, cdim, vdim, polyOrder) := block(
  [varsC, bC, varsP, bP, NP, NC, bC_p1, NC_p1, cflPt, cid, vid, vTrans, 
   F_0_e, G_1_e, F_0_source_e, G_1_source_e, G_1_vperp_e,
   ux_e, uy_e, uz_e, u_e, bx_e, by_e, bz_e, b_e, 
   p_force_e, bb_grad_u_e, div_b_e, p_perp_div_b_e, 
   incr_cdim_F_0_c, incr_cdim_G_1_c, alpha_cdim_c, expr, alpha_cdim_NoZero, alpha_cdim_NoZero_e, 
   amid, amid_vdim, alpha_vdim_c, clst, alpha_vdim_NoZero, alpha_vdim_NoZero_e, incr_vdim_F_0_c, incr_vdim_G_1_c, 
   alpha_div_b_c, alpha_div_b_NoZero, alpha_div_b_NoZero_e, amid_div_b, 
   incr_F_0_div_b_c, incr_G_1_div_b_c],

  printf(fh, "#include <gkyl_vlasov_pkpm_kernels.h> ~%"),

  /* Load basis of dimensionality requested. */
  [varsC,bC,varsP,bP] : loadPhaseBasis("Tensor", cdim, vdim, polyOrder),

  /* Number of basis monomials. */
  NP : length(bP),
  NC : length(bC),

  /* Load a p=1 basis for the flow velocity */
  [varsC,bC_p1] : loadBasis("Tensor", cdim, 1),
  NC_p1 : length(bC_p1),

  /* specify a point to evaluate alpha at for use in computing CFL */
  cflPt : makelist(varsP[d]=0, d, 1, length(varsP)),

  printf(fh, "GKYL_CU_DH double ~a(const double *w, const double *dxv, 
  const double *bvar, const double *pkpm_u, 
  const double *div_b, const double *pkpm_accel_vars, 
  const double *g_dist_source, const double *f, 
  double* GKYL_RESTRICT out) ~%{ ~%", funcNm),
  printf(fh, "  // w[NDIM]:         Cell-center coordinates. ~%"),
  printf(fh, "  // dxv[NDIM]:       Cell spacing. ~%"),
  printf(fh, "  // bvar:            Input magnetic field unit vector and tensor (nine components; first three components, b_i, other six components, b_i b_j). ~%"),
  printf(fh, "  // pkpm_u:          Input flow velocity [ux, uy, uz]. ~%"),
  printf(fh, "  // div_b:           Input volume expansion of div(b). ~%"), 
  printf(fh, "  // pkpm_accel_vars: Input pkpm acceleration variables [T_perp/m*div(b), bb:grad(u), p_force, p_perp_source]. ~%"), 
  printf(fh, "  // g_dist_source:   Input [2.0*T_perp/m*(2.0*T_perp/m G + T_perp/m (F_2 - F_0)), ~%"), 
  printf(fh, "  //                  (-vpar div(b) + bb:grad(u) - div(u) - 2 nu) T_perp/m G + 2 nu vth^2 F_0 ]. ~%"), 
  printf(fh, "  //                  First input is mirror force source, second input is vperp characteristics source. ~%"), 
  printf(fh, "  // f:               Input distribution functions [F_0, T_perp/m G = T_perp/m (F_0 - F_1)].~%"),
  printf(fh, "  // out:             Incremented output distribution functions. ~%"),

  cid : cidx(cdim),
  vid : vidx(cdim,vdim),

  /* common expressions in streaming [v_parallel b_hat + u] dot grad(f) term */
  for dir : 1 thru cdim do (
    printf(fh, "  const double dx~a = 2.0/dxv[~a]; ~%", dir-1, cid[dir])
  ),
  /* grid spacing in v_parallel and v_parallel coordinate */
  printf(fh, "  const double dv1par = 2.0/dxv[~a]; ~%", cdim),
  printf(fh, "  const double dvpar = dxv[~a], wvpar = w[~a]; ~%", cdim, cdim),

  /* access flow velocity and magnetic field unit vector data */
  printf(fh, "  const double *ux = &pkpm_u[~a]; ~%", 0*NC_p1),
  printf(fh, "  const double *uy = &pkpm_u[~a]; ~%", 1*NC_p1),
  printf(fh, "  const double *uz = &pkpm_u[~a]; ~%", 2*NC_p1),

  printf(fh, "  const double *bx = &bvar[~a]; ~%", 0*NC),
  printf(fh, "  const double *by = &bvar[~a]; ~%", 1*NC),
  printf(fh, "  const double *bz = &bvar[~a]; ~%", 2*NC),
  printf(fh, "~%"),

  /* Access F_0 and T_perp/m G, first two Laguerre moments */
  printf(fh, "  const double *F_0 = &f[~a]; ~%", 0*NP),
  printf(fh, "  const double *G_1 = &f[~a]; ~%", 1*NP),  

  /* Access source distribution functions for mirror force
     F_0 source = T_perp/m G_1; 
     G_1 source = 2.0*T_perp/m*(2.0*T_perp/m*G_1 + T_perp/m*(F_2 - F_0)) (First NP components of g_dist_source) 
     G_1 vperp characteristics = (-vpar div(b) + bb:grad(u) - div(u) - 2 nu) T_perp/m G + nu vth^2 F_0 (Second NP components of g_dist_source) */
  printf(fh, "  const double *F_0_source = &f[~a]; ~%", 1*NP),
  printf(fh, "  const double *G_1_source = &g_dist_source[~a]; ~%", 0*NP),  
  printf(fh, "  const double *G_1_vperp = &g_dist_source[~a]; ~%", 1*NP),  

  /* Access pkpm acceleration variables for forces. */
  printf(fh, "  const double *p_perp_div_b = &pkpm_accel_vars[~a]; ~%", 0*NC),
  printf(fh, "  const double *bb_grad_u = &pkpm_accel_vars[~a]; ~%", 1*NC),
  printf(fh, "  const double *p_force = &pkpm_accel_vars[~a]; ~%", 2*NC),
  printf(fh, "~%"),

  /* Access the components of out for the first two Laguerre moments */
  printf(fh, "  double *out_F_0 = &out[~a]; ~%", 0*NP),
  printf(fh, "  double *out_G_1 = &out[~a]; ~%", 1*NP),    
  printf(fh, "~%"),

  /* cflFreq_mid is our approximation of sum_i max(abs(alpha_i))/(dx_i/(2p+1)) */
  printf(fh, "  double cflFreq_mid = 0.0; ~%"),
  printf(fh, "~%"),

  printf(fh, "  double alpha_cdim[~a] = {0.0}; ~%", cdim*NP),
  printf(fh, "  double alpha_vdim[~a] = {0.0}; ~%", NP),
  printf(fh, "  double alpha_div_b[~a] = {0.0}; ~%", NP),
  printf(fh, "~%"),

  printf(fh, "  double incr_cdim_F_0[~a] = {0.0}; ~%", NP),
  printf(fh, "  double incr_vdim_F_0[~a] = {0.0}; ~%", NP),
  printf(fh, "  double incr_F_0_div_b[~a] = {0.0}; ~%", NP),
  printf(fh, "  double incr_cdim_G_1[~a] = {0.0}; ~%", NP),
  printf(fh, "  double incr_vdim_G_1[~a] = {0.0}; ~%", NP),
  printf(fh, "  double incr_G_1_div_b[~a] = {0.0}; ~%", NP),
  printf(fh, "~%"),

  /* Coordinate transformation for velocity space */
  vTrans : vx*dvpar/2+wvpar,

  /* Expand distribution functions F_0 and T_perp/m G in phase basis. */
  F_0_e : doExpand1(F_0, bP),
  G_1_e : doExpand1(G_1, bP),
  /* Expand F_0_source = T_perp/m G_1
            G_1_source = 2.0*T_perp/m*(2.0*T_perp/m*G_1 + T_perp/m*(F_2 - F_0))
            G_1 vperp characteristics = (-vpar div(b) + bb:grad(u) - div(u) - 2 nu) T_perp/m G + nu vth^2 F_0 */
  F_0_source_e : doExpand1(F_0_source, bP),
  G_1_source_e : doExpand1(G_1_source, bP),
  G_1_vperp_e : doExpand1(G_1_vperp, bP),

  /* Expand u, b, p_force = 1/rho (div(p_parallel b_hat) - p_perp div(b)), 
     bb : grad(u), div(b), and p_perp_div_b = 2*p_perp/rho*div(b), 
     in configuration space basis 
     Note: u is only a p=1 basis */
  ux_e : doExpand1(ux, bC_p1),
  uy_e : doExpand1(uy, bC_p1),
  uz_e : doExpand1(uz, bC_p1),
  u_e : [ux_e, uy_e, uz_e],

  bx_e : doExpand1(bx, bC),
  by_e : doExpand1(by, bC),
  bz_e : doExpand1(bz, bC),
  b_e : [bx_e, by_e, bz_e],

  p_perp_div_b_e : doExpand1(p_perp_div_b, bC),
  p_force_e : doExpand1(p_force, bC),
  bb_grad_u_e : doExpand1(bb_grad_u, bC),
  div_b_e : doExpand1(div_b, bC),

  incr_cdim_F_0_c : 0,
  incr_cdim_G_1_c : 0,
  /* alpha_cdim = [v_parallel b_hat + u] */
  for cdir : 1 thru cdim do (
    alpha_cdim_c : calcInnerProdList(varsP, 1, bP, vTrans*b_e[cdir] + u_e[cdir]),
    expr : gcfac(float(expand(radcan(alpha_cdim_c)))),

    /* Write out the component of alpha_cdim's phase space expansion */
    for i : 1 thru NP do (
      if expr[i] # 0.0 then printf(fh, "  ~a = ~a; ~%", alpha_cdim[NP*(cdir-1)+i-1], expr[i]*dx11[cdir])
    ),
    printf(fh, "~%"),
    
    /* zero out components of alpha which are empty */
    alpha_cdim_NoZero   : doMakeExprLstOff(alpha_cdim_c, alpha_cdim, a0),
    alpha_cdim_NoZero_e : doExpand(alpha_cdim_NoZero, bP),

    /* Volume increment from configuration space. */
    incr_cdim_F_0_c : incr_cdim_F_0_c+subst(a0=NP*(cdir-1),calcInnerProdList(varsP, 1, diff(bP,varsP[cdir]), alpha_cdim_NoZero_e*F_0_e)),
    incr_cdim_G_1_c : incr_cdim_G_1_c+subst(a0=NP*(cdir-1),calcInnerProdList(varsP, 1, diff(bP,varsP[cdir]), alpha_cdim_NoZero_e*G_1_e))
  ),

  /* alpha_v = [1/rho (div(p_par b) - p_perp div(b)) - v_par bb : grad(u)]
     alpha_v consolidated as alpha_vdim = p_force - v_par bb_grad_u (pressure forces and bb_grad_u pre-computed) */
  alpha_vdim_c : calcInnerProdList(varsP, 1, bP, p_force_e - vTrans*bb_grad_u_e),

  clst : [dv1par, dvpar, wvpar],
  writeCExprsCollect1(alpha_vdim, alpha_vdim_c*dv1par, clst),
  printf(fh, "~%"),

  /* Zero out components of alpha_vdim for F_0 and G_1 which are empty. */
  alpha_vdim_NoZero : doMakeExprLst(alpha_vdim_c, alpha_vdim),
  alpha_vdim_NoZero_e : doExpand(alpha_vdim_NoZero, bP),

  /* Volume increment from configuration space. */
  incr_vdim_F_0_c : calcInnerProdList(varsP, 1, diff(bP,varsP[cdim+1]), alpha_vdim_NoZero_e*F_0_e),
  incr_vdim_G_1_c : calcInnerProdList(varsP, 1, diff(bP,varsP[cdim+1]), alpha_vdim_NoZero_e*G_1_e),

  /* additional forces and sources on F_0 and T_perp/m G_1
     additional force on F_0 -> div(b) T_perp/m G_1, 
     additional force on G_1 -> div(b) 2.0*T_perp/m*(2.0*T_perp/m*G_1 + T_perp/m*(F_2 - F_0))] = div(b) g_dist_source */
  /* First project div(b) onto phase basis */
  alpha_div_b_c : calcInnerProdList(varsP, 1, bP, div_b_e),

  writeCExprsCollect1(alpha_div_b, alpha_div_b_c*dv1par, clst),
  printf(fh, "~%"),

  /* Zero out components of alpha_div_b which are empty. */
  alpha_div_b_NoZero : doMakeExprLst(alpha_div_b_c, alpha_div_b),
  alpha_div_b_NoZero_e : doExpand(alpha_div_b_NoZero, bP),

  /* addition force on F_0 = d/dvpar (div(b) T_perp/m G) */
  incr_F_0_div_b_c : calcInnerProdList(varsP, 1, diff(bP,varsP[cdim+1]), alpha_div_b_NoZero_e*F_0_source_e),

  /* additional force on G_1 = d/dvpar (div(b) * 2.0*T_perp/m*(2.0*T_perp/m*G_1 + T_perp/m*(F_2 - F_0))) 
     additional force on G_1 consolidated as = d/dvpar (div(b)*g_dist_source) */
  incr_G_1_div_b_c : calcInnerProdList(varsP, 1, diff(bP,varsP[cdim+1]), alpha_div_b_NoZero_e*G_1_source_e),

  /* evaluate additional force p_perp_div_b = (p_perp/rho*div(b)) at cflPt for further 
     time step restriction. Note: other time-step restrictions computed in surface kernels */
  amid_div_b : gcfac(float(expand(subst(cflPt,0.5*p_perp_div_b_e)))),
  printf(fh, "  cflFreq_mid += ~a*fabs(~a)*dv1par; ~%", float(2*polyOrder+1), amid_div_b),
  printf(fh, "~%"),

  writeCExprs1(incr_cdim_F_0, incr_cdim_F_0_c), 
  printf(fh, "~%"),
  /* Zero out components of cdim increment of F_0 which are empty. */
  incr_cdim_F_0_NoZero : doMakeExprLst(incr_cdim_F_0_c, incr_cdim_F_0),
  incr_cdim_F_0_NoZero_e : doExpand(incr_cdim_F_0_NoZero, bP),
  incr_cdim_F_0_NoZero_c : calcInnerProdList(varsP, 1, bP, incr_cdim_F_0_NoZero_e), 

  writeCExprs1(incr_vdim_F_0, incr_vdim_F_0_c), 
  printf(fh, "~%"),
  /* Zero out components of vdim increment of F_0 which are empty. */
  incr_vdim_F_0_NoZero : doMakeExprLst(incr_vdim_F_0_c, incr_vdim_F_0),
  incr_vdim_F_0_NoZero_e : doExpand(incr_vdim_F_0_NoZero, bP),
  incr_vdim_F_0_NoZero_c : calcInnerProdList(varsP, 1, bP, incr_vdim_F_0_NoZero_e), 

  writeCExprs1(incr_F_0_div_b, incr_F_0_div_b_c), 
  printf(fh, "~%"),
  /* Zero out components of div(b) increment of F_0 which are empty. */
  incr_F_0_div_b_NoZero : doMakeExprLst(incr_F_0_div_b_c, incr_F_0_div_b),
  incr_F_0_div_b_NoZero_e : doExpand(incr_F_0_div_b_NoZero, bP),
  incr_F_0_div_b_NoZero_c : calcInnerProdList(varsP, 1, bP, incr_F_0_div_b_NoZero_e), 

  writeCExprs1(incr_cdim_G_1, incr_cdim_G_1_c), 
  printf(fh, "~%"),
  /* Zero out components of cdim increment of G_1 which are empty. */
  incr_cdim_G_1_NoZero : doMakeExprLst(incr_cdim_G_1_c, incr_cdim_G_1),
  incr_cdim_G_1_NoZero_e : doExpand(incr_cdim_G_1_NoZero, bP),
  incr_cdim_G_1_NoZero_c : calcInnerProdList(varsP, 1, bP, incr_cdim_G_1_NoZero_e), 

  writeCExprs1(incr_vdim_G_1, incr_vdim_G_1_c), 
  printf(fh, "~%"),
  /* Zero out components of vdim increment of G_1 which are empty. */
  incr_vdim_G_1_NoZero : doMakeExprLst(incr_vdim_G_1_c, incr_vdim_G_1),
  incr_vdim_G_1_NoZero_e : doExpand(incr_vdim_G_1_NoZero, bP),
  incr_vdim_G_1_NoZero_c : calcInnerProdList(varsP, 1, bP, incr_vdim_G_1_NoZero_e), 

  writeCExprs1(incr_G_1_div_b, incr_G_1_div_b_c), 
  printf(fh, "~%"),
  /* Zero out components of div(b) increment of G_1 which are empty. */
  incr_G_1_div_b_NoZero : doMakeExprLst(incr_G_1_div_b_c, incr_G_1_div_b),
  incr_G_1_div_b_NoZero_e : doExpand(incr_G_1_div_b_NoZero, bP),
  incr_G_1_div_b_NoZero_c : calcInnerProdList(varsP, 1, bP, incr_G_1_div_b_NoZero_e), 

  /* Final source increment already computed (vperp characteristics) so we can just add to volume increment. */
  incr_source_G_1 : calcInnerProdList(varsP, 1, bP, G_1_vperp_e),

  writeCIncrExprs1(out_F_0, incr_cdim_F_0_NoZero_c + incr_vdim_F_0_NoZero_c + incr_F_0_div_b_NoZero_c),
  writeCIncrExprs1(out_G_1, incr_cdim_G_1_NoZero_c + incr_vdim_G_1_NoZero_c + incr_G_1_div_b_NoZero_c + incr_source_G_1),

  printf(fh, "~%"),
  printf(fh, "  return cflFreq_mid; ~%"),
  printf(fh, "} ~%")
)$
