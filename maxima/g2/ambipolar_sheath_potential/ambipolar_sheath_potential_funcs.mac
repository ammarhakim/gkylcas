/*
  Functions to generate kernels that compute the sheath potential, the
  sheath entrance electron density, and the potential in the whole domain.
  We assume ambipolar fluxes and adiabaticity of the electrons.
*/

load("out-scripts")$
load("modal-basis")$
load("nodalOperations/quadratureFuncs.mac")$

doMakeExprLst(vals, S) := makelist(if vals[i] # 0 then S[i-1] else 0, i, 1, length(vals))$

loadBasisX(dimTot, basisType, pOrder) := block([basis,vars],
  /* Load a basis based on dimensionality and basis type alone. */
  kill(varsC, varsP, basisC, basisP),
  if (dimTot < 4) then (
    load(sconcat("basis-precalc/basis", basisType, dimTot, "x"))
  ) else (
    load(sconcat("basis-precalc/basis", basisType, dimTot-3, "x", 3, "v"))
  ),

  if (dimTot < 4) then (
    basis : basisC[pOrder],
    vars  : varsC
  ) else (
    basis : basisP[pOrder],
    vars  : varsP
  ),
  return([basis,vars])
)$

genSheathEvGaussQuad(fh, funcNm, dim, basisNm, polyOrder) := block(
  [],

  numQuad : polyOrder+1, /* Number of quarature points in 1D. */

  [basis, vars] : loadBasisX(dim, basisNm, polyOrder),
  sheathDir : dim,  /* Assume the last dimension is the sheath direction. */
  sheathVar : vars[sheathDir],
  sheathSurfVars : delete(sheathVar, vars),

  /* Load a basis of one fewer dimension for projecting onto sheath surface. */
  if dim>1 then (
    [basisLowD, varsLowD] : loadBasisX(dim-1, basisNm, polyOrder),
    subList   : makelist(varsLowD[i]=sheathSurfVars[i],i,1,dim-1),
    varsLowD  : psubst(subList, varsLowD),
    basisLowD : psubst(subList, basisLowD)
  ) else (
    varsLowD : [x],  basisLowD : [1/innerProd(varsLowD,1,1,1)]
  ),
  numBLowD : length(basisLowD),

  [basis, vars] : loadBasisX(dim, basisNm, polyOrder), /* Again because of the kill in loadBasisX. */
  numB : length(basis),

  /* Get the Gaussian quadrature weights and ordinates (in [-1,1] space) for
     quadrature in one dimension lower. */
  if dim>1 then (
    ordWeight : gaussOrdWeight(numQuad,dim-1),
    normOrds  : float(ordWeight[1]),
    weights   : float(ordWeight[2]),
    ordNum    : length(normOrds)
  ),

  boundaryStr : ["lower","upper"],
  ghostEvSign : [1, -1],  skinEvSign  : [-1, 1],
  
  for bS : 1 thru 2 do ( /* One kernel for each of lower and upper boundaries. */
    printf(fh,sconcat("void ",funcNm,"(const double q_e, const double m_e, const double *Gamma_i, const double *m0Elc, const double *vtSqElc, double *m0ElcS, double *phiS) ~%{ ~%"), boundaryStr[bS]),
    printf(fh,"  // q_e: electron change.~%"),
    printf(fh,"  // m_e: electron mass.~%"),
    printf(fh,"  // Gamma_i: ion particle flux through sheath entrance.~%"),
    printf(fh,"  // m0Elc: electron density.~%"),
    printf(fh,"  // vtSqElc: electron thermal speed squared.~%"),
    printf(fh,"  // m0ElcS: electron density at the sheath entrance.~%"),
    printf(fh,"  // phiS: electrostatic sheath potential.~%"),
    printf(fh,"~%"),
  
    GammaIon_e : doExpand1(Gamma_i, basis),     /* Particle flux expanded in basis. */
    /* Evaluate the (ghost cell) flux at the boundary surface. */
    GammaIonB_c : calcInnerProdList(varsLowD,1,basisLowD,subst(sheathVar=ghostEvSign[bS], GammaIon_e)),
    GammaIonB_noZero_c : doMakeExprLst(GammaIonB_c, GammaIonB), 
    GammaIonB_e : doExpand(GammaIonB_noZero_c,basisLowD),
    printf(fh,"  double GammaIonB[~a];~%", numBLowD),
    writeCExprs1(GammaIonB, GammaIonB_c),

    m0Elc_e : doExpand1(m0Elc, basis),  vtSqElc_e : doExpand1(vtSqElc, basis),
    /* Evaluate the (skin cell) electron density and thermal speed squared at the boundary surface. */
    m0ElcB_c : calcInnerProdList(varsLowD,1,basisLowD,subst(sheathVar=skinEvSign[bS], m0Elc_e)),
    m0ElcB_noZero_c : doMakeExprLst(m0ElcB_c, m0ElcB), 
    m0ElcB_e : doExpand(m0ElcB_noZero_c,basisLowD),
    printf(fh,"  double m0ElcB[~a];~%", numBLowD),
    writeCExprs1(m0ElcB, m0ElcB_c),
    vtSqElcB_c : calcInnerProdList(varsLowD,1,basisLowD,subst(sheathVar=skinEvSign[bS], vtSqElc_e)),
    vtSqElcB_noZero_c : doMakeExprLst(vtSqElcB_c, vtSqElcB), 
    vtSqElcB_e : doExpand(vtSqElcB_c,basisLowD),
    printf(fh,"  double vtSqElcB[~a];~%", numBLowD),
    writeCExprs1(vtSqElcB, vtSqElcB_c),
    printf(fh,"~%"),

    /* Compute the sheath potential
         phiS = (m_e/q_e)*(v_te^2)*log( sqrt(2*pi)*Gamma_i/(n_e*sqrt(v_te^2)) )
       using quadrature. If dim=1 no quadrature is needed. */
    phiSlowD_c : makelist(0,i,1,numBLowD),
    if dim=1 then (
      phiSlowD_c : [(m_e/q_e)*vtSqElcB_e*log(sqrt(2*%pi)*GammaIonB_e/(m0ElcB_e*sqrt(vtSqElcB_e)))],
      phiSlowD_e : phiSlowD_c[1]
    ) else (
      m0ElcB_n    : makelist(0,i,1,ordNum),
      vtSqElcB_n  : makelist(0,i,1,ordNum),
      GammaIonB_n : makelist(0,i,1,ordNum),
      for i : 1 thru ordNum do (
        nOrd : normOrds[i],
        cSub : makelist(varsLowD[d]=normOrds[i][d],d,1,dim-1),

        m0ElcB_n[i]    : subst(cSub, m0ElcB_e),
        vtSqElcB_n[i]  : subst(cSub, vtSqElcB_e),
        GammaIonB_n[i] : subst(cSub, GammaIonB_e)
      ),
      printf(fh,"  double vtSqElcB_qp[~a];~%", ordNum),
      writeCExprs1(vtSqElcB_qp, vtSqElcB_n),
      printf(fh,"~%"),

      phiS_n : makelist(0,i,1,ordNum),
      for i : 1 thru ordNum do (
        phiS_n[i] : (m_e/q_e)*vtSqElcB_qp[i-1]*log(sqrt(2*%pi)*GammaIonB_n[i]/(m0ElcB_n[i]*sqrt(vtSqElcB_qp[i-1])))
      ),
      printf(fh,"  double phiS_qp[~a];~%", ordNum),
      writeCExprs1(phiS_qp, phiS_n),
      printf(fh,"~%"),

      phiSlowD_c : makelist(0,i,1,numB),
      for i : 1 thru ordNum do (
        nOrd : normOrds[i],
        /* Add contribution to each DG coefficient. */
        for k : 1 thru numBLowD do (
          phiSlowD_c[k] : phiSlowD_c[k]+weights[i]*subst(makelist(varsLowD[d]=nOrd[d],d,1,dim-1),basisLowD[k])*phi_qp[i-1]
        )
      ),
      writeCExprsNoExpand1(phiSlowD, phiSlowD_c),
      phiSlowD_e : doExpand(phiSlowD_c, basisLowD)
    ),
    printf(fh,"~%"),

    phiS_c : calcInnerProdList(vars, 1, basis, phiSlowD_e),
    writeCExprsNoExpand1(phiS, phiS_c),
    printf(fh,"~%"),

    m0ElcS_c : calcInnerProdList(vars, 1, basis, subst(sheathVar=skinEvSign[bS],m0Elc_e)),
    writeCExprsNoExpand1(m0ElcS, m0ElcS_c),

    printf(fh, "}~%"),
    printf(fh, "~%")
  )
)$ 

