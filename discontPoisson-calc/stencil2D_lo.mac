kill(all)$
load("recovery-calc/recovery")$

dirs : [x, y]$
size : [2, 2]$
range : [[-1,1], [-1,1]]$
basisNm : ["Ser", "Tensor"]$
J : [4/dx[1]^2, 4/dx[2]^2]$

for bIdx : 1 thru length(basisNm) do (
  load(sconcat("basis-precalc/basis", basisNm[bIdx], "2x")),
  for d : 1 thru length(dirs) do (
    for polyOrder : 1 thru 3 do (
      fname : sconcat("~/max-out/discontPoisson", basisNm[bIdx], "Stencil2D_", polyOrder, "p_", dirs[d], "Lo.lua"),
      fh : openw(fname),
      printf(fh, "local Lin = require \"Lib.Linalg\"~%"),
      printf(fh, "local function stencilFn(dx, a, b, val)~%"),
      printf(fh, "  local _M = {}~%~%"),
      
      numBasis : length(basisC[polyOrder]),
      st : diff(calcRecov3CellGen(basisNm[bIdx], dirs[d], size, polyOrder, 1, val, hC, hU, [a,b],[0,0]), dirs[d], 2)*J[d],
      stProj : calcInnerProdListGen(dirs, range, 1, basisC[polyOrder], st),
      
      ML : coefmatrix(stProj, [val]),
      printf(fh, "  _M[1] = Lin.Vec(~a)~%", numBasis),
      for i : 1 thru numBasis do (
        printf(fh, "  _M[1][~a] = ~a*val~%", i, float(ML[i][1]))
        ),
      
      MC : coefmatrix(stProj, makelist(hC[i], i, 1, numBasis)),
      printf(fh, "  _M[2] = Lin.Mat(~a,~a)~%", numBasis, numBasis),
      for i : 1 thru numBasis do (
        for j : 1 thru numBasis do (
          printf(fh, "  _M[2][~a][~a] = ~a~%", i, j, float(MC[i][j]))
          )
        ),
      
      MU : coefmatrix(stProj, makelist(hU[i], i, 1, numBasis)),
      printf(fh, "  _M[3] = Lin.Mat(~a,~a)~%", numBasis, numBasis),
      for i : 1 thru numBasis do (
        for j : 1 thru numBasis do (
          printf(fh, "  _M[3][~a][~a] = ~a~%", i, j, float(MU[i][j]))
          )
        ),
      
      printf(fh, "  return(_M)~%"),
      printf(fh, "end~%~%"),
      printf(fh, "return(stencilFn)"),
      close(fh)
      )
    )
  )$